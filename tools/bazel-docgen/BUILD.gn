# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/go/golden_go_proto.gni")

if (is_host) {
  golden_go_proto("stardoc_output") {
    sources = [ "third_party/stardoc/stardoc_output.proto" ]
  }

  go_library("proto_lib") {
    source_dir = "third_party/stardoc"
    sources = [ "stardoc_output.pb.go" ]
    deps = [ "//third_party/golibs:google.golang.org/protobuf" ]
    non_go_deps = [ ":stardoc_output" ]
  }

  go_library("main") {
    source_dir = "cmd/bazel-docgen"
    sources = [
      "main.go",
      "main_test.go",
    ]

    deps = [
      ":proto_lib",
      "//third_party/golibs:google.golang.org/protobuf",
    ]
  }

  go_test("main_tests") {
    library = ":main"
  }

  go_binary("bin") {
    output_name = "bazel-docgen"
    library = ":main"
  }
}

group("tests") {
  testonly = true
  deps = [ ":main_tests($host_toolchain)" ]
}

install_host_tools("bazel-docgen") {
  deps = [ ":bin($host_toolchain)" ]
  outputs = [ "bazel-docgen" ]
}
