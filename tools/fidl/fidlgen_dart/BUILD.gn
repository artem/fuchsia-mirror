# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/host.gni")
import("//build/sdk/sdk_host_tool.gni")
import("//build/testing/golden_files.gni")
import("//tools/fidl/fidlc/testdata/info.gni")

if (is_host) {
  go_library("codegen") {
    source_dir = "codegen"
    deps = [ "//tools/fidl/lib/fidlgen" ]
    sources = [
      "bits.tmpl",
      "const.tmpl",
      "enum.tmpl",
      "generator.go",
      "interface.tmpl",
      "ir.go",
      "library.tmpl",
      "struct.tmpl",
      "table.tmpl",
      "union.tmpl",
    ]
  }

  go_library("main") {
    deps = [
      ":codegen",
      "//tools/fidl/lib/fidlgen",
    ]
    sources = [ "main.go" ]
  }

  go_binary("fidlgen_dart") {
    library = ":main"
  }

  sdk_host_tool("fidlgen_dart_sdk") {
    category = "partner"
    output_name = "fidlgen_dart"
    deps = [ ":fidlgen_dart" ]
  }
}

install_host_tools("host") {
  deps = [ ":fidlgen_dart" ]
  outputs = [ "fidlgen_dart" ]
}
