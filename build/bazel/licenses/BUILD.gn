# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# These rules aggregate license information for all the GN-built bazel input resources
# into a licenses SPDX file to be used in bazel build too.
#
# The SPDX file is a bazel input resource as well.

import("//build/bazel/bazel_inputs.gni")
import("//build/bazel/legacy_ninja_build_outputs.gni")
import("//build/licenses/generated_licenses_spdx.gni")
import("//build/security.gni")
import("//tools/check-licenses/build/license_data.gni")

declare_args() {
  # Whether to generate real licenses for
  # `//build/bazel/licenses:legacy_ninja_build_outputs_licenses_spdx`.
  # License collection + generation for ninja targets is slow and expensive,
  # so it is turned off by default, and a placeholder is produced instead.
  generate_legacy_ninja_build_outputs_licenses_spdx = false

  # Enable hermetic GN licensing pipeline.
  # TODO(132725): Remove once migration completes.
  generate_legacy_ninja_build_outputs_licenses_spdx_from_metadata = false
}

# TODO(fxb/117740): We only need to cover bazel inputs that end up in the product,
# e.g. not tests or build tools. This can be achieved by adding metadata to
# the bazel inputs list (i.e. converting it into some dictionary) or by
# splitting into more granular lists.

_excluded_bazel_inputs = [
  # Remove a potential circular dependency on the spdx bazel_input_resource itself.
  "//build/bazel/licenses:legacy_ninja_build_outputs_licenses_spdx",

  # See fxb/126949#c15. Zedboot is only used in eng builds and is testonly, so can't
  # be used in license calculations.
  "//build/images/zedboot:zedboot.bazel_legacy_aib",

  # Bringup is testonly too.
  "//build/images/bringup:bringup.bazel_legacy_aib",

  # This is only used for `//tools/devshell:fx_publish_test`.
  "//build/images/updates/tests:fx_publish_test.gn_assembly.bazel_legacy_aib",
]

# Remove all non-user platform aibs
_excluded_bazel_inputs += eng_platform_aib_labels
_excluded_bazel_inputs += user_platform_aib_labels
_excluded_bazel_inputs -= user_platform_aib_labels

if (fuchsia_zbi_testonly) {
  # Remove fuchsia_zbi_testonly=true targets. See fxb/126949#c18 for why that is ok.
  _excluded_bazel_inputs +=
      [ "//build/images/fuchsia:fuchsia.bazel_legacy_aib" ]
}

group("all_bazel_inputs") {
  deps = gn_labels_for_bazel_inputs + extra_gn_labels_for_bazel_inputs

  deps += _excluded_bazel_inputs
  deps -= _excluded_bazel_inputs

  metadata = {
    # Prevent packages from leaking into the universe packages list in
    # //build/images/updates:universe_packages.list which uses
    # generate_package_metadata()
    package_names_barrier = []
  }
}

# Collects the license data for all GN inputs and makes it available for Bazel
# (1) This target is relatively slow.
# (2) When developing locally, may produce inaccurate results without 'fx clean'.
#     To avoid 'fx clean' do:
#     * fxbug.dev/84924 workaround: Manually delete the SPDX output :
#       `rm -rf out/default/obj/build/bazel/licenses/licenses_data/`
#     * fxbug.dev/117694 workaround: Manually delete cached depenedency calculation:
#       `rm out/default/project.json`
# TODO(132725): Remove once migration completes.
license_data("licenses_data") {
  target = "//build/bazel/licenses:all_bazel_inputs"  # Must be a fully
                                                      # qualified label.
  out_dir = "$target_out_dir/licenses_data"
  produce_spdx = true

  # This is an intermediate input to the licenses pipeline, no need to upload to GCS.
  emit_metadata = false

  # Analysis is not needed - it is assumed it is done elsewhere.
  run_analysis = false

  # These targets are not being distributed.
  prune_targets = [
    "//prebuilt/third_party/android/aemu/release/linux-x64",
    "//src/developer/ffx/plugins/emulator",
    "//vendor/third_party/eigen3",
    "//vendor/third_party/eigen3/",
  ]

  # Force build in preparation of fxr/924760.
  # Otherwise, license_data doesn't actually build "target". It only
  # searches for it in the generated project.json.
  deps = [ ":all_bazel_inputs" ]
}

if (generate_legacy_ninja_build_outputs_licenses_spdx) {
  if (generate_legacy_ninja_build_outputs_licenses_spdx_from_metadata) {
    generated_licenses_spdx("spdx_from_metadata") {
      target = ":all_bazel_inputs"
      spdx_root_package_name = "All Bazel Inputs"
      output = "${target_out_dir}/licenses_from_metadata.spdx.json"

      debug_hints = true

      # Current Bazel inputs don't need host tool licenses.
      include_host_tools = false

      ignore_collection_errors = true

      # Compare the output with the legacy spdx
      # TODO(132725): Remove once migration completes.
      deps = [ ":licenses_data" ]
      compare_with_legacy_spdx =
          "${target_out_dir}/licenses_data/results.spdx.json"

      ignore_comparison_errors = true
    }

    bazel_input_resource("legacy_ninja_build_outputs_licenses_spdx") {
      deps = [ ":spdx_from_metadata" ]
      sources = get_target_outputs(":spdx_from_metadata")
      outputs = [ "legacy_ninja_build_outputs_licenses.spdx.json" ]
    }
  } else {
    bazel_input_resource("legacy_ninja_build_outputs_licenses_spdx") {
      deps = [ ":licenses_data" ]
      sources = get_target_outputs(":licenses_data_spdx_copy")
      outputs = [ "legacy_ninja_build_outputs_licenses.spdx.json" ]
    }
  }
} else {
  # Use a placeholder instead.
  bazel_input_resource("legacy_ninja_build_outputs_licenses_spdx") {
    sources = [ "licenses_placeholder.spdx.json" ]
    outputs = [ "legacy_ninja_build_outputs_licenses.spdx.json" ]
  }
}
