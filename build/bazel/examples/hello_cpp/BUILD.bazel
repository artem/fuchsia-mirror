# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

load(
    "@fuchsia_sdk//fuchsia:defs.bzl",
    "fuchsia_cc_binary",
    "fuchsia_cc_test",
    "fuchsia_component",
    "fuchsia_component_manifest",
    "fuchsia_package",
    "fuchsia_unittest_package",
)

fuchsia_cc_binary(
    name = "hello_world",
    srcs = [
        "hello_world.cc",
    ],
    deps = select({
        # This is an example of how to conditionally include a depenency based
        # on the fuchsia API leve. This example is slightly convoluted because
        # the api level should really be exposed in the library this is used
        # to test that our packages pick up the correct deps when using different
        # api levels.
        "@fuchsia_sdk//fuchsia/constraints:api_level_17": [
            "//build/bazel/examples/hello_cpp/lib:hello_printer_17",
        ],
        "//conditions:default": [],
    }) + [
        "@fuchsia_sdk//pkg/fdio",
        "@fuchsia_sdk//pkg/syslog",
        # the fidl libraries below are not effectively used in the hello world sample, they are here just to validate that
        # the Bazel build can include FIDL dependencies from both internal and partner SDK correctly:
        "@fuchsia_sdk//fidl/fuchsia.component.config:fuchsia.component.config_cc",
    ],
)

fuchsia_component_manifest(
    name = "manifest",
    src = "meta/hello_world.cml",
)

fuchsia_component(
    name = "component",
    manifest = ":manifest",
    deps = [":hello_world"],
)

fuchsia_package(
    name = "pkg",
    package_name = "hello_cpp",
    components = [
        ":component",
    ],
    fuchsia_api_level = "HEAD",
    package_repository_name = "fuchsia.com",
    visibility = ["//visibility:public"],
)

fuchsia_cc_test(
    name = "hello_test",
    size = "small",
    srcs = ["hello_test.cc"],
    deps = [
        "@fuchsia_sdk//pkg/fdio",
        "@fuchsia_sdk//pkg/syslog",
    ],
)

fuchsia_cc_test(
    name = "hello_gtest",
    size = "small",
    srcs = ["hello_gtest.cc"],
    death_unittest = True,
    deps = [
        "@com_google_googletest//:gtest_main",
        "@fuchsia_sdk//pkg/fdio",
        "@fuchsia_sdk//pkg/syslog",
    ],
)

fuchsia_unittest_package(
    name = "test_pkg",
    package_name = "hello_cpp_test",
    fuchsia_api_level = "HEAD",
    unit_tests = [
        ":hello_test",
        ":hello_gtest",
    ],
    visibility = ["//visibility:public"],
)
