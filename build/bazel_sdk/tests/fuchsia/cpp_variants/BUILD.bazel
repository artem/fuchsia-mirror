# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

load(":fuchsia_sdk_cc_source_library_test.bzl", "fuchsia_sdk_cc_source_library_test")
load("@fuchsia_sdk//:api_version.bzl", "DEFAULT_TARGET_API", "VALID_TARGET_APIS")

package(default_visibility = ["//visibility:public"])

CC_VERSIONS = [
    "17",
    "20",
]

COPTS = [
    [],
    ["-Wshadow"],
]

# TODO(b/318701414): API 11 tests fail on an unknown identifier "QueryCallback".
# Reorganize the tests to better recognize API version incompatibilities.
# Consider attempting to compile the tests and verify the compiliation fails.
NUM_APIS_IN_TEST = 6

OLDEST_API = DEFAULT_TARGET_API - NUM_APIS_IN_TEST

common_ignored_deps = [
    # error: redefinition of 'TreeServerSendPreference'
    # conflicts with '//pkg/inspect_component_cpp'
    "//pkg/inspect_service_cpp",

    # error: no type named 'ValueSpec' in namespace 'fuchsia::component::config'
    "//pkg/sys_component_cpp_testing",

    # error: declaration shadows a local variable [-Werror,-Wshadow].
    # //pkg/async_patterns_cpp/internal/task_queue.cc declares two functions
    # with the name "guard".
    # "-Wshadow" is enabled by default, so this needs to be skipped for all tests.
    "//pkg/async_patterns_cpp",
    "//pkg/async_patterns_testing_cpp",
]

all_targets = [
    fuchsia_sdk_cc_source_library_test(
        api_level = entry.api_level,
        cc_version = cc_version,
        copts = copts,
        ignored_deps = common_ignored_deps,
    )
    for cc_version in CC_VERSIONS
    for entry in VALID_TARGET_APIS[:-1]
    if entry.api_level.isdigit() and int(entry.api_level) > OLDEST_API
    for copts in COPTS
]

test_suite(
    name = "tests",
    tests = all_targets,
)
