# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/assembled_system.gni")
import("//build/board.gni")
import("//build/images/args.gni")
import("//build/testing/host_test_data.gni")
import("//src/sys/core/build/core.gni")

assert(images_config_label != false,
       "images_config_label must be defined for the build")

core_realm_definition("core") {
  testonly = true
}

assembled_system("overnet") {
  testonly = true
  assembly_images_config_label = images_config_label
  product_assembly_config_label = "//build/assembly:default_product_config"
  use_bringup_platform_bundles_only = true

  generate_fvm = !fxfs_blob
  generate_fxfs = fxfs_blob

  generate_fvm_fastboot = assembly_generate_fvm_fastboot
  generate_fvm_nand = assembly_generate_fvm_nand
  generate_vbmeta = use_vbmeta

  base_packages = [
    "//src/connectivity/overnet/overnetstack",
    "//src/connectivity/overnet/tools/debug-serial",
    "//src/sys/pkg/bin/system-updater",
    "//src/sys/pkg/bin/system-update-checker",
    "//src/sys/pkg/bin/system-update-committer",
  ]
  bootfs_labels = [
    "//bundles/drivers:bootstrap",
    "//bundles/drivers:usb-host-stack",
    "//bundles/drivers:usb-peripheral-stack",
  ]
  bootfs_labels += [
    "//src/sys/component_manager:component_manager_bootfs_config",
    "//src/power/power-manager:base_config",
  ]

  core_realm_definition = ":core"

  ramdisk_in_zbi = true
  include_component_id_index = true
}

if (is_host) {
  host_test_data("test_data") {
    out_dir = get_label_info(":anything($target_toolchain)", "target_out_dir")
    sources = [ "${out_dir}/overnet.zbi" ]
    deps = [ "//build/images/overnet($target_toolchain)" ]
  }
}
