# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("cpp") {
  testonly = true
  deps = [ ":archive_reader" ]
}

group("tests") {
  testonly = true
  deps = [
    ":diagnostics_reader_cpp_tests",
    "tests",
  ]
}

source_set("archive_reader") {
  sources = [
    "archive_reader.cc",
    "archive_reader.h",
    "constants.h",
    "inspect.cc",
    "inspect.h",
    "logs.cc",
    "logs.h",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_hlcpp",
    "//third_party/rapidjson",
    "//zircon/system/ulib/inspect",
  ]

  deps = [
    "//src/lib/fsl",
    "//src/lib/fxl",
  ]
}

executable("archive_reader_test") {
  testonly = true
  output_name = "archive_reader_test"
  sources = [ "archive_reader_unittest.cc" ]
  deps = [
    ":archive_reader",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_component("archive_reader_unittest") {
  deps = [ ":archive_reader_test" ]
}

fuchsia_test_package("diagnostics_reader_cpp_tests") {
  test_components = [ ":archive_reader_unittest" ]
}
