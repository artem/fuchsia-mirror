# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD_style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/cpp/sdk_source_set.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":metadata_test" ]
}

bind_library("fuchsia_driver_metadata_test_bind_library") {
  testonly = true
  source = "fuchsia.driver.metadata.test.bind"
  public_deps = [ "//src/devices/bind/fuchsia.test" ]
}

test("metadata-test") {
  testonly = true
  output_name = "metadata_test"
  sources = [ "metadata_test.cc" ]

  deps = [
    "fuchsia.hardware.test:fuchsia.hardware.test_cpp",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_cpp",
    "//sdk/lib/device-watcher/cpp",
    "//sdk/lib/driver/metadata/cpp",
    "//sdk/lib/driver_test_realm/realm_builder/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
  ]
}

fuchsia_unittest_package("metadata_test") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  deps = [
    ":metadata-test",
    "test_child:no_use_component",
    "test_child:use_component",
    "test_grandchild:component",
    "test_parent:expose_component",
    "test_parent:no_expose_component",
    "test_root:component",
  ]
}
