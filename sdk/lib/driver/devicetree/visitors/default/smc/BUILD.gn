# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/cpp/sdk_source_set.gni")
import("//build/devicetree/devicetree.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":smc-visitor-test" ]
  testonly = true
}

sdk_source_set("smc") {
  sdk_name = "devicetree-smc"
  category = "internal"
  include_base = "//sdk"

  sources = [ "smc.cc" ]
  public = [ "smc.h" ]
  deps = [ "//sdk/lib/driver/logging/cpp" ]

  public_deps = [
    "//sdk/lib/driver/devicetree/manager:headers",
    "//sdk/lib/driver/devicetree/visitors:property-parser",
    "//zircon/system/ulib/zx",
  ]
}

test("smc-visitor-test-bin") {
  sources = [ "test/smc-test.cc" ]
  deps = [
    ":smc",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("smc-dtb") {
  sources = [ "test/dts/smc.dts.S" ]
}

resource("smc-dtb-file") {
  sources = get_target_outputs(":smc-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":smc-dtb" ]
}

fuchsia_unittest_package("smc-visitor-test") {
  component_name = "smc-visitor-test"
  deps = [
    ":smc-dtb-file",
    ":smc-visitor-test-bin",
  ]
}
