# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/cpp/sdk_source_set.gni")
import("//build/devicetree/devicetree.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":boot-metadata-visitor-test" ]
  testonly = true
}

sdk_source_set("boot-metadata") {
  sdk_name = "devicetree-boot-metadata"
  category = "internal"
  include_base = "//sdk"

  sources = [ "boot-metadata.cc" ]
  public = [ "boot-metadata.h" ]
  deps = [ "//sdk/lib/driver/logging/cpp" ]

  public_deps = [
    "//sdk/lib/driver/devicetree/manager:headers",
    "//sdk/lib/driver/devicetree/visitors:property-parser",
    "//zircon/system/ulib/zx",
  ]
}

test("boot-metadata-visitor-test-bin") {
  sources = [ "test/boot-metadata-test.cc" ]
  deps = [
    ":boot-metadata",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("boot-metadata-dtb") {
  sources = [ "test/dts/boot-metadata.dts.S" ]
}

resource("boot-metadata-dtb-file") {
  sources = get_target_outputs(":boot-metadata-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":boot-metadata-dtb" ]
}

fuchsia_unittest_package("boot-metadata-visitor-test") {
  component_name = "boot-metadata-visitor-test"
  deps = [
    ":boot-metadata-dtb-file",
    ":boot-metadata-visitor-test-bin",
  ]
}
