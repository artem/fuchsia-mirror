# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/devicetree/devicetree_visitor.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":ethernet-phy-visitor-test" ]
  testonly = true
}

devicetree_visitor("ethernet-phy-visitor") {
  deps = [ ":ethernet-phy-visitor-impl" ]
}

source_set("ethernet-phy-visitor-impl") {
  sources = [
    "ethernet-phy-visitor.cc",
    "ethernet-phy-visitor.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.ethernet.board:fuchsia.hardware.ethernet.board_bindlib_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/devicetree/manager",
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
  ]
  public_deps = [ "//sdk/lib/driver/devicetree/visitors:property-parser" ]
}

test("ethernet-phy-visitor-test-bin") {
  sources = [ "test/ethernet-phy-visitor-test.cc" ]
  deps = [
    ":ethernet-phy-visitor-impl",
    "//sdk/fidl/fuchsia.hardware.ethernet.board:fuchsia.hardware.ethernet.board_bindlib_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//sdk/lib/driver/devicetree/visitors/default/mmio",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("ethernet-phy-visitor-dtb") {
  sources = [ "test/dts/ethernet-phy.dts.S" ]
}

resource("ethernet-phy-visitor-dtb-file") {
  sources = get_target_outputs(":ethernet-phy-visitor-dtb")
  outputs = [ "test-data/ethernet-phy.dtb" ]
  deps = [ ":ethernet-phy-visitor-dtb" ]
}

fuchsia_unittest_package("ethernet-phy-visitor-test") {
  component_name = "ethernet-phy-visitor-test"
  deps = [
    ":ethernet-phy-visitor-dtb-file",
    ":ethernet-phy-visitor-test-bin",
  ]
}
