# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/devicetree/devicetree_visitor.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":sysmem-visitor-test" ]
  testonly = true
}

devicetree_visitor("sysmem-visitor") {
  deps = [ ":sysmem-visitor-impl" ]
}

source_set("sysmem-visitor-impl") {
  sources = [
    "sysmem-visitor.cc",
    "sysmem-visitor.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devicetree/manager",
    "//sdk/lib/driver/devicetree/visitors:driver-visitor",
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
  ]
}

test("sysmem-visitor-test-bin") {
  sources = [ "test/sysmem-visitor-test.cc" ]
  deps = [
    ":sysmem-visitor-impl",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_cpp",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("sysmem-dtb") {
  sources = [ "test/dts/sysmem.dts.S" ]
}

resource("sysmem-visitor-dtb-file") {
  sources = get_target_outputs(":sysmem-dtb")
  outputs = [ "test-data/sysmem.dtb" ]
  deps = [ ":sysmem-dtb" ]
}

fuchsia_unittest_package("sysmem-visitor-test") {
  component_name = "sysmem-visitor-test"
  deps = [
    ":sysmem-visitor-dtb-file",
    ":sysmem-visitor-test-bin",
  ]
}
