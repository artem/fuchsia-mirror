# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/devicetree/devicetree_visitor.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":pwm-init-visitor-test" ]
  testonly = true
}

devicetree_visitor("pwm-init-visitor") {
  deps = [ ":pwm-init-visitor-impl" ]
}

source_set("pwm-init-visitor-impl") {
  sources = [
    "pwm-init-visitor.cc",
    "pwm-init-visitor.h",
  ]
  deps = [
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devicetree/manager",
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.pwm:fuchsia.pwm_cpp",
  ]
}

test("pwm-init-visitor-test-bin") {
  sources = [ "test/pwm-init-visitor-test.cc" ]
  deps = [
    ":pwm-init-visitor-impl",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.pwm:fuchsia.pwm_cpp",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("pwm-init-dtb") {
  sources = [ "test/dts/pwm-init.dts" ]
}

resource("pwm-init-dtb-file") {
  sources = get_target_outputs(":pwm-init-dtb")
  outputs = [ "test-data/pwm-init.dtb" ]
  deps = [ ":pwm-init-dtb" ]
}

fuchsia_unittest_package("pwm-init-visitor-test") {
  component_name = "pwm-init-visitor-test"
  deps = [
    ":pwm-init-dtb-file",
    ":pwm-init-visitor-test-bin",
  ]
}
