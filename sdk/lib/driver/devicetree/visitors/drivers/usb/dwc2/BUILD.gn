# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/devicetree/devicetree_visitor.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":dwc2-visitor-test" ]
  testonly = true
}

devicetree_visitor("dwc2-visitor") {
  deps = [ ":dwc2-visitor-impl" ]
}

source_set("dwc2-visitor-impl") {
  sources = [
    "dwc2-visitor.cc",
    "dwc2-visitor.h",
  ]
  deps = [
    "//sdk/lib/driver/devicetree/manager",
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
    "//src/devices/usb/lib/usb:dwc2",
    "//src/lib/ddk:ddk_metadata",
  ]
  public_deps = [
    "//sdk/lib/driver/devicetree/visitors:driver-visitor",
    "//sdk/lib/driver/devicetree/visitors:property-parser",
  ]
}

test("dwc2-visitor-test-bin") {
  sources = [ "test/dwc2-visitor-test.cc" ]
  deps = [
    ":dwc2-visitor-impl",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/devices/usb/lib/usb:dwc2",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("dwc2-visitor-dtb") {
  sources = [ "test/dts/dwc2.dts.S" ]
}

resource("dwc2-visitor-dtb-file") {
  sources = get_target_outputs(":dwc2-visitor-dtb")
  outputs = [ "test-data/dwc2.dtb" ]
  deps = [ ":dwc2-visitor-dtb" ]
}

fuchsia_unittest_package("dwc2-visitor-test") {
  component_name = "dwc2-visitor-test"
  deps = [
    ":dwc2-visitor-dtb-file",
    ":dwc2-visitor-test-bin",
  ]
}
