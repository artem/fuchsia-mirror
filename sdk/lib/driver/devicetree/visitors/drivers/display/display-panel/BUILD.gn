# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/devicetree/devicetree_visitor.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":display-panel-visitor-test" ]
  testonly = true
}

devicetree_visitor("display-panel-visitor") {
  deps = [ ":display-panel-visitor-impl" ]
}

source_set("display-panel-visitor-impl") {
  sources = [
    "display-panel-visitor.cc",
    "display-panel-visitor.h",
  ]
  deps = [
    "//sdk/lib/driver/devicetree/manager",
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
    "//src/graphics/display/lib/device-protocol-display",
    "//src/lib/ddk:ddk_metadata",
  ]
  public_deps = [ "//sdk/lib/driver/devicetree/visitors:property-parser" ]
}

test("display-panel-visitor-test-bin") {
  sources = [ "test/display-panel-visitor-test.cc" ]
  deps = [
    ":display-panel-visitor-impl",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/graphics/display/lib/device-protocol-display",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("display-panel-dtb") {
  sources = [ "test/dts/display-panel.dts.S" ]
}

resource("display-panel-dtb-file") {
  sources = get_target_outputs(":display-panel-dtb")
  outputs = [ "test-data/display-panel.dtb" ]
  deps = [ ":display-panel-dtb" ]
}

fuchsia_unittest_package("display-panel-visitor-test") {
  component_name = "display-panel-visitor-test"
  deps = [
    ":display-panel-dtb-file",
    ":display-panel-visitor-test-bin",
  ]
}
