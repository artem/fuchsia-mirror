# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":sdmmc-visitor-test" ]
  testonly = true
}

devicetree_visitor("sdmmc-visitor") {
  deps = [ ":sdmmc" ]
}

source_set("sdmmc") {
  sources = [
    "sdmmc-visitor.cc",
    "sdmmc-visitor.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devicetree/manager",
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/lib/ddk:ddk_metadata",
  ]
  public_deps = [ "//sdk/lib/driver/devicetree/visitors:driver-visitor" ]
}

test("sdmmc-visitor-test-bin") {
  sources = [ "test/sdmmc-visitor-test.cc" ]
  deps = [
    ":sdmmc",
    "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_cpp",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.platform:fuchsia.platform_cpp",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("sdmmc-dtb") {
  sources = [ "test/dts/sdmmc.dts.S" ]
}

resource("sdmmc-dtb-file") {
  sources = get_target_outputs(":sdmmc-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":sdmmc-dtb" ]
}

fuchsia_unittest_package("sdmmc-visitor-test") {
  component_name = "sdmmc-visitor-test"
  deps = [
    ":sdmmc-dtb-file",
    ":sdmmc-visitor-test-bin",
  ]
}
