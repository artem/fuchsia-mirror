# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":arm-gicv2-visitor-test" ]
  testonly = true
}

source_set("arm-gicv2-visitor") {
  sources = [ "arm-gicv2-visitor.cc" ]
  deps = [
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
  ]
  public_deps = [
    "//sdk/lib/driver/devicetree/visitors:driver-visitor",
    "//sdk/lib/driver/devicetree/visitors:interrupt-parser",
    "//sdk/lib/driver/devicetree/visitors:reference-property",
  ]
}

test("arm-gicv2-visitor-test-bin") {
  sources = [ "test/arm-gicv2-test.cc" ]
  deps = [
    ":arm-gicv2-visitor",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("arm-gicv2-dtb") {
  sources = [ "test/dts/interrupts.dts.S" ]
}

resource("arm-gicv2-dtb-file") {
  sources = get_target_outputs(":arm-gicv2-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":arm-gicv2-dtb" ]
}

fuchsia_unittest_package("arm-gicv2-visitor-test") {
  component_name = "arm-gicv2-visitor-test"
  deps = [
    ":arm-gicv2-dtb-file",
    ":arm-gicv2-visitor-test-bin",
  ]
}
