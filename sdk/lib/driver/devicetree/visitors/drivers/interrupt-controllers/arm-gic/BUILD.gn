# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/devicetree/devicetree_visitor.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":arm-gic-visitor-test" ]
  testonly = true
}

devicetree_visitor("arm-gic") {
  deps = [ ":arm-gic-visitor" ]
}

source_set("arm-gic-visitor") {
  sources = [ "arm-gic-visitor.cc" ]
  deps = [
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
  ]
  public_deps = [
    "//sdk/lib/driver/devicetree/visitors:driver-visitor",
    "//sdk/lib/driver/devicetree/visitors:interrupt-parser",
    "//sdk/lib/driver/devicetree/visitors:property-parser",
  ]
}

test("arm-gic-visitor-test-bin") {
  sources = [ "test/arm-gic-test.cc" ]
  deps = [
    ":arm-gic-visitor",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("arm-gic-dtb") {
  sources = [ "test/dts/interrupts.dts.S" ]
}

resource("arm-gic-dtb-file") {
  sources = get_target_outputs(":arm-gic-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":arm-gic-dtb" ]
}

fuchsia_unittest_package("arm-gic-visitor-test") {
  component_name = "arm-gic-visitor-test"
  deps = [
    ":arm-gic-dtb-file",
    ":arm-gic-visitor-test-bin",
  ]
}
