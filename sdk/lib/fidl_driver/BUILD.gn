# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cpp/sdk_source_set.gni")

config("include") {
  include_dirs = [ "include" ]
}

sdk_source_set("fidl_driver_transport") {
  category = "partner"
  sdk_name = "fidl_driver_transport"
  stable = true
  public = [ "include/lib/fidl_driver/cpp/transport.h" ]
  sources = [ "transport.cc" ]
  public_configs = [ ":include" ]
  public_deps = [
    "//sdk/lib/driver/runtime:driver_runtime",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/fidl/cpp/wire",
  ]
}

sdk_source_set("fidl_driver") {
  category = "partner"
  sdk_name = "fidl_driver"
  stable = true
  public = [ "include/lib/fidl_driver/cpp/wire_types.h" ]
  public_configs = [ ":include" ]
  public_deps = [
    ":fidl_driver_transport",
    "//sdk/lib/driver/runtime:driver_runtime",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/fidl/cpp/wire",
  ]

  if (is_fuchsia) {
    public += [
      "include/lib/fidl_driver/cpp/internal/server_details.h",
      "include/lib/fidl_driver/cpp/internal/wire_client_details.h",
      "include/lib/fidl_driver/cpp/server.h",
      "include/lib/fidl_driver/cpp/sync_call.h",
      "include/lib/fidl_driver/cpp/unknown_interactions.h",
      "include/lib/fidl_driver/cpp/wire_client.h",
      "include/lib/fidl_driver/cpp/wire_messaging.h",
      "include/lib/fidl_driver/cpp/wire_messaging_declarations.h",
    ]
    sources = [ "unknown_interactions.cc" ]
  }
}

sdk_source_set("fidl_driver_natural") {
  category = "partner"
  sdk_name = "fidl_driver_natural"
  stable = true
  public = [
    "include/lib/fidl_driver/cpp/natural_ostream.h",
    "include/lib/fidl_driver/cpp/natural_types.h",
  ]
  public_configs = [ ":include" ]
  public_deps = [
    ":fidl_driver",
    "//sdk/lib/driver/runtime:driver_runtime",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/lib/fidl/cpp:cpp_base",
    "//src/lib/fidl/cpp:natural_ostream",
  ]

  if (is_fuchsia) {
    public += [
      "include/lib/fidl_driver/cpp/internal/endpoint_conversions.h",
      "include/lib/fidl_driver/cpp/internal/natural_client_details.h",
      "include/lib/fidl_driver/cpp/natural_client.h",
      "include/lib/fidl_driver/cpp/natural_messaging.h",
      "include/lib/fidl_driver/cpp/unified_messaging_declarations.h",
    ]
    sources = [ "natural_messaging.cc" ]
    public_deps += [ "//src/lib/fidl/cpp" ]
  }
}
