# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cpp/sdk_source_set.gni")

#  ________  _________  ________  ________
# |\   ____\|\___   ___\\   __  \|\   __  \
# \ \  \___|\|___ \  \_\ \  \|\  \ \  \|\  \
#  \ \_____  \   \ \  \ \ \  \\\  \ \   ____\
#   \|____|\  \   \ \  \ \ \  \\\  \ \  \___|
#     ____\_\  \   \ \__\ \ \_______\ \__\
#    |\_________\   \|__|  \|_______|\|__|
#    \|_________|
#
# This is an allowlist of targets that use the deprecated `inspect` C++ library which relies
# on the diagnostics directory (instead of InspectSink) as well as the deprecated HLCPP bindings.
#
# Developers are encouraged to migrate to use the `//sdk/lib/inspect/component/cpp` library instead.
#
# The policy at this time is:
# 1. Pre-existing use of this library in fuchsia.git is allowlisted.
# 2. Use of this library in new libraries and binaries is strongly discouraged and only allowed
#    under special circumstances under which the new library isn't covering the needs.
#
# To add items to the allowlist, please send a change to one of the OWNERS of
# this file to add an element to the visibility list below.
# Please allowlist entire directories rather than individual targets as it
# requires less allowlist churn over time.
#
# To regenerate:
# fx gn refs $(fx get-build-dir) //sdk/lib/sys/inspect/cpp //sdk/lib/sys/inspect/cpp:cpp | sed 's|\(.*:\).*|"\1*",|' | sort | uniq
inspect_hlcpp_allowlist = [
  # keep-sorted start
  "//build/sdk:*",
  "//sdk/lib/sys/inspect/cpp/tests:*",
  "//sdk/lib/sys/inspect/cpp:*",

  # keep-sorted end
]

sdk_source_set("cpp") {
  category = "partner"

  sdk_name = "sys_inspect_cpp"

  include_base = "//sdk"
  sources = [
    "component.cc",
    "component.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.inspect:fuchsia.inspect_hlcpp",
    "//sdk/lib/inspect/service/cpp",
    "//sdk/lib/sys/cpp",
    "//zircon/system/ulib/inspect",
  ]

  deps = [ "//sdk/lib/vfs/cpp" ]

  data_deps = [ "//sdk/lib/inspect:client_includes" ]

  public_configs = [ "//sdk/config" ]

  visibility = inspect_hlcpp_allowlist
}

group("tests") {
  testonly = true

  deps = [ "tests:sys_inspect_cpp_tests" ]
}
