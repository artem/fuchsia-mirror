# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("cpp") {
  testonly = true
  deps = [ ":read_visitor" ]
}

group("tests") {
  testonly = true
  deps = [ ":inspect_contrib_cpp_tests" ]
}

source_set("read_visitor") {
  sources = [ "read_visitor.h" ]

  public_deps = [
    "//sdk/lib/fit",
    "//sdk/lib/fit-promise",
    "//sdk/lib/stdcompat",
    "//zircon/system/ulib/inspect",
  ]
}

source_set("bounded_list_node") {
  sources = [
    "bounded_list_node.cc",
    "bounded_list_node.h",
  ]

  public_deps = [ "//zircon/system/ulib/inspect" ]
  public_configs = [ "//sdk/config" ]
}

executable("bounded_list_node_test") {
  testonly = true
  output_name = "bounded_list_node_test"
  sources = [ "bounded_list_node_unittest.cc" ]
  deps = [
    ":bounded_list_node",
    "//zircon/system/ulib/zxtest",
  ]
}

executable("read_visitor_test") {
  testonly = true
  output_name = "read_visitor_test"
  sources = [ "read_visitor_unittest.cc" ]
  deps = [
    ":read_visitor",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_component("read_visitor_unittest") {
  deps = [ ":read_visitor_test" ]
}

fuchsia_unittest_component("bounded_list_node_unittest") {
  deps = [ ":bounded_list_node_test" ]
}

fuchsia_test_package("inspect_contrib_cpp_tests") {
  test_components = [
    ":read_visitor_unittest",
    ":bounded_list_node_unittest",
  ]
}
