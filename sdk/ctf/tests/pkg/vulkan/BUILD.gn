# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//sdk/ctf/build/ctf.gni")
import("//src/lib/vulkan/vulkan.gni")

ctf_executable("vulkan-icd-load-bin") {
  testonly = true
  output_name = "vulkan_icd_load_test"
  sources = [ "icd_load.cc" ]
  deps = [
    "//src/lib/vulkan",
    "//zircon/system/ulib/zxtest",
  ]
}

ctf_fuchsia_component("vulkan-icd-load-test") {
  testonly = true
  deps = [
    ":vulkan-icd-load-bin",
    "//sdk/lib/vulkan",
    "//src/lib/vulkan/validation_layers",
  ]

  manifest = "meta/vulkan-icd-load-test.cml"
}

ctf_fuchsia_package("vulkan-loader-tests") {
  testonly = true
  package_name = "vulkan-loader-tests"
  deps = [ ":vulkan-icd-load-test" ]
}

fuchsia_test_component("vulkan-loader-test-root") {
  manifest = "meta/vulkan-loader-test-root.cml"
  test_type = "ctf"
}

fuchsia_test_package("vulkan-loader-tests-package") {
  test_components = [ ":vulkan-loader-test-root" ]
  subpackages = [ ":vulkan-loader-tests" ]
  test_specs = {
    environments = vulkan_envs
    log_settings = {
      max_severity = "WARN"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":vulkan-loader-tests-package",
    ":vulkan-loader-tests_archive",
  ]
}
