# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/generated_images_config.gni")
import("//build/images/vboot/vboot.gni")

#################################
# Fuchsia images configurations #
#################################

_default_zbi = {
  zbi = {
    name = "fuchsia"
    compression = "zstd"
  }
}

_default_vbmeta = {
  vbmeta = {
    key = "//third_party/android/platform/external/avb/test/data/testkey_atx_psk.pem"
    key_metadata =
        "//third_party/android/platform/external/avb/test/data/atx_metadata.bin"
  }
}

_vim3_vbmeta = {
  vbmeta = {
    key = "//src/firmware/avb_keys/vim3/vim3-dev-key/vim3_devkey_atx_psk.pem"
    key_metadata =
        "//src/firmware/avb_keys/vim3/vim3-dev-key/vim3_dev_atx_metadata.bin"
  }
}

_default_fvm = {
  fvm = {
    slice_size = 8388608
    blobfs = {
      layout = "compact"
    }
    reserved = {
      slices = 0
    }
  }
}

_default_fxfs = {
  fxfs = {
  }
}

_default_fvm_with_size_limits = {
  fvm = {
    slice_size = 8388608
    blobfs = {
      layout = "compact"
      maximum_contents_size = 5216665600
    }
    reserved = {
      slices = 0
    }
  }
}

generated_images_config("default") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  forward_variables_from(_default_fvm, "*")
}

generated_images_config("arm64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")
}

generated_images_config("riscv64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_fvm, "*")
}

generated_images_config("x64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")

  fvm.emmc = {
    # TODO(fxbug.dev/68692): finish implementing decompression in Gigaboot so we
    # can compress here to speed up FVM transmission.k
    compress = false

    # TODO(fxbug.dev/78185): don't hardcode the expected size, we should instead
    # inflate to whatever the FVM partition size is at runtime.
    #
    # For now just hardcode 16GiB which is the minimum FVM partition size defined
    # in the x64 paver.
    #
    # This also assumes we are not using raw NAND but that's a safer assumption.
    # In this case "emmc" is a misnomer, it really just means "not raw NAND".
    truncate_to_length = 17179869184
  }
}

generated_images_config("x64-fxfs") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  forward_variables_from(_default_fxfs, "*")
}

generated_images_config("qemu-x64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_fvm, "*")
}

generated_images_config("qemu-arm64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")
}

generated_images_config("qemu-riscv64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")
}

# NOTE: if you touch this target, please add this line to your commit
# description so that the vim3 tests are run as we do not run vim3 tests in CQ
# by default.
# Cq-Include-Trybots: luci.turquoise.global.try:bringup.vim3-debug,core.vim3-debug,core.vim3-vg-debug
generated_images_config("vim3") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_vim3_vbmeta, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")

  fvm.emmc = {
    compress = true

    # For VIM3, FVM partition uses all of the remaining eMMC. However, the total size of the eMMC
    # storage maybe 16G or 32G depending on whether it is a basic or pro version. In addition,
    # the actual size of the user block allocated by Fuchsia can be further different. (i.e. 'lsblk'
    # shows a 29G size user block for the 32Gb version). To avoid the risk of overflowing
    # available size, here we set it to be the same as sherlock (3280mb), which is clearly safe
    # and sufficient for now.
    truncate_to_length = 3439329280
  }
}

generated_images_config("chromebook-x64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  forward_variables_from(_default_fvm, "*")

  zbi.signing_script = vboot_action.script
  zbi.signing_args = vboot_action.args

  fvm.emmc = {
    compress = false

    # TODO(fxbug.dev/78185): don't hardcode the expected size, we should instead
    # inflate to whatever the FVM partition size is at runtime.
    #
    # For now just hardcode 16GiB which is the minimum FVM partition size defined
    # in the chromebook-x64 paver:
    # http://cs/fuchsia/src/storage/lib/paver/chromebook-x64.cc?l=124
    #
    # This also assumes we are not using raw NAND but that's a safer assumption.
    # In this case "emmc" is a misnomer, it really just means "not raw NAND".
    truncate_to_length = 17179869184
  }

  deps = vboot_action.deps
}

generated_images_config("imx8mmevk") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")

  zbi.signing_script =
      "//zircon/kernel/target/arm64/board/imx8mmevk/package-image.sh"
  deps = [ "//zircon/kernel/target/arm64/boot-shim:imx8mmevk" ]
}

#################################
# zedboot images configurations #
#################################

_default_zedboot_zbi = {
  zbi = {
    name = "zedboot"
    compression = "zstd"
  }
}

generated_images_config("zedboot_default") {
  forward_variables_from(_default_zedboot_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
}

generated_images_config("zedboot_vim3") {
  forward_variables_from(_default_zedboot_zbi, "*")
  forward_variables_from(_vim3_vbmeta, "*")
}

generated_images_config("zedboot_chromebook-x64") {
  forward_variables_from(_default_zedboot_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  zbi.signing_script = vboot_action.script
  zbi.signing_args = vboot_action.args
  deps = vboot_action.deps
}

generated_images_config("zedboot_qemu") {
  forward_variables_from(_default_zedboot_zbi, "*")
}

#################################
# bringup images configurations #
#################################

_default_bringup_zbi = {
  zbi = {
    name = "bringup"
    compression = "zstd"
  }
}

generated_images_config("bringup_default") {
  forward_variables_from(_default_bringup_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
}

generated_images_config("bringup_vim3") {
  forward_variables_from(_default_bringup_zbi, "*")
  forward_variables_from(_vim3_vbmeta, "*")
}

generated_images_config("bringup_chromebook-x64") {
  forward_variables_from(_default_bringup_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  zbi.signing_script = vboot_action.script
  zbi.signing_args = vboot_action.args
  deps = vboot_action.deps
}

generated_images_config("bringup_qemu") {
  forward_variables_from(_default_bringup_zbi, "*")
}

##########################################
# bringup fastboot images configurations #
##########################################

_bringup_fastboot_outdir =
    get_label_info("//build/images/bringup:bringup_fastboot", "target_out_dir")
_bringup_fastboot_outdir = rebase_path(_bringup_fastboot_outdir, root_build_dir)

_default_bringup_fastboot_zbi = {
  zbi = {
    name = "bringup_fastboot"
    compression = "zstd"
    signing_script = "//build/images/bringup/concat_zbi_and_vbmeta.sh"
    signing_args = []
  }
}

_bringup_fastboot_vbmeta_args = [
  "-v",
  "${_bringup_fastboot_outdir}/bringup_fastboot/bringup_fastboot.vbmeta",
]

generated_images_config("bringup_fastboot_default") {
  forward_variables_from(_default_bringup_fastboot_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  zbi.signing_args += _bringup_fastboot_vbmeta_args
}

generated_images_config("bringup_fastboot_vim3") {
  forward_variables_from(_default_bringup_fastboot_zbi, "*")
  forward_variables_from(_vim3_vbmeta, "*")
  zbi.signing_args += _bringup_fastboot_vbmeta_args
}

generated_images_config("bringup_fastboot_chromebook-x64") {
  forward_variables_from(_default_bringup_fastboot_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")

  _rebased_script = rebase_path(vboot_action.script, root_build_dir)
  zbi.signing_args += _bringup_fastboot_vbmeta_args
  zbi.signing_args += [ "-s ${_rebased_script}" ]
  zbi.signing_args += vboot_action.args
  deps = vboot_action.deps
}

generated_images_config("bringup_fastboot_qemu") {
  forward_variables_from(_default_bringup_fastboot_zbi, "*")
}

group("images") {
  public_deps = [
    # Fuchsia images configs
    ":arm64",
    ":chromebook-x64",
    ":default",
    ":qemu-x64",
    ":vim3",
    ":x64",
    ":x64-fxfs",

    # zedboot images configs
    ":zedboot_chromebook-x64",
    ":zedboot_default",
    ":zedboot_qemu",
    ":zedboot_vim3",

    # bringup images configs
    ":bringup_chromebook-x64",
    ":bringup_default",
    ":bringup_qemu",
    ":bringup_vim3",

    # bringup fastboot images configs
    ":bringup_fastboot_chromebook-x64",
    ":bringup_fastboot_default",
    ":bringup_fastboot_qemu",
    ":bringup_fastboot_vim3",
  ]
}
