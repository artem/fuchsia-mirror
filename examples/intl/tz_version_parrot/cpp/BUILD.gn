# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/lib/icu/third_party_icu_headers.gni")

group("cpp") {
  testonly = true
  deps = [ ":tz-version-parrot-cpp" ]
}

group("tests") {
  testonly = true
  deps = [ ":cpp" ]
}

# [START icudata_library]
third_party_icu_headers("icu_headers") {
  headers = [
    "third_party/icu/source/common/unicode/utypes.h",
    "third_party/icu/source/i18n/unicode/timezone.h",
  ]
}

executable("bin") {
  testonly = true
  output_name = "tz_version_parrot_cpp"

  sources = [ "test.cc" ]

  deps = [
    ":icu_headers",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//sdk/lib/syslog/cpp",
    "//src/lib/files",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/icu_data/cpp",
    "//third_party/googletest:gtest",
  ]
}

# [END icudata_library]

fuchsia_test_component("tz_version_parrot_cpp") {
  manifest = "meta/tz_version_parrot.cml"
  deps = [ ":bin" ]

  # Depends on `tzdata-icu-44-le` which is a system capability.
  test_type = "system"
}

fuchsia_test_package("tz-version-parrot-cpp") {
  test_components = [ ":tz_version_parrot_cpp" ]
  deps = [
    "//examples/intl/tz_version_parrot:tz_version_parrot_resources",

    # [START icudata_resource]
    "//src/intl:icudtl",

    # [END icudata_resource]
  ]
}
