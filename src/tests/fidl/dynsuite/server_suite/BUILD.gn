# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

runners = [
  "cpp",
  "cpp_wire",
  "go",
  "hlcpp",
  "rust",
]

fuchsia_test_component("test_component") {
  component_name = "test"
  manifest = "meta/test.cml"
}

foreach(runner, runners) {
  fuchsia_test_package("fidl_server_suite_${runner}_test") {
    subpackages = [ "//src/tests/fidl/dynsuite/server_suite/harness" ]
    renameable_subpackages = [
      {
        name = "server"
        package = "//src/tests/fidl/dynsuite/server_suite/runners/${runner}"
      },
    ]
    test_components = [ ":test_component" ]
  }
}

group("server_suite") {
  testonly = true

  deps = []
  foreach(runner, runners) {
    deps += [ ":fidl_server_suite_${runner}_test" ]
  }
}
