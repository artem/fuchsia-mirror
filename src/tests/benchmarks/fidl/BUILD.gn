# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/testing/perf/test.gni")

group("benchmarks-under-migration") {
  testonly = true
  deps = [
    "driver_cpp",
    "driver_llcpp",
    "fidlc:fidlc_microbenchmarks",
    "go",
    "hlcpp",
    "lib-fidl",
    "llcpp",
    "reference",
  ]
}

_tests = [
  "cpp",
  "rust",
]

_benchmarks = []
_test_packages = []

foreach(test, _tests) {
  _benchmark_name = "fidl_microbenchmarks_${test}"

  fuchsia_component_perf_test("${_benchmark_name}") {
    package = "${test}:fidl_microbenchmarks_${test}"
    component_name = "benchmark"
    expected_metric_names_filepath = "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.fidl_microbenchmarks.${test}.txt"

    if (test == "rust") {
      process_runs = 1
    } else {
      process_runs = 6
      results_path_test_arg = "--out"
      test_component_args = [
        "-p",
        "--quiet",
      ]
    }
  }

  _test_packages += [ "${test}:fidl_microbenchmarks_${test}" ]

  _benchmarks += [ ":${_benchmark_name}" ]
}

group("benchmarks") {
  testonly = true
  deps = [ "fidlc:benchmarks" ]
  deps += _benchmarks
}

group("tests") {
  testonly = true
  deps = _test_packages
}
