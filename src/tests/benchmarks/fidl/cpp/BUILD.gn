# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/testing/environments.gni")
import("//src/tests/benchmarks/fidl/benchmark_suite/benchmark_suite.gni")
import("//tools/fidl/gidl/gidl.gni")

gidl_deps = []
foreach(suite, benchmark_suites) {
  suite_target = "benchmark_suite__${suite.name}"
  gidl_deps += [ ":" + suite_target ]

  gidl_cpp_benchmarks(suite_target) {
    testonly = true
    inputs = suite.gidl
    fidl = suite.fidl_target
    deps = [
      "//sdk/ctf/tests/pkg/fidl/cpp:handle_util",
      "//sdk/lib/fidl",
      "//sdk/lib/fidl/cpp",
      "//zircon/system/ulib/async-loop:async-loop-cpp",
      "//zircon/system/ulib/async-loop:async-loop-default",
      "//zircon/system/ulib/perftest",
    ]
    extra_args = [
      "-cpp-benchmarks-fidl-library",
      suite.fidl_library_suffix,
    ]
  }
}

executable("bin") {
  output_name = "cpp_fidl_microbenchmarks"
  testonly = true
  sources = [ "main.cc" ]
  deps = [
           "//src/zircon/lib/zircon",
           "//zircon/system/ulib/async-loop:async-loop-cpp",
           "//zircon/system/ulib/async-loop:async-loop-default",
           "//zircon/system/ulib/perftest",
         ] + gidl_deps
}

fuchsia_package_with_single_component("fidl_microbenchmarks_cpp") {
  testonly = true
  manifest = "meta/benchmark.cml"
  component_name = "benchmark"
  deps = [ ":bin" ]
}
