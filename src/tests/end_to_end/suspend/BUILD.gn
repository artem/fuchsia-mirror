# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//build/testing/perf/test.gni")
import("//src/starnix/tests/build/starnix_linux_test_component.gni")
import("//src/starnix/tests/starnix_test_subpackages.gni")

group("suspend") {
  testonly = true
  public_deps = [
    ":power_suspend_test",
    ":power_suspend_test_fc($host_toolchain)",
  ]
}

executable("suspend_bin") {
  testonly = true
  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/lib/files",
    "//src/lib/fxl/test:gtest_main",
    "//src/tests/end_to_end/power:power_utils",
    "//third_party/googletest:gtest",
  ]
  sources = [ "main.cc" ]
}

starnix_linux_test_component("suspend_test") {
  test_label = ":suspend_bin"
  test_binary = "suspend_bin"
  test_type = "starnix"
}

fuchsia_test_package("power_suspend_test") {
  test_components = [ ":suspend_test" ]

  subpackages = starnix_test_subpackages +
                [ "//src/starnix/containers/debian:debian_package" ]
}

python_perf_test("power_suspend_test_fc") {
  main_source = "suspend_test.py"
  libraries = [
    "//src/performance/lib/trace_processing",
    "//src/testing/end_to_end/mobly_base_tests:fuchsia_power_base_test",
    "//src/testing/end_to_end/honeydew",
    "//src/tests/end_to_end/power:power_test_utils",
  ]

  expected_metric_names_filepaths = [ "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.power.suspend_cpu.txt" ]

  params_source = "suspend_test_params.yaml"
  transport = "fuchsia-controller"
}
