# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/testing/perf/test.gni")

python_perf_test("perf_publish_example") {
  main_source = "test/perf_publish_example.py"
  expected_metric_names_filepaths = [
    "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.example.txt",
  ]
  libraries = [
    "//src/testing/end_to_end/mobly_base_tests:fuchsia_base_test",
    "//src/performance/lib/perf_publish",
  ]
}

python_perf_test("perftest_trace_events_test") {
  main_source = "test/perftest_trace_events_test.py"
  expected_metric_names_filepaths = []
  libraries = [
    "//src/performance/lib/trace_processing",
    "//src/testing/end_to_end/mobly_base_tests:fuchsia_base_test",
  ]
}

python_perf_test("tracing_microbenchmarks_test") {
  main_source = "test/tracing_microbenchmarks_test.py"
  expected_metric_names_filepaths = [
    "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.microbenchmarks.tracing.txt",
    "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.microbenchmarks.tracing_categories_disabled.txt",
    "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.trace_records.rust.tracing_categories_disabled.txt",
    "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.trace_records.rust.tracing_disabled.txt",
  ]
  libraries = [
    "//src/testing/end_to_end/mobly_base_tests:fuchsia_base_test",
    "//src/performance/lib/perf_publish",
    "//src/performance/lib/py_test_utils:perf_test_utils",
    "//src/performance/lib/trace_processing",
  ]
  target_deps = [
    "//src/lib/trace/rust/bench",
    "//src/tests/microbenchmarks:fuchsia_microbenchmarks",
  ]
}

group("test") {
  testonly = true

  deps = [
    ":perf_publish_example($host_toolchain)",
    ":perftest_trace_events_test($host_toolchain)",
    ":tracing_microbenchmarks_test($host_toolchain)",
    "//src/connectivity/network:benchmarks",
    "//src/devices/bin/driver_runtime/microbenchmarks",
    "//src/diagnostics:benchmarks",
    "//src/graphics/benchmarks",
    "//src/lib/diagnostics:benchmarks",
    "//src/media/audio/audio_core/shared/mixer/tools:benchmarks",
    "//src/starnix:benchmarks",
    "//src/storage/benchmarks/fuchsia:benchmarks",
    "//src/storage/benchmarks/starnix:benchmarks",
    "//src/storage/benchmarks/verity:benchmarks",
    "//src/tests/benchmarks",
    "//src/tests/microbenchmarks:benchmarks",

    # Many tests use sl4f to communicate with the target.
    "//src/testing/sl4f",
  ]
}

group("tests_to_run_on_internal_builders") {
  testonly = true

  deps = [
    "//src/media/audio/audio_core/shared/mixer/tools:benchmarks",
    "//src/storage/benchmarks/fuchsia:benchmarks",
    "//src/tests/benchmarks:kernel_boot_benchmarks",
  ]
}
