# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("buttons-bind") {
  rules = "buttons.bind"
  bind_output = "buttons.bindbc"
  tests = "bind-tests.json"
  disable_fragment_gen = true
  deps = [
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

fuchsia_driver("buttons-driver") {
  output_name = "buttons"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = []
  deps = [ ":common" ]
}

fuchsia_driver_component("buttons") {
  visibility = [ ":*" ]
  component_name = "buttons"
  deps = [ ":buttons-driver" ]
  info = "buttons-info.json"
  manifest = "meta/buttons.cml"
}

fuchsia_driver_package("package") {
  package_name = "buttons"
  driver_components = [ ":buttons" ]
}

source_set("common") {
  configs += [ "//build/config:all_source" ]
  sources = [ "buttons.cc" ]

  public_deps = [
    ":buttons-bind",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
    "//sdk/fidl/fuchsia.input.report:fuchsia.input.report_cpp",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/sync:sync-cpp",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test("buttons-test") {
  configs += [ "//build/config:all_source" ]
  output_name = "buttons-test"
  sources = [ "buttons-test.cc" ]
  deps = [
    ":common",
    "//sdk/lib/async_patterns/testing/cpp",
    "//src/devices/gpio/testing/fake-gpio",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/mock-function",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("buttons-test-package") {
  package_name = "buttons-test"
  deps = [ ":buttons-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":buttons-bind_test",
    ":buttons-test-package",
  ]
}
