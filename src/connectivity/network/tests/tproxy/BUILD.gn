# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

test("bin") {
  output_name = "tproxy_test"
  sources = [
    "../os.h",
    "../socket/util.cc",
    "../socket/util.h",
    "main.cc",
  ]
  deps = [
    "//sdk/lib/component/incoming/cpp",
    "//src/connectivity/network/testing/netemul/sync-manager/fidl:sync_cpp",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/fbl",
  ]
  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_cpp",
      "//sdk/lib/fdio",
    ]
  }
}

fuchsia_test_component("test") {
  manifest = "meta/test.cml"
  deps = [ ":bin" ]
}

fuchsia_unittest_package("tproxy-test") {
  manifest = "meta/tproxy-test.cml"
  deps = [
    ":test",
    "filter-setup",
    "//src/connectivity/network/testing/netemul/configurable-netstack:configurable-netstack-netstack3",
    "//src/connectivity/network/testing/netemul/sync-manager",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":tproxy-test" ]
}
