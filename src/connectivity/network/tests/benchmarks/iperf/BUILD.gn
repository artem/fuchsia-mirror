# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/testing/host_test_data.gni")
import("//build/testing/perf/test.gni")

_test_configs = [
  {
    label = "localhost_tcp"
    protocol = "tcp"
    direction = "loopback"
  },
  {
    label = "localhost_udp"
    protocol = "udp"
    direction = "loopback"
  },
  {
    label = "ethernet_tcp_send"
    protocol = "tcp"
    direction = "send"
  },
  {
    label = "ethernet_tcp_recv"
    protocol = "tcp"
    direction = "recv"
  },
  {
    label = "ethernet_udp_send"
    protocol = "udp"
    direction = "send"
  },
  {
    label = "ethernet_udp_recv"
    protocol = "udp"
    direction = "recv"
  },
]

_benchmarks = []
foreach(test_config, _test_configs) {
  _test_label = "netstack_iperf_test_${test_config.label}"
  _runtime_deps_label = "${_test_label}_runtime_deps"
  _benchmarks += [ ":${_test_label}" ]

  if (is_host) {
    host_test_data(_runtime_deps_label) {
      _runtime_deps_dir = "${target_out_dir}/${_test_label}_test/runtime_deps"
      sources = [ "${root_out_dir}/iperf3" ]
      outputs = [ "${_runtime_deps_dir}/{{source_file_part}}" ]
      deps = [ "//third_party/iperf:bin" ]
    }
  }

  python_perf_test(_test_label) {
    main_source = "netstack_iperf_test.py"
    expected_metric_names_filepaths = [ "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.netstack.iperf_benchmarks.${test_config.label}.txt" ]
    libraries = [
      "//src/testing/end_to_end/mobly_base_tests:fuchsia_base_test",
      "//src/performance/lib/perf_publish",
      "//src/performance/lib/py_test_utils:perf_test_utils",
      "//src/performance/lib/trace_processing",
    ]
    target_deps = [ "//third_party/iperf:iperf3_pkg" ]

    params = test_config

    # TODO(https://fxbug.dev/42125198): Enable host build for non-linux hosts as well.
    if (host_os == "linux") {
      deps = [ ":${_runtime_deps_label}($host_toolchain)" ]
    }

    transport = "fuchsia-controller"
  }
}

group("benchmarks") {
  testonly = true
  deps = _benchmarks
}
