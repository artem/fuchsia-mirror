# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/python/python_mobly_test.gni")
import("//build/testing/perf/test.gni")

if (is_host) {
  python_mobly_test("netstack_benchmarks_with_tracing_test") {
    main_source = "test.py"
    libraries =
        [ "//src/testing/end_to_end/mobly_base_tests:fuchsia_base_test" ]
    transport = "fuchsia-controller"
    params = {
      skip_netstack2 = target_cpu == "riscv64"
    }
    environments = performance_testing_environments
  }
}

group("benchmarks") {
  testonly = true
  deps = [
    ":netstack_benchmarks_with_tracing_test($host_toolchain)",
    "//src/connectivity/network/tests/benchmarks/socket-loopback:loopback-socket-benchmarks-with-tracing-pkg-netstack3",
    "//src/connectivity/network/tests/benchmarks/socket-tun:tun-socket-benchmarks-tests-netstack3",
  ]

  # The Go runtime on Fuchsia does not support RISC-V.
  if (target_cpu != "riscv64") {
    deps += [
      "//src/connectivity/network/tests/benchmarks/socket-loopback:loopback-socket-benchmarks-with-tracing-pkg-netstack2",
      "//src/connectivity/network/tests/benchmarks/socket-tun:tun-socket-benchmarks-tests-netstack2",
    ]
  }
}
