# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//build/testing/perf/test.gni")

rustc_binary("tun_socket_benchmarks") {
  testonly = true
  edition = "2021"

  sources = [ "src/main.rs" ]

  deps = [
    "//sdk/fidl/fuchsia.hardware.network:fuchsia.hardware.network_rust",
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.net.interfaces.admin:fuchsia.net.interfaces.admin_rust",
    "//sdk/fidl/fuchsia.net.stack:fuchsia.net.stack_rust",
    "//sdk/fidl/fuchsia.net.tun:fuchsia.net.tun_rust",
    "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_rust",
    "//sdk/fidl/fuchsia.tracing.controller:fuchsia.tracing.controller_rust",
    "//src/connectivity/lib/net-declare",
    "//src/connectivity/lib/net-types",
    "//src/connectivity/network/testing/netemul/rust:lib",
    "//src/connectivity/network/tests/integration/common:netstack_testing_common",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/network/fidl_fuchsia_net_ext",
    "//src/lib/network/fidl_fuchsia_net_interfaces_ext",
    "//src/lib/trace/rust:trace",
    "//src/lib/trace/rust:trace-provider",
    "//src/performance/lib/fuchsiaperf",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:socket2",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

common_deps = [
  ":tun_socket_benchmarks",
  "//src/connectivity/network/testing/netemul/service:netemul-sandbox",
  "//src/connectivity/network/tun/network-tun:component",
  "//src/sys/stash:stash_secure_v2",
]

netstack3_deps = common_deps + [
                   "//src/connectivity/network/netstack3:component",

                   # TODO(https://fxbug.dev/42076541): remove this when the production Netstack3
                   # component no longer brings its own DHCPv4 client, given that it is
                   # otherwise unused in this benchmark.
                   "//src/connectivity/network/dhcpv4/client/bindings:component",
                 ]

fuchsia_test_component("tun-socket-benchmarks-netstack3") {
  deps = netstack3_deps
  manifest = "meta/tun-socket-benchmarks-ns3.cml"
}

fuchsia_test_component("tun-socket-benchmarks-netstack3-with-tracing") {
  deps = netstack3_deps
  manifest = "meta/tun-socket-benchmarks-ns3-with-tracing.cml"
  test_type = "system"
}

fuchsia_test_package("tun-socket-benchmarks-tests-netstack3") {
  test_components = [
    ":tun-socket-benchmarks-netstack3",
    ":tun-socket-benchmarks-netstack3-with-tracing",
  ]
}

fuchsia_component_perf_test("tun_socket_benchmarks_with_netstack3") {
  package = ":tun-socket-benchmarks-tests-netstack3"
  component_name = "tun-socket-benchmarks-netstack3"
  expected_metric_names_filepath = "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.network.socket.tun.netstack3.txt"
  test_component_args = [ "--output-fuchsiaperf" ]
}

# The Go runtime on Fuchsia does not support RISC-V.
if (target_cpu != "riscv64") {
  fuchsia_test_component("tun-socket-benchmarks-netstack2") {
    deps = common_deps + [ "//src/connectivity/network/netstack:component" ]
    manifest = "meta/tun-socket-benchmarks-ns2.cml"
  }

  fuchsia_test_component("tun-socket-benchmarks-netstack2-with-tracing") {
    deps = common_deps +
           [ "//src/connectivity/network/netstack:component-with-tracing" ]
    manifest = "meta/tun-socket-benchmarks-ns2-with-tracing.cml"
    test_type = "system"
  }

  fuchsia_test_package("tun-socket-benchmarks-tests-netstack2") {
    test_components = [
      ":tun-socket-benchmarks-netstack2",
      ":tun-socket-benchmarks-netstack2-with-tracing",
    ]
  }

  fuchsia_component_perf_test("tun_socket_benchmarks_test") {
    package = ":tun-socket-benchmarks-tests-netstack2"
    component_name = "tun-socket-benchmarks-netstack2"
    expected_metric_names_filepath = "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.network.socket.tun.txt"
    test_component_args = [ "--output-fuchsiaperf" ]
  }

  ns2_test_targets = [ ":tun-socket-benchmarks-tests-netstack2" ]
  ns2_benchmark_targets = [ ":tun_socket_benchmarks_test($host_toolchain)" ]
} else {
  ns2_test_targets = []
  ns2_benchmark_targets = []
}

all_test_targets =
    [ ":tun-socket-benchmarks-tests-netstack3" ] + ns2_test_targets
all_benchmark_targets =
    [ ":tun_socket_benchmarks_with_netstack3($host_toolchain)" ] +
    ns2_benchmark_targets

group("benchmarks") {
  testonly = true
  deps = all_benchmark_targets
}

group("tests") {
  testonly = true
  public_deps = all_test_targets
}
