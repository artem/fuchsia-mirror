# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

rustc_binary("tun_socket_benchmarks") {
  testonly = true
  edition = "2021"

  sources = [ "src/main.rs" ]

  deps = [
    "//sdk/fidl/fuchsia.hardware.network:fuchsia.hardware.network_rust",
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.net.interfaces.admin:fuchsia.net.interfaces.admin_rust",
    "//sdk/fidl/fuchsia.net.stack:fuchsia.net.stack_rust",
    "//sdk/fidl/fuchsia.net.tun:fuchsia.net.tun_rust",
    "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_rust",
    "//sdk/fidl/fuchsia.tracing.controller:fuchsia.tracing.controller_rust",
    "//src/connectivity/lib/net-declare",
    "//src/connectivity/lib/net-types",
    "//src/connectivity/network/testing/netemul/rust:lib",
    "//src/connectivity/network/tests/integration/common:netstack_testing_common",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/network/fidl_fuchsia_net_ext",
    "//src/lib/network/fidl_fuchsia_net_interfaces_ext",
    "//src/lib/trace/rust:trace",
    "//src/lib/trace/rust:trace-provider",
    "//src/performance/lib/fuchsiaperf",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:socket2",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

common_deps = [
  ":tun_socket_benchmarks",
  "//src/connectivity/network/testing/netemul/service:netemul-sandbox",
  "//src/connectivity/network/tun/network-tun:component",
  "//src/sys/stash:stash_secure_v2",
]

netstack3_deps = common_deps + [
                   "//src/connectivity/network/netstack3:component",

                   # TODO(https://fxbug.dev/125773): remove this when the production Netstack3
                   # component no longer brings its own DHCPv4 client, given that it is
                   # otherwise unused in this benchmark.
                   "//src/connectivity/network/dhcpv4/client/bindings:component",
                 ]

fuchsia_test_component("tun-socket-benchmarks-netstack3") {
  deps = netstack3_deps
  manifest = "meta/tun-socket-benchmarks-ns3.cml"
}

fuchsia_test_component("tun-socket-benchmarks-netstack3-with-tracing") {
  deps = netstack3_deps
  manifest = "meta/tun-socket-benchmarks-ns3-with-tracing.cml"
  test_type = "system"
}

fuchsia_test_package("tun-socket-benchmarks-tests-netstack3") {
  test_components = [
    ":tun-socket-benchmarks-netstack3",
    ":tun-socket-benchmarks-netstack3-with-tracing",
  ]
}

# The Go runtime on Fuchsia does not support RISC-V.
if (target_cpu != "riscv64") {
  fuchsia_test_component("tun-socket-benchmarks-netstack2") {
    deps = common_deps + [ "//src/connectivity/network/netstack:component" ]
    manifest = "meta/tun-socket-benchmarks-ns2.cml"
  }

  fuchsia_test_component("tun-socket-benchmarks-netstack2-with-tracing") {
    deps = common_deps +
           [ "//src/connectivity/network/netstack:component-with-tracing" ]
    manifest = "meta/tun-socket-benchmarks-ns2-with-tracing.cml"
    test_type = "system"
  }

  fuchsia_test_package("tun-socket-benchmarks-tests-netstack2") {
    test_components = [
      ":tun-socket-benchmarks-netstack2",
      ":tun-socket-benchmarks-netstack2-with-tracing",
    ]
  }

  ns2_test_targets = [ ":tun-socket-benchmarks-tests-netstack2" ]
} else {
  ns2_test_targets = []
}

all_test_targets =
    [ ":tun-socket-benchmarks-tests-netstack3" ] + ns2_test_targets

group("benchmarks") {
  testonly = true
  deps = all_test_targets
}

group("tests") {
  testonly = true
  public_deps = all_test_targets
}
