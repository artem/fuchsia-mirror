# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

fuchsia_test_component("test-component") {
  component_name = "test"
  manifest = "meta/test.cml"
  deps = [ ":test-bin" ]
}

rustc_library("delegated-provisioning-constants") {
  edition = "2021"
  name = "delegated_provisioning_constants"
  source_root = "src/constants.rs"
  sources = [ "src/constants.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//src/connectivity/lib/net-declare",
  ]
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

rustc_test("test-bin") {
  edition = "2021"
  name = "test_bin"
  deps = [
    ":delegated-provisioning-constants",
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.net.interfaces:fuchsia.net.interfaces_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/network/fidl_fuchsia_net_interfaces_ext",
    "//third_party/rust_crates:tracing",
  ]
  source_root = "src/test.rs"
  sources = [ "src/test.rs" ]
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_component("bootstrap-dhcpd-component") {
  component_name = "bootstrap-dhcpd"
  manifest = "meta/bootstrap-dhcpd.cml"
  deps = [ ":bootstrap-dhcpd-bin" ]
}

rustc_binary("bootstrap-dhcpd-bin") {
  edition = "2021"
  name = "bootstrap_dhcpd_bin"
  deps = [
    ":delegated-provisioning-constants",
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.net.dhcp:fuchsia.net.dhcp_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:tracing",
  ]
  source_root = "src/bootstrap_dhcpd.rs"
  sources = [ "src/bootstrap_dhcpd.rs" ]
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

group("tests") {
  testonly = true
  deps = [
    # Note the following targets aren't actually tests, but we add them to this
    # group to ensure they are hooked into the build graph. Otherwise they'd be
    # floating targets that never get built.
    #
    # We don't include `test-component` as it's not a standalone test and is
    # intended to be included as part of one of the delegated provisioning
    # netemul runner tests.
    ":bootstrap-dhcpd-bin",
    ":test-bin",
  ]
}
