# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/connectivity/network/drivers/network-device/ddk_source_split.gni")

ddk_source_split("mac") {
  sources = [
    "log.h",
    "mac_interface.cc",
    "mac_interface.h",
    "public/network_mac.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.hardware.network:fuchsia.hardware.network_cpp",
    "//sdk/fidl/fuchsia.hardware.network.driver:fuchsia.hardware.network.driver_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.network.driver:fuchsia.hardware.network.driver_cpp",
    "//sdk/lib/fidl",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/fbl",
  ]
}

source_set("test_util") {
  testonly = true
  sources = [
    "test_util.cc",
    "test_util.h",
    "test_util_banjo.cc",
    "test_util_banjo.h",
  ]
  public_deps = [
    ":mac",
    "//src/lib/testing/predicates",
    "//zircon/system/ulib/sync:sync-cpp",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":network-device-mac-lib-test" ]
}

test("test_bin") {
  output_name = "network-device-mac-lib-test"
  sources = [ "network_mac-test.cc" ]
  deps = [
    ":mac",
    ":test_util",
    "//sdk/lib/driver/testing/cpp:cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/sync:sync-cpp",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_unittest_package("network-device-mac-lib-test") {
  deps = [ ":test_bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
