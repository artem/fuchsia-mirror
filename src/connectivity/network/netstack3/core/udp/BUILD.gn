# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import(
    "//src/connectivity/network/netstack3/core/rustc_library_with_features.gni")

rustc_library_with_features("netstack3-udp") {
  edition = "2021"
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
  sources = [
    "src/base.rs",
    "src/lib.rs",
  ]
  deps = [
    "//src/connectivity/lib/net-types",
    "//src/connectivity/lib/packet-formats",
    "//src/connectivity/network/netstack3/core/lock-order",
    "//src/lib/fakealloc",
    "//src/lib/network/packet",
    "//third_party/rust_crates:derivative",
    "//third_party/rust_crates:either",
    "//third_party/rust_crates:smallvec",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",

    # TODO(https://github.com/rust-lang/rust/issues/62502): remove this module.
    "//src/connectivity/network/netstack3/core/fakestd",
  ]

  _testutils_deps = []

  configs += [ "//src/connectivity/network/netstack3:netstack3_configs" ]

  feature_sets = [
    {
      features = []
      deps += [
        "//src/connectivity/network/netstack3/core/base:netstack3-base",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip",
      ]
    },

    {
      target_name = "netstack3-udp-testutils"
      testonly = true
      features = [
        "testutils",
        "instrumented",
      ]
      deps += _testutils_deps
      deps += [
        "//src/connectivity/network/netstack3/core/base:netstack3-base-testutils",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-testutils",
      ]
    },
    {
      target_name = "netstack3-udp-benchmarks"
      testonly = true
      features = [ "testutils" ]
      deps += _testutils_deps
      deps += [
        "//src/connectivity/network/netstack3/core/base:netstack3-base-benchmarks",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-benchmarks",
      ]
      configs += [ "//src/connectivity/network/netstack3/core:benchmark" ]
    },
    {
      target_name = "netstack3-udp-instrumented"
      with_unit_tests = true
      features = [ "instrumented" ]
      non_test_deps = [
        "//src/connectivity/network/netstack3/core/base:netstack3-base-instrumented",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-instrumented",
      ]
      test_deps = [
        "//src/connectivity/lib/ip-test-macro",
        "//src/connectivity/lib/net-declare",
        "//src/connectivity/network/netstack3/core/base:netstack3-base-testutils",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-testutils",
        "//src/lib/const-unwrap",
        "//third_party/rust_crates:assert_matches",
        "//third_party/rust_crates:itertools",
        "//third_party/rust_crates:test-case",
      ]
      test_deps += _testutils_deps
    },
  ]
  if (is_host) {
    feature_sets += [
      {
        target_name = "netstack3-udp-loom"
        features = [
          "testutils",
          "instrumented",
        ]
        testonly = true
        deps += _testutils_deps
        deps += [
          "//src/connectivity/network/netstack3/core/base:netstack3-base-loom",
          "//src/connectivity/network/netstack3/core/ip:netstack3-ip-loom",
        ]
        configs += [ "//src/connectivity/network/netstack3/core:loom" ]
      },
    ]
  }
}

fuchsia_unittest_package("netstack3-core-udp-test") {
  deps = [ ":netstack3-udp-instrumented_test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":netstack3-core-udp-test",
    ":netstack3-udp-instrumented_test($host_toolchain)",
    "tests",
  ]
}
