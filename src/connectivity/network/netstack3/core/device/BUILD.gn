# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import(
    "//src/connectivity/network/netstack3/core/rustc_library_with_features.gni")

rustc_library_with_features("netstack3-device") {
  edition = "2021"
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
  sources = [
    "src/api.rs",
    "src/arp.rs",
    "src/base.rs",
    "src/config.rs",
    "src/ethernet.rs",
    "src/id.rs",
    "src/lib.rs",
    "src/loopback.rs",
    "src/pure_ip.rs",
    "src/queue.rs",
    "src/queue/api.rs",
    "src/queue/fifo.rs",
    "src/queue/rx.rs",
    "src/queue/tx.rs",
    "src/socket.rs",
    "src/state.rs",
  ]
  deps = [
    "//src/connectivity/lib/net-types",
    "//src/connectivity/lib/packet-formats",
    "//src/connectivity/network/netstack3/core/lock-order",
    "//src/lib/const-unwrap",
    "//src/lib/fakealloc",
    "//src/lib/network/packet",
    "//third_party/rust_crates:derivative",
    "//third_party/rust_crates:ref-cast",
    "//third_party/rust_crates:tracing",
  ]

  _testutils_deps = [ "//third_party/rust_crates:assert_matches" ]

  configs += [ "//src/connectivity/network/netstack3:netstack3_configs" ]

  feature_sets = [
    {
      features = []
      deps += [
        "//src/connectivity/network/netstack3/core/base:netstack3-base",
        "//src/connectivity/network/netstack3/core/filter:netstack3-filter",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip",
      ]
    },

    {
      target_name = "netstack3-device-testutils"
      testonly = true
      features = [
        "testutils",
        "instrumented",
      ]
      deps += _testutils_deps
      deps += [
        "//src/connectivity/network/netstack3/core/base:netstack3-base-testutils",
        "//src/connectivity/network/netstack3/core/filter:netstack3-filter-testutils",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-testutils",
      ]
    },
    {
      target_name = "netstack3-device-benchmarks"
      testonly = true
      features = [ "testutils" ]
      deps += _testutils_deps
      deps += [
        "//src/connectivity/network/netstack3/core/base:netstack3-base-benchmarks",
        "//src/connectivity/network/netstack3/core/filter:netstack3-filter-benchmarks",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-benchmarks",
      ]
      configs += [ "//src/connectivity/network/netstack3/core:benchmark" ]
    },
    {
      target_name = "netstack3-device-instrumented"
      with_unit_tests = true
      features = [ "instrumented" ]
      non_test_deps = [
        "//src/connectivity/network/netstack3/core/base:netstack3-base-instrumented",
        "//src/connectivity/network/netstack3/core/filter:netstack3-filter-instrumented",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-instrumented",
      ]
      test_deps = [
        "//src/connectivity/lib/net-declare",
        "//src/connectivity/network/netstack3/core/base:netstack3-base-testutils",
        "//src/connectivity/network/netstack3/core/filter:netstack3-filter-testutils",
        "//src/connectivity/network/netstack3/core/ip:netstack3-ip-testutils",
        "//third_party/rust_crates:test-case",
      ]
      test_deps += _testutils_deps
    },
  ]
  if (is_host) {
    feature_sets += [
      {
        target_name = "netstack3-device-loom"
        features = [
          "testutils",
          "instrumented",
        ]
        testonly = true
        deps += _testutils_deps
        deps += [
          "//src/connectivity/network/netstack3/core/base:netstack3-base-loom",
          "//src/connectivity/network/netstack3/core/filter:netstack3-filter-loom",
          "//src/connectivity/network/netstack3/core/ip:netstack3-ip-loom",
        ]
        configs += [ "//src/connectivity/network/netstack3/core:loom" ]
      },
    ]
  }
}

fuchsia_unittest_package("netstack3-core-device-test") {
  deps = [ ":netstack3-device-instrumented_test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":netstack3-core-device-test",
    ":netstack3-device-instrumented_test($host_toolchain)",
  ]
}
