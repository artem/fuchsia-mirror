# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import(
    "//src/connectivity/network/netstack3/core/rustc_library_with_features.gni")

rustc_library_with_features("netstack3-filter") {
  edition = "2021"
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
  sources = [
    "src/api.rs",
    "src/conntrack.rs",
    "src/context.rs",
    "src/lib.rs",
    "src/logic.rs",
    "src/logic/nat.rs",
    "src/matchers.rs",
    "src/packets.rs",
    "src/state.rs",
    "src/state/validation.rs",
  ]
  deps = [
    "//src/connectivity/lib/net-types",
    "//src/connectivity/lib/packet-formats",
    "//src/lib/fakealloc",
    "//src/lib/network/packet",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:derivative",
    "//third_party/rust_crates:once_cell",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:tracing",
    "//third_party/rust_crates:zerocopy",
  ]

  if (!is_host) {
    configs += [ "//src/connectivity/network/netstack3:netstack3_configs" ]
  }

  feature_sets = [
    {
      features = []
      deps +=
          [ "//src/connectivity/network/netstack3/core/base:netstack3-base" ]
    },

    {
      target_name = "netstack3-filter-instrumented"
      features = [ "instrumented" ]
      with_unit_tests = true
      non_test_deps = [ "//src/connectivity/network/netstack3/core/base:netstack3-base-instrumented" ]
      test_deps = [
        "//src/connectivity/lib/ip-test-macro",
        "//src/connectivity/lib/net-declare",
        "//src/connectivity/network/netstack3/core/base:netstack3-base-testutils",
        "//src/lib/const-unwrap",
        "//third_party/rust_crates:test-case",
      ]

      if (!is_host) {
        test_deps += [ "//src/connectivity/network/netstack3/fuchsia:netstack3-fuchsia-testutils" ]
      }
    },

    {
      target_name = "netstack3-filter-testutils"
      testonly = true
      features = [
        "testutils",
        "instrumented",
      ]
      deps += [ "//src/connectivity/network/netstack3/core/base:netstack3-base-testutils" ]
    },
    {
      target_name = "netstack3-filter-benchmarks"
      testonly = true
      features = [ "testutils" ]
      deps += [ "//src/connectivity/network/netstack3/core/base:netstack3-base-benchmarks" ]
    },
  ]

  if (is_host) {
    feature_sets += [
      {
        target_name = "netstack3-filter-loom"
        testonly = true
        features = [ "testutils" ]
        deps += [
          "//src/connectivity/network/netstack3/core/base:netstack3-base-loom",
        ]
        configs += [ "//src/connectivity/network/netstack3/core:loom" ]
      },
    ]
  }
}

fuchsia_unittest_package("netstack3-filter-test") {
  deps = [ ":netstack3-filter-instrumented_test" ]
}

group("tests") {
  testonly = true
  public_deps = [
    ":netstack3-filter-instrumented_test($host_toolchain)",
    ":netstack3-filter-test",
  ]
}
