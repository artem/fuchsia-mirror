# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")

fidl("fuchsia.wlan.wlanix") {
  sdk_category = "internal"

  sources = [ "wlanix.fidl" ]

  public_deps = [ "//zircon/vdso/zx" ]
}

rustc_binary("wlanix_bin") {
  output_name = "wlanix"
  edition = "2018"
  with_unit_tests = true

  deps = [
    ":fuchsia.wlan.wlanix_rust",
    "//sdk/fidl/fuchsia.wlan.common:fuchsia.wlan.common_rust",
    "//sdk/fidl/fuchsia.wlan.device.service:fuchsia.wlan.device.service_rust",
    "//sdk/fidl/fuchsia.wlan.sme:fuchsia.wlan.sme_rust",
    "//src/connectivity/wlan/lib/common/rust:wlan-common",
    "//src/lib/diagnostics/log/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:netlink-packet-core",
    "//third_party/rust_crates:netlink-packet-generic",
    "//third_party/rust_crates:netlink-packet-utils",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//sdk/fidl/fuchsia.wlan.internal:fuchsia.wlan.internal_rust",
    "//src/lib/fidl/rust/fidl",
    "//third_party/rust_crates:test-case",
  ]

  sources = [
    "src/main.rs",
    "src/nl80211/attr/band.rs",
    "src/nl80211/attr/bss.rs",
    "src/nl80211/attr/mod.rs",
    "src/nl80211/cmd.rs",
    "src/nl80211/constants.rs",
    "src/nl80211/mod.rs",
    "src/nl80211/nested.rs",
    "src/service/mod.rs",
    "src/service/sme.rs",
  ]
}

fuchsia_component("component") {
  manifest = "meta/wlanix.cml"
  component_name = "wlanix"
  deps = [ ":wlanix_bin" ]
}

fuchsia_package("wlanix") {
  deps = [ ":component" ]
}

fuchsia_unittest_package("wlanix-tests") {
  deps = [ ":wlanix_bin_test" ]
}
