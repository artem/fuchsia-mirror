# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

rustc_library("donut-lib") {
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.wlan.common:fuchsia.wlan.common_rust",
    "//sdk/fidl/fuchsia.wlan.policy:fuchsia.wlan.policy_rust",
    "//sdk/fidl/fuchsia.wlan.product.deprecatedconfiguration:fuchsia.wlan.product.deprecatedconfiguration_rust",
    "//src/lib/fidl/rust/fidl",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:clap",
    "//third_party/rust_crates:eui48",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:hex",
    "//third_party/rust_crates:percent-encoding",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:structopt",
  ]

  # Most of the functionality in this library can be shared between the host tools and the Fuchsia
  # platform.  However, the unit tests make use of TestExecutor::run_single_threaded which does not
  # appear to be supported for the host.  The wlan_common library is used only for assisting in
  # these tests and itself includes many Fuchsia-specific libraries.  As such, there attributes
  # should only be included when the build targets Fuchsia.
  if (current_os == "fuchsia") {
    with_unit_tests = true
    test_deps = [
      "//src/connectivity/wlan/lib/common/rust/:wlan-common",
      "//src/connectivity/wlan/lib/ieee80211",
      "//src/lib/fuchsia",
      "//src/lib/fuchsia-async",
      "//src/lib/zircon/rust:fuchsia-zircon-status",
      "//third_party/rust_crates:test-case",
    ]
  }
  sources = [
    "src/lib.rs",
    "src/opts.rs",
    "src/serialize.rs",
  ]
}

fuchsia_unittest_package("donut-lib-tests") {
  deps = [ ":donut-lib_test" ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
