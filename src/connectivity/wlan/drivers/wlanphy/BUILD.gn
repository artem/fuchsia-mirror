# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

wlanphy_sources = [
  "device.cc",
  "device.h",
]

wlanphy_deps = [
  ":wlanphy_bind",
  "//sdk/fidl/fuchsia.wlan.device:fuchsia.wlan.device_cpp",
  "//sdk/fidl/fuchsia.wlan.phyimpl:fuchsia.wlan.phyimpl_cpp",
  "//sdk/lib/driver/component/cpp",
  "//sdk/lib/driver/devfs/cpp",
  "//sdk/lib/driver/runtime:driver_runtime_cpp",
  "//src/connectivity/wlan/drivers/lib/log/cpp:dfv2",
  "//src/connectivity/wlan/lib/common/cpp:common",
  "//src/devices/lib/driver:driver_runtime",
  "//zircon/system/ulib/async:async-cpp",
  "//zircon/system/ulib/async-loop:async-loop-cpp",
  "//zircon/system/ulib/async-loop:async-loop-default",
  "//zircon/system/ulib/sync:sync-cpp",
]

driver_bind_rules("wlanphy_bind") {
  rules = "wlanphy.bind"
  bind_output = "wlanphy.bindbc"
  tests = "wlanphy_bind_test.json"
  deps = [ "//sdk/fidl/fuchsia.wlan.phyimpl:fuchsia.wlan.phyimpl_bindlib" ]
}

fuchsia_driver("wlanphy_driver-driver") {
  output_name = "wlanphy"
  sources = wlanphy_sources

  deps = wlanphy_deps

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("wlanphy_driver") {
  component_name = "wlanphy"
  deps = [ ":wlanphy_driver-driver" ]
  info = "wlanphy_driver-info.json"
  manifest = "meta/wlanphy.cml"
}

fuchsia_driver_package("wlanphy") {
  driver_components = [ ":wlanphy_driver" ]
}

# unit tests start here

executable("wlanphy_device_test") {
  testonly = true
  sources = wlanphy_sources + [ "tests/wlanphy_device_test.cc" ]

  deps = wlanphy_deps + [
           "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_cpp",
           "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_cpp",
           "//sdk/lib/async_patterns/testing/cpp:cpp",
           "//sdk/lib/driver/testing/cpp:cpp",
           "//sdk/lib/sys/cpp/testing:cpp",
           "//src/lib/fxl/test:gtest_main",
           "//zircon/system/ulib/sync",
           "//zircon/system/ulib/sync:sync-cpp",
           "//zircon/system/ulib/zx",
         ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_component("wlanphy_device_test_component") {
  deps = [ ":wlanphy_device_test" ]
  manifest = "meta/wlanphy-test.cml"
}

fuchsia_test_package("wlanphy-unittests") {
  test_components = [ ":wlanphy_device_test_component" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":wlanphy-unittests",
    ":wlanphy_bind_test",
  ]
}
