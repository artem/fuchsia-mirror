# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

driver_bind_rules("bt-hci-virtual-bind") {
  rules = "bt-hci-virtual.bind"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.platform" ]
}

source_set("common") {
  testonly = true

  sources = [
    "//src/connectivity/bluetooth/hci/vendor/broadcom/packets.h",
    "controller.cc",
    "controller.h",
    "emulated_peer.cc",
    "emulated_peer.h",
    "emulator.cc",
    "emulator.h",
    "loopback.cc",
    "loopback.h",
  ]

  public_deps = [
    ":bt-hci-virtual-bind",
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_cpp",
    "//sdk/fidl/fuchsia.hardware.bluetooth:fuchsia.hardware.bluetooth_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/devfs/cpp:cpp",
    "//src/connectivity/bluetooth/core/bt-host/common",
    "//src/connectivity/bluetooth/core/bt-host/fidl:helpers",
    "//src/connectivity/bluetooth/core/bt-host/testing:fake_controller",
    "//src/devices/lib/driver:driver_runtime",
    "//third_party/pigweed/backends/pw_async_fuchsia:dispatcher",
    "//third_party/pigweed/backends/pw_log:printf",
    "//third_party/pigweed/backends/pw_random:zircon_random_generator",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
  ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver("fuchsia_driver") {
  testonly = true

  output_name = "bt-hci-virtual"
  sources = []
  deps = [ ":common" ]
}

fuchsia_driver_component("driver") {
  testonly = true

  component_name = "bt-hci-virtual"
  deps = [ ":fuchsia_driver" ]
  info = "driver-info.json"
  manifest = "meta/driver.cml"
}

fuchsia_driver_package("virtual") {
  testonly = true

  driver_components = [ ":driver" ]

  # This package is reachable from //src/connectivity/bluetooth
}

group("tests") {
  testonly = true

  deps = [ ":bt-hci-virtual-bind_test" ]
}
