# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fuzz.gni")
import("//build_overrides/pigweed.gni")
import("$dir_pw_fuzzer/fuzzer.gni")
import("$dir_pw_unit_test/test.gni")

public_dir = "//src/connectivity/bluetooth/core/bt-host/public/pw_bluetooth_sapphire/internal/host/l2cap"

# Basic target with protocol definitions and no logic, suitable for test
# emulation.
pw_source_set("definitions") {
  sources = [
    "$public_dir/frame_headers.h",
    "$public_dir/l2cap_defs.h",
  ]

  public_deps = [
    "//src/connectivity/bluetooth/core/bt-host/hci-spec",
    "//src/connectivity/bluetooth/core/bt-host/sm:definitions",
  ]
}

pw_source_set("l2cap") {
  sources = [
    "$public_dir/a2dp_offload_manager.h",
    "$public_dir/basic_mode_rx_engine.h",
    "$public_dir/basic_mode_tx_engine.h",
    "$public_dir/bredr_command_handler.h",
    "$public_dir/bredr_dynamic_channel.h",
    "$public_dir/bredr_signaling_channel.h",
    "$public_dir/channel.h",
    "$public_dir/channel_configuration.h",
    "$public_dir/channel_manager.h",
    "$public_dir/command_handler.h",
    "$public_dir/dynamic_channel.h",
    "$public_dir/dynamic_channel_registry.h",
    "$public_dir/enhanced_retransmission_mode_engines.h",
    "$public_dir/enhanced_retransmission_mode_rx_engine.h",
    "$public_dir/enhanced_retransmission_mode_tx_engine.h",
    "$public_dir/fcs.h",
    "$public_dir/fragmenter.h",
    "$public_dir/le_signaling_channel.h",
    "$public_dir/logical_link.h",
    "$public_dir/low_energy_command_handler.h",
    "$public_dir/pdu.h",
    "$public_dir/recombiner.h",
    "$public_dir/rx_engine.h",
    "$public_dir/scoped_channel.h",
    "$public_dir/signaling_channel.h",
    "$public_dir/tx_engine.h",
    "$public_dir/types.h",
    "a2dp_offload_manager.cc",
    "basic_mode_rx_engine.cc",
    "basic_mode_tx_engine.cc",
    "bredr_command_handler.cc",
    "bredr_dynamic_channel.cc",
    "bredr_signaling_channel.cc",
    "channel.cc",
    "channel_configuration.cc",
    "channel_manager.cc",
    "command_handler.cc",
    "dynamic_channel.cc",
    "dynamic_channel_registry.cc",
    "enhanced_retransmission_mode_engines.cc",
    "enhanced_retransmission_mode_rx_engine.cc",
    "enhanced_retransmission_mode_tx_engine.cc",
    "fcs.cc",
    "fragmenter.cc",
    "le_signaling_channel.cc",
    "logical_link.cc",
    "low_energy_command_handler.cc",
    "pdu.cc",
    "recombiner.cc",
    "scoped_channel.cc",
    "signaling_channel.cc",
    "types.cc",
  ]

  public_deps = [
    ":definitions",
    "$dir_pw_third_party/fuchsia:fit",
    "//src/connectivity/bluetooth/core/bt-host/hci",
    "//src/connectivity/bluetooth/core/bt-host/transport",
    "//third_party/pigweed/src/pw_string",
  ]
}

# Separate from :testing to avoid a dependency cycle.
pw_source_set("channel_manager_mock_controller_test_fixture") {
  testonly = true

  sources = [ "$public_dir/channel_manager_mock_controller_test_fixture.h" ]

  public_deps = [
    ":l2cap",
    ":testing",
    "//src/connectivity/bluetooth/core/bt-host/hci",
    "//src/connectivity/bluetooth/core/bt-host/testing",
  ]
}

pw_source_set("testing") {
  testonly = true

  sources = [
    "$public_dir/fake_channel.h",
    "$public_dir/fake_channel_test.h",
    "$public_dir/fake_l2cap.h",
    "$public_dir/fake_signaling_channel.h",
    "$public_dir/mock_channel_test.h",
    "$public_dir/test_packets.h",
    "fake_channel.cc",
    "fake_channel_test.cc",
    "fake_l2cap.cc",
    "fake_signaling_channel.cc",
    "mock_channel_test.cc",
    "test_packets.cc",
  ]

  public_deps = [
    ":l2cap",
    "$dir_pw_bluetooth",
    "$dir_pw_unit_test",
    "//src/connectivity/bluetooth/core/bt-host/common",
    "//src/connectivity/bluetooth/core/bt-host/hci",
    "//src/connectivity/bluetooth/core/bt-host/testing:test_helpers",
    "//third_party/pigweed:pw_async_heap_dispatcher",
    "//third_party/pigweed/backends/pw_async_fuchsia:fake_dispatcher_fixture",
  ]
}

pw_test("tests") {
  sources = [
    "a2dp_offload_manager_unittest.cc",
    "basic_mode_rx_engine_unittest.cc",
    "basic_mode_tx_engine_unittest.cc",
    "bredr_command_handler_unittest.cc",
    "bredr_dynamic_channel_unittest.cc",
    "bredr_signaling_channel_unittest.cc",
    "channel_configuration_unittest.cc",
    "channel_manager_unittest.cc",
    "channel_unittest.cc",
    "command_handler_unittest.cc",
    "dynamic_channel_registry_unittest.cc",
    "enhanced_retransmission_mode_engines_unittest.cc",
    "enhanced_retransmission_mode_rx_engine_unittest.cc",
    "enhanced_retransmission_mode_tx_engine_unittest.cc",
    "fcs_unittest.cc",
    "fragmenter_unittest.cc",
    "frame_headers_unittest.cc",
    "le_signaling_channel_unittest.cc",
    "logical_link_unittest.cc",
    "low_energy_command_handler_unittest.cc",
    "pdu_unittest.cc",
    "recombiner_unittest.cc",
    "scoped_channel_unittest.cc",
    "signaling_channel_unittest.cc",
    "types_unittest.cc",
  ]

  deps = [
    ":channel_manager_mock_controller_test_fixture",
    ":l2cap",
    ":testing",
    "//src/connectivity/bluetooth/core/bt-host/hci",
    "//src/connectivity/bluetooth/core/bt-host/hci:testing",
    "//src/connectivity/bluetooth/core/bt-host/testing",
    "//src/connectivity/bluetooth/core/bt-host/transport:testing",
    "//third_party/pigweed/backends/pw_async_fuchsia:fake_dispatcher_fixture",
  ]
}

fuchsia_library_fuzzer("basic_mode_rx_engine_fuzzer") {
  sources = [ "basic_mode_rx_engine_fuzztest.cc" ]
  deps = [
    ":l2cap",
    ":testing",
    "//src/connectivity/bluetooth/core/bt-host/testing:fuzztest_driver",
  ]
}

pw_fuzzer("pw_basic_mode_rx_engine_fuzzer") {
  sources = [ "basic_mode_rx_engine_fuzztest.cc" ]
  deps = [
    ":l2cap",
    ":testing",
  ]
}

fuchsia_library_fuzzer("enhanced_retransmission_mode_engines_fuzzer") {
  sources = [ "enhanced_retransmission_mode_engines_fuzztest.cc" ]
  deps = [
    ":l2cap",
    ":testing",
    "//src/connectivity/bluetooth/core/bt-host/testing:fuzztest_driver",
  ]
}

pw_fuzzer("pw_enhanced_retransmission_mode_engines_fuzzer") {
  sources = [ "enhanced_retransmission_mode_engines_fuzztest.cc" ]
  deps = [
    ":l2cap",
    ":testing",
  ]
}

fuchsia_library_fuzzer("bredr_dynamic_channel_registry_fuzzer") {
  sources = [ "bredr_dynamic_channel_registry_fuzztest.cc" ]
  deps = [
    ":l2cap",
    ":testing",
    "$dir_pw_random:fuzzer_generator",
    "//src/connectivity/bluetooth/core/bt-host/testing:fuzztest_driver",
  ]
}

pw_fuzzer("pw_bredr_dynamic_channel_registry_fuzzer") {
  sources = [ "bredr_dynamic_channel_registry_fuzztest.cc" ]
  deps = [
    ":l2cap",
    ":testing",
    "$dir_pw_random:fuzzer_generator",
  ]
}

fuchsia_library_fuzzer("channel_configuration_fuzzer") {
  sources = [ "channel_configuration_fuzztest.cc" ]
  deps = [
    ":l2cap",
    ":testing",
    "//src/connectivity/bluetooth/core/bt-host/testing:fuzztest_driver",
  ]
}

pw_fuzzer("pw_channel_configuration_fuzzer") {
  sources = [ "channel_configuration_fuzztest.cc" ]
  deps = [
    ":l2cap",
    ":testing",
  ]
}

fuchsia_library_fuzzer("common_handler_fuzzer") {
  sources = [ "common_handler_fuzztest.cc" ]
  deps = [
    ":l2cap",
    ":testing",
    "//src/connectivity/bluetooth/core/bt-host/testing:fuzztest_driver",
  ]
}

pw_fuzzer("pw_common_handler_fuzzer") {
  sources = [ "common_handler_fuzztest.cc" ]
  deps = [
    ":l2cap",
    ":testing",
  ]
}

fuchsia_library_fuzzer("l2cap_fuzzer") {
  sources = [ "l2cap_fuzztest.cc" ]
  deps = [
    ":l2cap",
    "$dir_pw_random:fuzzer_generator",
    "//src/connectivity/bluetooth/core/bt-host/testing",
    "//src/connectivity/bluetooth/core/bt-host/testing:controller_test_double_base",
    "//src/connectivity/bluetooth/core/bt-host/testing:fuzztest_driver",
  ]
}

pw_fuzzer("pw_l2cap_fuzzer") {
  sources = [ "l2cap_fuzztest.cc" ]
  deps = [
    ":l2cap",
    "$dir_pw_random:fuzzer_generator",
    "//src/connectivity/bluetooth/core/bt-host/testing",
    "//src/connectivity/bluetooth/core/bt-host/testing:controller_test_double_base",
  ]
}
