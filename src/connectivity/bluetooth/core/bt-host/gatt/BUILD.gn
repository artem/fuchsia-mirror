# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build_overrides/pigweed.gni")
import("$dir_pw_build/target_types.gni")
import("$dir_pw_unit_test/test.gni")

public_dir = "//src/connectivity/bluetooth/core/bt-host/public/pw_bluetooth_sapphire/internal/host/gatt"

# Basic target with protocol definitions and no logic, suitable for test
# emulation.
pw_source_set("definitions") {
  sources = [
    "$public_dir/gatt_defs.h",
    "gatt_defs.cc",
  ]

  public_deps = [ "//src/connectivity/bluetooth/core/bt-host/att:definitions" ]
}

pw_source_set("gatt") {
  sources = [
    "$public_dir/client.h",
    "$public_dir/connection.h",
    "$public_dir/gatt.h",
    "$public_dir/generic_attribute_service.h",
    "$public_dir/local_service_manager.h",
    "$public_dir/remote_characteristic.h",
    "$public_dir/remote_service.h",
    "$public_dir/remote_service_manager.h",
    "$public_dir/server.h",
    "$public_dir/types.h",
    "client.cc",
    "connection.cc",
    "gatt.cc",
    "generic_attribute_service.cc",
    "local_service_manager.cc",
    "remote_characteristic.cc",
    "remote_service.cc",
    "remote_service_manager.cc",
    "server.cc",
    "types.cc",
  ]

  public_deps = [
    ":definitions",
    "$dir_pw_third_party/fuchsia:fit",
    "//src/connectivity/bluetooth/core/bt-host/att",
  ]
}

pw_source_set("testing") {
  testonly = true

  sources = [
    "$public_dir/fake_client.h",
    "$public_dir/fake_layer.h",
    "$public_dir/mock_server.h",
    "fake_client.cc",
    "fake_layer.cc",
    "mock_server.cc",
  ]

  public_deps = [
    ":gatt",
    "$dir_pw_unit_test",
    "//src/connectivity/bluetooth/core/bt-host/common",
    "//third_party/pigweed:pw_async_heap_dispatcher",
  ]
}

pw_test("tests") {
  sources = [
    "client_unittest.cc",
    "gatt_unittest.cc",
    "generic_attribute_service_unittest.cc",
    "local_service_manager_unittest.cc",
    "remote_service_manager_unittest.cc",
    "server_unittest.cc",
  ]

  deps = [
    ":gatt",
    ":testing",
    "//src/connectivity/bluetooth/core/bt-host/att",
    "//src/connectivity/bluetooth/core/bt-host/l2cap:testing",
    "//src/connectivity/bluetooth/core/bt-host/testing",
  ]
}
