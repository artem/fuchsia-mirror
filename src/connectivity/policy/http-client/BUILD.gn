# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//src/connectivity/network/unified_binary/network_unified_binary.gni")

group("tests") {
  testonly = true

  deps = [ "integration:tests" ]
}

network_unified_binary("http_client") {
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.net.http:fuchsia.net.http_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-hyper",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/storage/lib/vfs/rust:vfs",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:hyper",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/main.rs" ]
}

fuchsia_component_manifest("manifest") {
  component_name = "http-client"
  manifest = "meta/http_client.cml"
  restricted_features = [ "config_capabilities" ]
}

fuchsia_structured_config_values("default_config_value") {
  cm_label = ":manifest"
  values = {
    stop_on_idle_timeout_millis = -1
  }
}

network_unified_binary_regular_and_gub_components("component") {
  component_name = "http-client"
  cm_label = ":manifest"
  gub_deps = [ ":http_client" ]
  non_gub_deps = [ ":default_config_value" ]
}

network_unified_binary_regular_and_gub_packages("package") {
  package_name = "http-client"
  gub_deps = [ ":component" ]
  visibility = [ "//bundles/assembly/*" ]
}
