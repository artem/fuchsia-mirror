# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("http_client_integration_test") {
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.net.http:fuchsia.net.http_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
    "//src/lib/fuchsia-hyper",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:hyper",
    "//third_party/rust_crates:test-case",
  ]

  sources = [ "src/lib.rs" ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_test_component("component") {
  component_name = "http-client-integration-test"
  manifest = "meta/http_client_integration_test.cml"
  deps = [ ":http_client_integration_test" ]
  test_type = "system"
  restricted_features = [ "config_capabilities" ]
}

fuchsia_test_package("http-client-integration-tests") {
  test_components = [ ":component" ]
  deps = [
    ":config_idle_1ms",
    ":config_never_idle",

    # The component under test.
    "../:component",
    "//src/connectivity/network/netstack:component-debug",
  ]
}

# A component providing configuration that the http client should never stop itself on idle.
fuchsia_component_manifest("config_never_idle") {
  component_name = "http-client-config-never-idle"
  manifest = "meta/config_never_idle.cml"
  restricted_features = [ "config_capabilities" ]
}

# A component providing configuration that the http client should stop itself if it did not
# receive requests for 1 milliseconds.
fuchsia_component_manifest("config_idle_1ms") {
  component_name = "http-client-config-idle-1ms"
  manifest = "meta/config_idle_1ms.cml"
  restricted_features = [ "config_capabilities" ]
}

group("tests") {
  testonly = true
  deps = [ ":http-client-integration-tests" ]
}
