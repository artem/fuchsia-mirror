# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/component/config.gni")
import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")

group("tests") {
  testonly = true
  deps = [ ":role_manager-test" ]
}

rustc_test("bin") {
  edition = "2021"
  source_root = "src/main.rs"
  sources = [ source_root ]
  name = "role_manager_test"
  deps = [
    "../testing/fidl:test.rolemanager_rust",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/testing/realm_proxy/client",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_component("test-suite-component") {
  testonly = true
  component_name = "test-suite"
  manifest = "meta/test-suite.cml"
  deps = [ ":bin" ]
}

fuchsia_package("test-suite") {
  testonly = true
  deps = [ ":test-suite-component" ]
}

fuchsia_test_component("test-root") {
  testonly = true

  # This needs to be a system test to access fuchsia.kernel.ProfileResource.
  test_type = "system"
  manifest = "meta/test-root.cml"
}

fuchsia_test_package("role_manager-test") {
  test_components = [ ":test-root" ]
  subpackages = [
    "../testing/realm-factory",
    ":test-suite",
  ]
}
