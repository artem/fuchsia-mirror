# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/cpp/library_headers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = []
  if (!is_asan) {
    deps += [
      ":restricted-mode-shared-long-stress-test",
      ":restricted-mode-shared-short-stress-test",
    ]
  }
}

library_headers("lib_headers") {
  include_dir = "."
  headers = [ "lib.h" ]
}

source_set("lib") {
  testonly = true
  sources = [ "lib.cc" ]
  public_deps = [ ":lib_headers" ]
  deps = [ "//zircon/system/ulib/zxtest" ]
}

test_types = [
  "short",
  "long",
]
foreach(test_type, test_types) {
  if (!is_asan) {
    test("${test_type}_bin") {
      output_name = "restricted-mode-shared-${test_type}-stress-test"
      sources = [ "${test_type}.cc" ]
      deps = [
        ":lib",
        "//zircon/system/ulib/zxtest",
      ]
    }

    fuchsia_unittest_component("restricted-mode-shared-${test_type}-stress") {
      manifest = "meta/restricted-mode-shared-${test_type}-stress-test.cml"
      deps = [ ":${test_type}_bin" ]
      test_type = "system"
    }

    fuchsia_test_package("restricted-mode-shared-${test_type}-stress-test") {
      test_components = [ ":restricted-mode-shared-${test_type}-stress" ]
      test_specs = {
        if (test_type == "long") {
          environments = [
            {
              dimensions = {
                device_type = "QEMU"
              }
              tags = [ "stress-tests" ]
            },
            {
              dimensions = {
                device_type = "Vim3"
              }
              tags = [ "stress-tests" ]
            },
          ]
        } else {
          environments = [
            emu_env,
            vim3_env,
            nuc_env,
          ]
        }
      }
    }
  }
}
