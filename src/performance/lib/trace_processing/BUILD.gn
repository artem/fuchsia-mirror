# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/python/python_binary.gni")
import("//build/python/python_library.gni")

python_library("trace_processing") {
  testonly = true
  library_name = "trace_processing"
  enable_mypy = true
  sources = [
    "__init__.py",
    "metrics/__init__.py",
    "metrics/app_render.py",
    "metrics/cpu.py",
    "metrics/cpu_breakdown.py",
    "metrics/fps.py",
    "metrics/input_latency.py",
    "metrics/scenic.py",
    "trace_importing.py",
    "trace_metrics.py",
    "trace_model.py",
    "trace_time.py",
    "trace_utils.py",
  ]
  library_deps = [ "//src/performance/lib/py_test_utils:perf_test_utils" ]
}

group("tests") {
  testonly = true
  public_deps = [ "tests" ]
}

python_binary("run_cpu_breakdown_bin") {
  testonly = true
  main_source = "run_cpu_breakdown.py"
  deps = [ ":trace_processing" ]
  enable_mypy = true
  output_name = "run_cpu_breakdown.pyz"
}

# Rename the resulting .pyz for the host tool
copy("copy") {
  testonly = true
  deps = [ ":run_cpu_breakdown_bin" ]
  sources = [ "${target_out_dir}/run_cpu_breakdown.pyz" ]
  outputs = [ "${root_out_dir}/run_cpu_breakdown" ]
}

install_host_tools("run_cpu_breakdown") {
  testonly = true
  deps = [ ":copy" ]
  outputs = [ "run_cpu_breakdown" ]
}
