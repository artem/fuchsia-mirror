# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found
# in the LICENSE file.

import("//build/python/python_mobly_test.gni")
import("//build/testing/environments.gni")
import("//build/testing/host_test_data.gni")

if (is_host) {
  host_test_data("tracing_runtime_deps") {
    sources = [ "$root_out_dir/trace2json" ]
    outputs = [ "$root_build_dir/trace_runtime_deps/{{source_file_part}}" ]
    deps = [ "//src/performance/trace2json:bin" ]
  }

  python_mobly_test("tracing_test_fc") {
    main_source = "test_tracing.py"
    libraries = [
      "//src/testing/end_to_end/honeydew",
      "//src/testing/end_to_end/mobly_base_tests:fuchsia_base_test",
    ]
    transport = "fuchsia-controller"

    # Only enable for AEMU-x64 as host-tests aren't supported on QEMU-arm64.
    environments = [ aemu_env ]
    deps = [ ":tracing_runtime_deps" ]
  }

  # Group containing all of the Tracing affordance functional test cases.
  group("tests") {
    testonly = true
    public_deps = [ ":tracing_test_fc($host_toolchain)" ]
  }
}
