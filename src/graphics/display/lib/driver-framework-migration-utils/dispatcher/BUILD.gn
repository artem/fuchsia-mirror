# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

source_set("dispatcher") {
  sources = [
    "dispatcher-factory.h",
    "dispatcher.h",
  ]
  public_deps = [
    "//sdk/lib/fidl/cpp/wire",
    "//sdk/lib/stdcompat",
    "//zircon/system/public",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
}

source_set("loop-backed-dispatcher") {
  sources = [
    "loop-backed-dispatcher-factory.cc",
    "loop-backed-dispatcher-factory.h",
    "loop-backed-dispatcher.cc",
    "loop-backed-dispatcher.h",
  ]

  public_deps = [
    ":dispatcher",
    "//src/lib/ddk",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]

  deps = [ "//zircon/system/ulib/fbl" ]
}

test("loop-backed-dispatcher-tests-bin") {
  output_name = "loop-backed-dispatcher-tests"
  sources = [ "loop-backed-dispatcher-test.cc" ]

  deps = [
    ":loop-backed-dispatcher",
    "//src/devices/testing/mock-ddk",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/sync:sync-cpp",
  ]
}

fuchsia_unittest_package("loop-backed-dispatcher-tests") {
  deps = [ ":loop-backed-dispatcher-tests-bin" ]
}

source_set("driver-runtime-backed-dispatcher") {
  sources = [
    "driver-runtime-backed-dispatcher-factory.cc",
    "driver-runtime-backed-dispatcher-factory.h",
    "driver-runtime-backed-dispatcher.cc",
    "driver-runtime-backed-dispatcher.h",
  ]

  public_deps = [
    ":dispatcher",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//zircon/system/ulib/zx",
  ]

  deps = [
    "//sdk/lib/driver/logging/cpp",
    "//zircon/system/public",
    "//zircon/system/ulib/fbl",
  ]
}

test("driver-runtime-backed-dispatcher-tests-bin") {
  output_name = "driver-runtime-backed-dispatcher-tests"
  sources = [ "driver-runtime-backed-dispatcher-test.cc" ]

  deps = [
    ":driver-runtime-backed-dispatcher",
    "testing:dfv2-driver-with-dispatcher",
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_cpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/driver/incoming/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/sync:sync-cpp",
  ]
}

fuchsia_unittest_package("driver-runtime-backed-dispatcher-tests") {
  deps = [ ":driver-runtime-backed-dispatcher-tests-bin" ]
}

group("tests") {
  testonly = true
  deps = [
    ":driver-runtime-backed-dispatcher-tests",
    ":loop-backed-dispatcher-tests",
  ]
}
