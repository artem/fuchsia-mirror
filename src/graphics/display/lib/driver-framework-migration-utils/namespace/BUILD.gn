# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

source_set("namespace") {
  sources = [
    "namespace.cc",
    "namespace.h",
  ]
  public_deps = [ "//sdk/lib/component/incoming/cpp" ]

  deps = [
    "//sdk/lib/fidl/cpp",
    "//zircon/system/ulib/zx",
  ]
}

source_set("namespace-dfv1") {
  sources = [
    "namespace-dfv1.cc",
    "namespace-dfv1.h",
  ]

  public_deps = [
    ":namespace",
    "//src/lib/ddk",
  ]

  deps = [ "//zircon/system/ulib/fbl" ]
}

test("namespace-dfv1-tests-bin") {
  output_name = "namespace-dfv1-tests"
  sources = [ "namespace-dfv1-test.cc" ]

  deps = [
    ":namespace-dfv1",
    "testing:test.display.namespace_cpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//src/devices/testing/mock-ddk",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]
}

fuchsia_unittest_package("namespace-dfv1-tests") {
  deps = [ ":namespace-dfv1-tests-bin" ]
}

source_set("namespace-dfv2") {
  sources = [
    "namespace-dfv2.cc",
    "namespace-dfv2.h",
  ]

  public_deps = [
    ":namespace",
    "//sdk/lib/driver/incoming/cpp",
    "//zircon/system/ulib/zx",
  ]

  deps = [
    "//sdk/lib/driver/logging/cpp",
    "//sdk/lib/fdio",
    "//zircon/system/public",
    "//zircon/system/ulib/fbl",
  ]
}

test("namespace-dfv2-tests-bin") {
  output_name = "namespace-dfv2-tests"
  sources = [ "namespace-dfv2-test.cc" ]

  deps = [
    ":namespace-dfv2",
    "testing:test.display.namespace_cpp",
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_cpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/incoming/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("namespace-dfv2-tests") {
  deps = [ ":namespace-dfv2-tests-bin" ]
}

group("tests") {
  testonly = true
  deps = [
    ":namespace-dfv1-tests",
    ":namespace-dfv2-tests",
  ]
}
