# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/magma/gnbuild/magma.gni")

visibility_list = [
  "//src/graphics/lib/magma/*",
  "//src/graphics/magma/*",
  "//src/graphics/drivers/*",
  "//third_party/imgtec-pvr-rgx-km/*",
]

declare_args() {
  magma_debug = is_debug
}

config("magma_util_config") {
  if (magma_debug) {
    defines = [ "MAGMA_DEBUG_INTERNAL_USE_ONLY=1" ]
  } else {
    defines = [ "MAGMA_DEBUG_INTERNAL_USE_ONLY=0" ]
  }
  configs = [ "../../..:src_config" ]
}

source_set("macros") {
  visibility = visibility_list

  public_configs = [
    ":magma_util_config",
    "$magma_build_root:magma_src_include_config",
  ]
  public_deps = [ "//src/graphics/magma/lib/magma/platform:logger" ]

  sources = [
    "dlog.h",
    "macros.h",
    "utils.h",
  ]
}

source_set("status") {
  visibility = visibility_list

  public_configs = [ "$magma_build_root:magma_src_include_config" ]

  sources = [ "status.h" ]

  public_deps = [ "//sdk/lib/magma_common" ]
}

source_set("thread") {
  visibility = visibility_list

  public_configs = [ "$magma_build_root:magma_src_include_config" ]

  sources = [ "thread.h" ]

  deps = [ "//src/graphics/magma/lib/magma/platform:thread" ]
}

source_set("short_macros") {
  # The short macros should only be used in leaf code that isn't included into other consumers.
  visibility = [
    "//src/graphics/drivers/*",
    "//src/graphics/lib/magma/src/libmagma:*",
    "//src/graphics/lib/magma/src/libmagma_virt:*",
    "//src/graphics/magma/lib/magma/platform/zircon:*",
    "//src/graphics/magma/lib/magma_client/mock:*",
    "//src/graphics/magma/lib/magma_service/mock:*",
    "//src/graphics/magma/lib/magma_service/sys_driver/dfv1:*",
    "//src/graphics/magma/lib/magma_service/sys_driver/tests:*",
    "//src/graphics/magma/tests/msd_conformance_tests:*",
    "//src/graphics/magma/tests/unit_tests:*",
    "//third_party/imgtec-pvr-rgx-km/*",
  ]
  sources = [ "short_macros.h" ]
  public_deps = [ ":macros" ]
}
