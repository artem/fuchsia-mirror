# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/magma/gnbuild/magma.gni")

source_set("buffer") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_buffer.cc",
    "zircon_platform_buffer.h",
  ]

  public_deps = [
    "..:buffer_header",
    "//zircon/system/ulib/zx",
  ]

  deps = [
    "..:object",
    "//src/graphics/magma/lib/magma/platform:trace",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
  ]
}

source_set("bus_mapper") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_bus_mapper.cc",
    "zircon_platform_bus_mapper.h",
  ]

  public_deps = [
    ":buffer",
    ":handle",
    "//src/graphics/magma/lib/magma/platform:bus_mapper_header",
  ]

  deps = [
    "..:logger",
    "..:object",
    "//src/graphics/magma/lib/magma/platform:trace",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
  ]
}

source_set("interrupt") {
  sources = [ "zircon_platform_interrupt.h" ]

  public_deps = [
    ":port",
    "..:interrupt_header",
    "//src/graphics/magma/lib/magma/util:short_macros",
    "//zircon/system/ulib/zx",
  ]
}

source_set("mmio") {
  configs += [ "..:platform_include_config" ]
  sources = [
    "zircon_platform_mmio.cc",
    "zircon_platform_mmio.h",
  ]
  deps = [
    "..:mmio_header",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
  ]
  public_deps = [ "//src/devices/lib/mmio" ]
}

source_set("device") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_device.cc",
    "zircon_platform_device.h",
  ]

  public_deps = [
    ":interrupt",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
  ]

  deps = [
    ":bus_mapper",
    ":handle",
    ":mmio",
    "..:device_header",
    "//src/devices/lib/driver",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
    "//zircon/system/ulib/zx",
  ]
}

source_set("connection_client") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_connection_client.cc",
    "zircon_platform_connection_client.h",
  ]

  public_deps = [
    ":handle",
    ":status",
    "..:connection_client_header",
    "..:trace",
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp",
    "//src/graphics/magma/lib/magma/util:macros",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/zx",
  ]

  deps = [ "//src/graphics/magma/lib/magma/util:short_macros" ]

  # Fidl bindings should not introduce a dep on libasync-default.so
  assert_no_deps = [ "//zircon/system/ulib/async-default" ]
}

source_set("device_client") {
  configs += [ "..:platform_include_config" ]

  sources = [ "zircon_platform_device_client.cc" ]

  deps = [
    "..:device_client_header",
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
    "//src/graphics/magma/lib/magma/util:status",
    "//zircon/system/ulib/zx",
  ]
}

source_set("sysmem_connection") {
  configs += [ "..:platform_include_config" ]

  sources = [ "zircon_platform_sysmem_connection.cc" ]

  deps = [
    ":buffer",
    "..:sysmem_connection_header",
    "..:thread",
    "..:trace",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp",
    "//src/graphics/magma/lib/magma/util:short_macros",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/zx",
  ]

  # Fidl bindings should not introduce a dep on libasync-default.so
  assert_no_deps = [ "//zircon/system/ulib/async-default" ]

  # TODO(https://fxbug.dev/42176699): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

source_set("thread") {
  configs += [ "..:platform_include_config" ]

  sources = [ "zircon_platform_thread.cc" ]

  deps = [
    "..:object",
    "..:thread_header",
  ]
}

source_set("thread_profile") {
  configs += [ "..:platform_include_config" ]

  sources = [ "zircon_platform_thread_profile.cc" ]

  deps = [
    "..:thread_header",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
    "//src/lib/ddk",
  ]
}

source_set("event") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_event.cc",
    "zircon_platform_event.h",
  ]

  deps = [
    "..:event_header",
    "//src/graphics/magma/lib/magma/util:macros",
    "//zircon/system/ulib/zx",
  ]
  public_deps = [ "//src/graphics/magma/lib/magma/util:short_macros" ]
}

source_set("handle") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_handle.cc",
    "zircon_platform_handle.h",
  ]

  public_deps = [
    ":port",
    "..:handle_header",
    "//src/graphics/magma/lib/magma/util:macros",
    "//zircon/system/ulib/zx",
  ]

  deps = [
    "..:object",
    "//src/graphics/magma/lib/magma/util:macros",
  ]
}

source_set("iommu") {
  configs += [ "..:platform_include_config" ]

  sources = [ "zircon_platform_iommu.cc" ]

  deps = [ "..:iommu_header" ]
}

source_set("logger") {
  public_deps = [
    "..:logger_header",
    "..:logger_provider_header",
  ]

  sources = [ "zircon_platform_logger_syslog.cc" ]

  deps = [
    ":handle",
    ":thread",
    "..:thread_header",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_cpp",
    "//sdk/lib/syslog/structured_backend",
    "//zircon/system/ulib/zx",
  ]
}

source_set("logger_for_system_drivers") {
  public_deps = [ "..:logger_header" ]

  sources = [ "zircon_platform_logger_ddk.cc" ]

  deps = [ "//src/lib/ddk" ]
}

source_set("logger_for_dfv2") {
  public_deps = [ "..:logger_header" ]

  sources = [
    "zircon_platform_logger_dfv2.cc",
    "zircon_platform_logger_dfv2.h",
  ]
  deps = [
    "//sdk/lib/driver/logging/cpp",
    "//src/graphics/magma/lib/magma/util:macros",
  ]
}

source_set("object") {
  configs += [ "..:platform_include_config" ]

  sources = [ "zircon_platform_object.cc" ]

  deps = [
    "..:object_header",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
    "//zircon/system/ulib/zx",
  ]
}

source_set("semaphore") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_semaphore.cc",
    "zircon_platform_semaphore.h",
    "zircon_vmo_semaphore.cc",
    "zircon_vmo_semaphore.h",
  ]

  public_deps = [
    "..:semaphore_header",
    "..:trace",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
    "//zircon/system/ulib/zx",
  ]

  deps = [
    ":port",
    "..:object",
  ]
}

source_set("port") {
  configs += [ "..:platform_include_config" ]

  sources = [
    "zircon_platform_port.cc",
    "zircon_platform_port.h",
  ]

  public_deps = [
    "..:port_header",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
  ]

  deps = [ "//zircon/system/ulib/zx" ]
}

source_set("trace") {
  configs += [
    "..:platform_include_config",
    "..:tracing",
  ]

  sources = [ "zircon_platform_trace.cc" ]

  deps = [ "..:trace_header" ]
}

source_set("trace_provider") {
  configs += [
    "..:platform_include_config",
    "..:tracing",
  ]

  sources = [
    "zircon_platform_trace_provider.cc",
    "zircon_platform_trace_provider.h",
  ]

  deps = [
    "..:trace_provider_header",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
    "//zircon/system/ulib/async:async-cpp",
  ]

  public_deps = [
    "//sdk/lib/fit",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/trace-provider:trace-provider-with-static-engine",
  ]

  assert_no_deps = [
    "//sdk/lib/fdio",
    "//zircon/system/ulib/async-default",
  ]
}

source_set("trace_provider_with_fdio") {
  configs += [
    "..:platform_include_config",
    "..:tracing",
  ]

  sources = [ "zircon_platform_trace_provider_with_fdio.cc" ]

  deps = [
    ":trace_provider",
    "..:trace_provider_with_fdio_header",
    "//sdk/lib/fdio",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
  ]

  public_deps = [ "//sdk/lib/fit" ]
}

source_set("status") {
  sources = [ "zircon_platform_status.h" ]

  public_deps = [
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:status",
  ]
}
