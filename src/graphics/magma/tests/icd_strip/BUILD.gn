# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

magma_vulkan_icd("icd_strip_test") {
  output_name = "libicd_strip_test"
  sources = [ "icd.c" ]
  deps = [ "//src/lib/vulkan:headers" ]

  # Required
  imported_symbols_allowlist =
      rebase_path("//src/graphics/lib/magma/gnbuild/imported_symbols.allowlist")
}

test("test_icd_strip_bin") {
  output_name = "test_icd_strip"

  sources = [ "test_icd_strip.cc" ]

  deps = [
    ":icd_strip_test",
    "//third_party/googletest:gtest",
  ]

  # Instrumented variants adds symbols (e.g. `__asan_init`)
  # which are not in the allowlist and will fail the build
  # so disable these.
  exclude_toolchain_tags = [ "instrumented" ]
}

fuchsia_unittest_package("test_icd_strip") {
  deps = [ ":test_icd_strip_bin" ]
}

group("icd_strip") {
  testonly = true
  deps = [ ":test_icd_strip" ]
}
