# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/magma/gnbuild/magma.gni")

executable("msd_intel_gen_nonhardware_tests") {
  testonly = true

  sources = []

  deps = []
  public_deps = [
    ":nonhardware_tests",
    "//src/devices/testing/no_ddk",
    "//src/graphics/magma/lib/magma/test_util:logger_init_helper",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-vthread",
  ]
}

# These tests don't require Intel graphics hardware to run, so they
# can be run under QEMU.
source_set("nonhardware_tests") {
  testonly = true

  sources = [
    "test_buffer.cc",
    "test_cache_config.cc",
    "test_connection.cc",
    "test_context.cc",
    "test_engine_command_streamer.cc",
    "test_forcewake.cc",
    "test_hardware_status_page.cc",
    "test_instruction_decoder.cc",
    "test_instructions.cc",
    "test_ppgtt.cc",
    "test_progress.cc",
    "test_register_io.cc",
    "test_render_init_batch.cc",
    "test_scheduler.cc",
    "test_semaphore.cc",
    "test_sequencer.cc",
    "test_timestamp.cc",
    "test_workarounds.cc",
  ]

  deps = [ ":test_deps" ]
}

# These tests do require Intel graphics hardware to run.  They can be
# run inside the test build of the driver.
source_set("hardware_tests") {
  testonly = true

  sources = [
    "test_hw_command_buffer.cc",
    "test_hw_device.cc",
    "test_hw_exec.cc",
  ]

  deps = [ ":test_deps" ]
}

group("test_deps") {
  testonly = true

  public_deps = [
    "//sdk/lib/fit",
    "//src/graphics/drivers/msd-intel-gen/include",
    "//src/graphics/drivers/msd-intel-gen/src",
    "//src/graphics/drivers/msd-intel-gen/tests/mock",
    "//src/graphics/magma:msd",
    "//src/graphics/magma/lib/magma/platform:semaphore",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma_service/mock:address_space",
    "//src/graphics/magma/lib/magma_service/mock:bus_mapper",
    "//src/graphics/magma/lib/magma_service/mock:mmio",
    "//src/graphics/magma/lib/magma_service/test_util:command_buffer_helper_cpp",
    "//src/graphics/magma/lib/magma_service/test_util:platform_pci_device_helper",
    "//src/graphics/magma/lib/magma_service/util:address_space",
    "//src/graphics/magma/lib/magma_service/util:registers",
    "//third_party/googletest:gtest",
  ]
}
