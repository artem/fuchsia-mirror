# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_macro.gni")
import("//build/toolchain/rbe.gni")
import("//build/tools/json_merge/json_merge.gni")
import("//src/developer/ffx/config.gni")

config_target = "//src/developer/ffx:ffx-defaults.json"
config_path =
    get_label_info(config_target, "target_out_dir") + "/ffx-defaults.json"

# This path can now be relative to where rustc is invoked.
rebased_config_path = rebase_path(config_path, root_build_dir)

rustc_macro("config-proc-macros") {
  name = "config_proc_macros"
  version = "0.1.0"
  edition = "2021"

  rustenv = [ "FFX_DEFAULT_CONFIG_JSON=$rebased_config_path" ]

  deps = [
    "//src/lib/include_str_from_working_dir",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:proc-macro2",
    "//third_party/rust_crates:quote",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:syn",
    config_target,
  ]

  sources = [ "src/lib.rs" ]
  inputs = [ config_path ]
}
