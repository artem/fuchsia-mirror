# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/compiled_action.gni")
import("//build/host.gni")
import("//build/rust/rustc_binary.gni")
import("//build/testing/golden_files.gni")

if (is_host && has_board) {
  rustc_binary("ffx_cli_goldens") {
    edition = "2021"
    testonly = true
    with_unit_tests = true
    output_name = "ffx_cli_goldens"
    sources = [ "src/main.rs" ]
    data_deps = [ ":sdk_exported_for_ffx" ]

    deps = [
      "//src/developer/ffx/command:lib",
      "//src/developer/ffx/config:lib",
      "//src/developer/ffx/lib/isolate:lib",
      "//src/lib/fuchsia",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:argh",
      "//third_party/rust_crates:serde",
      "//third_party/rust_crates:serde_json",
      "//third_party/rust_crates:tempfile",
    ]
  }

  # Exported SDK with ffx binary.
  _sdk_label = "//sdk:core($default_toolchain)"
  _sdk_outdir = get_label_info(_sdk_label, "root_out_dir")
  _sdk_path = _sdk_outdir + "/sdk/exported/core"

  host_test_data("sdk_exported_for_ffx") {
    sources = [ _sdk_path ]
    deps = [ _sdk_label ]
  }

  _golden_dir = "${target_out_dir}/goldens"
  _comparisons_file = "${target_gen_dir}/ffx_cli_comparisions.json"

  golden_files("ffx-golden-test") {
    testonly = true

    comparisons = _comparisons_file

    deps = [ ":invoke_ffx_cli_goldens($host_toolchain)" ]
  }

  compiled_action("invoke_ffx_cli_goldens") {
    testonly = true
    tool = ":ffx_cli_goldens"
    depfile = "$target_out_dir/$target_name.d"
    deps = []

    outputs = [
      "$_golden_dir/ffx.golden",
      _comparisons_file,
    ]
    args = [
      "--out-dir",
      rebase_path(_golden_dir, root_build_dir),
      "--base-golden-src-dir",
      rebase_path("goldens", "//"),
      "--gen-comparisons",
      rebase_path(_comparisons_file, root_build_dir),
      "--sdk-root",
      rebase_path(_sdk_path, root_build_dir),
    ]
  }
}

group("e2e_tests") {
  testonly = true

  # TODO(http://fxbug.dev/124461): Support product bundle for testing on mac
  # For now, just test on linux.
  # Also check for has_board, since this test cannot run on a configuration without a
  # board.
  if (is_host && is_linux && has_board) {
    # TODO(wilkinsonclay): In a stacked CL, enable this test. It will require adding all the
    # golden files, so it will be a large CL.
    # deps = [ ":ffx-golden-test" ]
  }
}
