# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/python/python_binary.gni")
import("//build/python/python_c_extension.gni")
import("//build/python/python_host_test.gni")
import("//build/python/python_library.gni")
import("//build/rust/rustc_staticlib.gni")
import("//src/tests/fidl/conformance_suite/gidl-conformance-suite.gni")
import("//tools/fidl/gidl/gidl.gni")

assert(is_host)

conformance_test_source = "$target_gen_dir/conformance_test.py"

gidl("conformance_tests_py") {
  type = "conformance"
  language = "fuchsia_controller"

  # Currently missing flexible enum tests, float tests, handle tests, protocol endpoint tests,
  # recursive depth tests, strings_utf8.gen tests, transformer1 tests, and union tests.
  #
  # All tests are currently running with encode-success targets only.
  inputs = [
    "//src/tests/fidl/conformance_suite/alignment.gidl",
    "//src/tests/fidl/conformance_suite/bits_flexible.gidl",
    "//src/tests/fidl/conformance_suite/bits_strict.gidl",
    "//src/tests/fidl/conformance_suite/bool.gidl",
    "//src/tests/fidl/conformance_suite/conformance.gidl",
    "//src/tests/fidl/conformance_suite/empty_struct.gidl",
    "//src/tests/fidl/conformance_suite/enums_strict.gidl",
    "//src/tests/fidl/conformance_suite/golden.gidl",
    "//src/tests/fidl/conformance_suite/integer.gidl",
    "//src/tests/fidl/conformance_suite/memcpy_compatibility.gidl",
    "//src/tests/fidl/conformance_suite/padding.gidl",
    "//src/tests/fidl/conformance_suite/ported_from_walker_handles.gidl",
    "//src/tests/fidl/conformance_suite/ported_from_walker_strings.gidl",
    "//src/tests/fidl/conformance_suite/ported_from_walker_vectors.gidl",
    "//src/tests/fidl/conformance_suite/recursive_types.gidl",
    "//src/tests/fidl/conformance_suite/strings.gidl",
    "//src/tests/fidl/conformance_suite/tables.gidl",
    "//src/tests/fidl/conformance_suite/transformer2.gidl",
    "//src/tests/fidl/conformance_suite/vectors.gidl",
  ]
  fidl = conformance_suite_fidl_target
  output = conformance_test_source
  visibility = [ ":*" ]
}

python_host_test("fuchsia_controller_conformance_tests") {
  libraries = [
    ":fidl_bindings",
    ":fuchsia_controller_py",
  ]
  deps = [ ":conformance_tests_py" ]
  test_data_deps = [
    "tests:fuchsia_controller_host_test_data",
    "//src/tests/fidl/conformance_suite:conformance_fidl_host_test_data",
  ]
  main_source = conformance_test_source
}

rustc_staticlib("lib") {
  output_name = "fuchsia_controller"
  edition = "2021"
  with_unit_tests = true

  test_deps = [ "//third_party/rust_crates:futures-test" ]
  deps = [
    "//src/connectivity/overnet/lib/core",
    "//src/developer/ffx/config:lib",
    "//src/developer/ffx/core:lib",
    "//src/developer/ffx/daemon:lib",
    "//src/developer/ffx/daemon/proxy:lib",
    "//src/developer/ffx/fidl:fuchsia.developer.ffx_rust",
    "//src/developer/ffx/lib/errors:lib",
    "//src/developer/ffx/lib/netext:lib",
    "//src/developer/ffx/lib/rcs:lib",
    "//src/developer/ffx/lib/target:lib",
    "//src/developer/ffx/lib/timeout:lib",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/versioning/version-history/rust:version-history",
    "//src/lib/zircon/rust:fuchsia-zircon-status",
    "//src/lib/zircon/rust:fuchsia-zircon-types",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-channel",
    "//third_party/rust_crates:async-lock",
    "//third_party/rust_crates:async-net",
    "//third_party/rust_crates:byteorder",
    "//third_party/rust_crates:camino",
    "//third_party/rust_crates:futures-lite",
    "//third_party/rust_crates:nix",
    "//third_party/rust_crates:tracing",
  ]

  sources = [
    "src/commands.rs",
    "src/env_context.rs",
    "src/ext_buffer.rs",
    "src/lib.rs",
    "src/lib_context.rs",
    "src/waker.rs",
  ]
}

python_c_extension("fidl_codec") {
  sources = [
    "cpp/abi/convert.h",
    "cpp/abi/macros.h",
    "cpp/fidl_codec/decode.cc",
    "cpp/fidl_codec/decode.h",
    "cpp/fidl_codec/encode.cc",
    "cpp/fidl_codec/encode.h",
    "cpp/fidl_codec/fidl_codec.cc",
    "cpp/fidl_codec/ir.cc",
    "cpp/fidl_codec/ir.h",
    "cpp/fidl_codec/mod.cc",
    "cpp/fidl_codec/mod.h",
    "cpp/fidl_codec/object_converter.cc",
    "cpp/fidl_codec/object_converter.h",
    "cpp/fidl_codec/python_dict_visitor.h",
    "cpp/python/py_header.h",
    "cpp/raii/py_wrapper.h",
  ]

  deps = [
    "//sdk/lib/fidl_base",
    "//src/lib/fidl_codec",
    "//third_party/rapidjson",
    "//zircon/system/public",
  ]
}

python_c_extension("fuchsia_controller_internal") {
  sources = [
    "cpp/abi/convert.h",
    "cpp/abi/error.cc",
    "cpp/abi/error.h",
    "cpp/abi/fuchsia_controller.h",
    "cpp/abi/fuchsia_controller_py.cc",
    "cpp/abi/macros.h",
    "cpp/abi/mod.cc",
    "cpp/abi/mod.h",
    "cpp/python/py_header.h",
    "cpp/raii/py_wrapper.h",
  ]

  deps = [
    ":lib",
    "//zircon/system/public",
  ]

  # make sure no fidl libraries not in the SDK are used.
  assert_no_deps = [
    "//sdk:marker-internal",
    "//sdk:marker-unknown",
  ]

  # TODO(https://fxbug.dev/135694): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-vla-cxx-extension" ]
}

python_library("fuchsia_controller_py") {
  library_name = "fuchsia_controller_py"
  source_root = "python/fuchsia_controller_py"
  library_deps = [
    ":fidl_codec",
    ":fuchsia_controller_internal",
  ]
  sources = [ "__init__.py" ]
}

python_library("fidl_bindings") {
  library_name = "fidl"
  source_root = "python/fidl"
  library_deps = [
    ":fidl_codec",
    ":fuchsia_controller_py",
  ]
  sources = [
    "__init__.py",
    "_async_socket.py",
    "_client.py",
    "_fidl_common.py",
    "_import.py",
    "_ipc.py",
    "_library.py",
    "_server.py",
  ]
}

python_binary("example") {
  main_source = "python/example.py"
  deps = [
    ":fidl_bindings",
    ":fidl_codec",
    ":fuchsia_controller_py",
    "//src/developer/ffx:ffx_tool",
  ]
}

group("fuchsia-controller") {
  testonly = true
  deps = [
    ":example",
    ":fidl_codec",
    ":fuchsia_controller_py",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":lib_test",
    "tests:fuchsia_controller_encode_test",
    "tests:fuchsia_controller_ir_test",
  ]

  # TODO(fxbug.dev/127523): These tests fail on mac builders. This is most likely
  # because of the infra machines running OSX 10.15
  if (host_os != "mac") {
    deps += [
      ":fuchsia_controller_conformance_tests",
      "tests:fuchsia_controller_errors_test",
      "tests:fuchsia_controller_fidl_channel_test",
      "tests:fuchsia_controller_fidl_client_test",
      "tests:fuchsia_controller_fidl_common_test",
      "tests:fuchsia_controller_fidl_server_test",
      "tests:fuchsia_controller_importing_test",
      "tests:fuchsia_controller_isolate_directory_test",
      "tests:fuchsia_controller_socket_test",
    ]

    # TODO(https://fxbug.dev/129633): Re-enable when the corrupt profile header
    # issue is fixed.
    if (!is_coverage) {
      deps += [ "tests:fuchsia_controller_e2e_test" ]
    }
  }
}
