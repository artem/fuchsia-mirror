# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")
import("//build/testing/test_spec.gni")

host_test_data("empty-repo-keys") {
  sources = [
    "//src/sys/pkg/tests/pkg-resolver/template/keys/root.json",
    "//src/sys/pkg/tests/pkg-resolver/template/keys/snapshot.json",
    "//src/sys/pkg/tests/pkg-resolver/template/keys/targets.json",
    "//src/sys/pkg/tests/pkg-resolver/template/keys/timestamp.json",
  ]
  outputs = [
    "$root_out_dir/test_data/ffx_lib_pkg/empty-repo/keys/{{source_file_part}}",
  ]
}

host_test_data("empty-repo-metadata") {
  sources = [
    "//src/sys/pkg/tests/pkg-resolver/template/repository/1.root.json",
    "//src/sys/pkg/tests/pkg-resolver/template/repository/1.snapshot.json",
    "//src/sys/pkg/tests/pkg-resolver/template/repository/1.targets.json",
    "//src/sys/pkg/tests/pkg-resolver/template/repository/root.json",
    "//src/sys/pkg/tests/pkg-resolver/template/repository/snapshot.json",
    "//src/sys/pkg/tests/pkg-resolver/template/repository/targets.json",
    "//src/sys/pkg/tests/pkg-resolver/template/repository/timestamp.json",
  ]
  outputs = [ "$root_out_dir/test_data/ffx_lib_pkg/empty-repo/repository/{{source_file_part}}" ]
}

relative_root_out_dir = rebase_path(root_out_dir, root_build_dir)
rustc_library("pkg") {
  name = "pkg"
  edition = "2021"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg_rust",
    "//sdk/fidl/fuchsia.pkg.rewrite:fuchsia.pkg.rewrite_rust",
    "//src/developer/ffx/config:lib",
    "//src/developer/ffx/fidl:fuchsia.developer.ffx_rust",
    "//src/developer/ffx/lib/fidl-fuchsia-developer-ffx-ext",
    "//src/developer/ffx/lib/protocols:lib",
    "//src/developer/ffx/lib/rcs:lib",
    "//src/lib/analytics/rust:lib",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-hyper",
    "//src/lib/fuchsia-url",
    "//src/lib/zircon/rust:fuchsia-zircon-status",
    "//src/sys/lib/fidl-fuchsia-pkg-rewrite-ext",
    "//src/sys/pkg/lib/fuchsia-repo",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-lock",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:percent-encoding",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tracing",
    "//third_party/rust_crates:tuf",
    "//third_party/rust_crates:url",
  ]

  test_deps = [
    ":empty-repo-metadata",
    "//src/lib/fuchsia",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:maplit",
    "//third_party/rust_crates:pretty_assertions",
  ]

  sources = [
    "src/config.rs",
    "src/lib.rs",
    "src/metrics.rs",
    "src/repo.rs",
    "src/tunnel.rs",
  ]

  rustenv = [ "ROOT_OUT_DIR=$relative_root_out_dir" ]
}

group("tests") {
  testonly = true
  deps = [ ":pkg_test($host_toolchain)" ]
}
