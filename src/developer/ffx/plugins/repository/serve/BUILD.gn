# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/testing/host_test_data.gni")
import("//src/developer/ffx/build/ffx_plugin.gni")

if (is_host) {
  host_test_data("empty-repo") {
    sources = [
      "//src/sys/pkg/tests/pkg-resolver/template/repository/1.root.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/1.snapshot.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/1.targets.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/root.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/snapshot.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/targets.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/timestamp.json",
    ]
    outputs = [ "$root_out_dir/test_data/ffx_plugin_serve_repo/empty-repo/repository/{{source_file_part}}" ]
  }
}

relative_root_out_dir = rebase_path(root_out_dir, root_build_dir)
ffx_plugin("ffx_repository_serve") {
  version = "0.1.0"
  edition = "2021"
  with_unit_tests = true

  args_deps = [ "//src/developer/ffx/fidl:fuchsia.developer.ffx_rust" ]
  args_sources = [ "src/args.rs" ]

  deps = [
    "//sdk/fidl/fuchsia.developer.remotecontrol:fuchsia.developer.remotecontrol_rust",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg_rust",
    "//sdk/fidl/fuchsia.pkg.rewrite:fuchsia.pkg.rewrite_rust",
    "//src/developer/ffx/config:lib",
    "//src/developer/ffx/fidl:fuchsia.developer.ffx_rust",
    "//src/developer/ffx/lib/fho:lib",
    "//src/developer/ffx/lib/fidl-fuchsia-developer-ffx-ext",
    "//src/developer/ffx/lib/pkg",
    "//src/developer/ffx/lib/rcs:lib",
    "//src/developer/ffx/lib/target:lib",
    "//src/developer/ffx/lib/timeout:lib",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/sys/pkg/lib/fuchsia-repo",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-lock",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
  test_deps = [
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//src/sys/lib/fidl-fuchsia-pkg-rewrite-ext",
  ]
  if (is_host) {
    test_deps += [
      ":empty-repo",
      "//src/lib/fuchsia-hyper",
      "//third_party/rust_crates:assert_matches",
      "//third_party/rust_crates:tempfile",
      "//third_party/rust_crates:url",
    ]
  }

  sources = [ "src/lib.rs" ]
  rustenv = [ "ROOT_OUT_DIR=$relative_root_out_dir" ]
}
