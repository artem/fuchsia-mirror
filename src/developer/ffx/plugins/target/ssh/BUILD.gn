# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/developer/ffx/build/ffx_plugin.gni")
import("//src/developer/ffx/build/ffx_tool.gni")

ffx_plugin("ffx_target_ssh") {
  version = "0.1.0"
  edition = "2021"
  with_unit_tests = true
  args_with_unit_tests = true
  deps = [
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//src/developer/ffx/config:lib",
    "//src/developer/ffx/fidl:fuchsia.developer.ffx_rust",
    "//src/developer/ffx/lib/addr:lib",
    "//src/developer/ffx/lib/fho:lib",
    "//src/developer/ffx/lib/ssh:lib",
    "//src/developer/ffx/lib/timeout:lib",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:nix",
    "//third_party/rust_crates:tracing",
  ]
  test_deps = [
    "//src/developer/ffx/config:lib",
    "//src/developer/ffx/fidl:fuchsia.developer.ffx_rust",
    "//src/developer/ffx/lib/timeout:lib",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:itertools",
    "//third_party/rust_crates:pretty_assertions",
    "//third_party/rust_crates:serde_json",
  ]

  args_sources = [ "src/args.rs" ]

  sources = [ "src/lib.rs" ]
}

if (is_host) {
  group("host_tests") {
    testonly = true
    if (has_board) {
      deps = [ ":ffx_target_ssh_e2e_tests" ]
    }
  }

  if (has_board) {
    rustc_test("ffx_target_ssh_e2e_tests") {
      edition = "2021"
      source_root = "tests/e2e.rs"
      sources = [ source_root ]
      deps = [
        "//src/developer/ffx/lib/e2e_emu:ffx_e2e_emu",
        "//src/lib/fuchsia",
        "//third_party/rust_crates:anyhow",
        "//third_party/rust_crates:pretty_assertions",
      ]
    }
  }
}
