# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/perf/test.gni")
import("//src/diagnostics/archivist/build/archivist.gni")

archivist_optimized_binary("logging-bin") {
  output_name = "archivist_logging_benchmarks"
  edition = "2018"

  source_root = "src/logging.rs"

  deps = [
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_rust",
    "//src/developer/fuchsia-criterion",
    "//src/diagnostics/archivist:lib",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "src/logging.rs" ]
}

archivist_optimized_binary("formatter-bin") {
  output_name = "archivist_formatter_benchmarks"
  edition = "2018"

  source_root = "src/formatter.rs"

  deps = [
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_rust",
    "//src/developer/fuchsia-criterion",
    "//src/diagnostics/archivist:lib",
    "//src/lib/diagnostics/data/rust",
    "//src/lib/diagnostics/hierarchy/rust",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "src/formatter.rs" ]
}

_formatter_benchmark_component = "formatter"
_logging_benchmark_component = "logging"

fuchsia_component("formatter-benchmarks-component") {
  component_name = _formatter_benchmark_component
  deps = [ ":formatter-bin" ]
  manifest = "meta/formatter.cml"
}

fuchsia_component("logging-benchmarks-component") {
  component_name = _logging_benchmark_component
  deps = [ ":logging-bin" ]
  manifest = "meta/logging.cml"
}

fuchsia_package("archivist-benchmarks") {
  deps = [
    ":formatter-benchmarks-component",
    ":logging-benchmarks-component",
  ]
}

fuchsia_component_perf_test("archivist-formatter-benchmark") {
  package = ":archivist-benchmarks"
  component_name = _formatter_benchmark_component
  expected_metric_names_filepath = "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.archivist.formatter.txt"
}

fuchsia_component_perf_test("archivist-logging-benchmark") {
  package = ":archivist-benchmarks"
  component_name = _logging_benchmark_component
  expected_metric_names_filepath = "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.archivist.logging.txt"
}

group("bench") {
  testonly = true
  deps = [
    ":archivist-formatter-benchmark",
    ":archivist-logging-benchmark",
  ]
}
