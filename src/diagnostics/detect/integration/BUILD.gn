# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This directory was based on fuchsia-mirror/src/sys/component_manager/tests/utc-time/

import("//build/component/config.gni")
import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")

group("integration") {
  testonly = true
  deps = [ ":tests" ]
}

group("tests") {
  testonly = true
  deps = [ ":detect-integration-test" ]
}

rustc_test("integration_test_bin") {
  edition = "2021"
  source_root = "integration_test.rs"
  name = "detect_integration_test"
  deps = [
    "//sdk/fidl/fuchsia.testing.harness:fuchsia.testing.harness_rust",
    "//src/diagnostics/detect/testing/fidl/fuchsia.diagnostics.test:fuchsia.diagnostics.test_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/testing/realm_proxy",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:test-case",
    "//third_party/rust_crates:tracing",
  ]

  sources = [
    "integration_test.rs",
    "test_filing_enable.rs",
    "test_snapshot_sanitizing.rs",
    "test_snapshot_throttle.rs",
    "test_trigger_truth.rs",
  ]
}

fuchsia_component("test-suite-component") {
  testonly = true
  component_name = "test-suite"
  manifest = "meta/integration-test.cml"
  deps = [ ":integration_test_bin" ]
}

fuchsia_package("test-suite") {
  testonly = true
  deps = [ ":test-suite-component" ]
}

fuchsia_component("test-root") {
  testonly = true
  manifest = "meta/test-root.cml"
  deps = [ ":integration_test_bin" ]
}

# Run with `fx test detect-integration-test`.
fuchsia_test_package("detect-integration-test") {
  test_components = [ ":test-root" ]
  subpackages = [
    "//src/diagnostics/detect/testing/realm-factory:triage-detect-realm-factory",
    ":test-suite",
  ]
}
