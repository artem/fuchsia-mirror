# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

_common_deps = [
  "//sdk/fidl/fuchsia.hardware.radar:fuchsia.hardware.radar_cpp",
  "//sdk/lib/component/incoming/cpp",
  "//sdk/lib/fdio",
  "//sdk/lib/fit",
  "//zircon/system/ulib/async-loop:async-loop-cpp",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/zxtest",
]

executable("bin") {
  testonly = true
  output_name = "radar-integration-test"
  sources = [ "radar-integration-test.cc" ]
  deps = _common_deps
}

fuchsia_test_component("component") {
  deps = [ ":bin" ]
  manifest = "meta/manifest.cml"
  test_type = "device"
}

executable("injection-bin") {
  testonly = true
  output_name = "radar-injection-integration-test"
  sources = [ "radar-injection-integration-test.cc" ]
  deps = _common_deps + [ "//zircon/system/ulib/zx" ]
}

fuchsia_test_component("injection-component") {
  deps = [ ":injection-bin" ]
  manifest = "meta/injection-manifest.cml"
  test_type = "device"
}

fuchsia_test_package("radar-integration-test") {
  test_components = [ ":component" ]
  deps = [ "//src/devices/radar/bin/radar-proxy:radar-proxy-component-without-injector" ]
  test_specs = {
    environments = [ nelson_env ]
  }
}

fuchsia_test_package("radar-integration-reader-proxying-test") {
  test_components = [
    ":component",
    ":injection-component",
  ]
  deps = [
    "//src/devices/radar/bin/radar-proxy:radar-proxy-component-with-injector",
  ]
  test_specs = {
    environments = [ nelson_env ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":radar-integration-reader-proxying-test",
    ":radar-integration-test",
  ]
}
