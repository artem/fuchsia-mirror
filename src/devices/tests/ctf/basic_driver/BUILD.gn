# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/component/config.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")
import("//sdk/ctf/build/ctf.gni")

group("tests") {
  testonly = true
  deps = [ ":basic-driver-test-latest" ]
}

group("ctf-tests") {
  testonly = true
  deps = [ ":basic-driver-test_archive" ]
}

group("drivers") {
  testonly = true
  deps = [ ":basic-driver-component" ]
}

fidl("fuchsia.basicdriver.ctftest") {
  testonly = true
  sources = [ "basic_driver.test.fidl" ]
}

driver_bind_rules("bind") {
  rules = "src/basic.bind"
  bind_output = "basic.bindbc"
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

fuchsia_driver("basic-driver") {
  testonly = true
  output_name = "ctf_basic_driver"
  sources = [ "src/basic_driver.cc" ]
  deps = [
    ":bind",
    ":fuchsia.basicdriver.ctftest_cpp",
    "//sdk/lib/driver/component/cpp",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_driver_component("basic-driver-component") {
  testonly = true
  manifest = "meta/basic-driver.cml"
  component_name = "basic-driver"
  deps = [ ":basic-driver" ]
  info = "info.json"
}

rustc_test("bin") {
  edition = "2021"
  source_root = "src/main.rs"
  sources = [ source_root ]
  name = "basic_driver_test"
  deps = [
    ":fuchsia.basicdriver.ctftest_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/lib/device-watcher/rust",
    "//src/devices/testing/fidl:fuchsia.driver.testing_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/testing/realm_proxy",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_component("basic-driver-test-component") {
  testonly = true
  component_name = "test-suite"
  manifest = "meta/test-suite.cml"
  deps = [ ":bin" ]
}

ctf_fuchsia_package("basic-driver-test") {
  testonly = true
  package_name = "basic-driver-test"
  deps = [
    ":basic-driver-component",
    ":basic-driver-test-component",
  ]
}

fuchsia_test_component("basic-driver-test-root") {
  testonly = true
  manifest = "meta/test-root.cml"
  test_type = "ctf"
}

fuchsia_test_package("basic-driver-test-latest") {
  test_components = [ ":basic-driver-test-root" ]
  subpackages = [
    "//src/devices/testing/realm-factory",
    ":basic-driver-test",
  ]
}
