# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/drivers.gni")
import("//build/rust/rustc_test.gni")

fuchsia_driver_component("component") {
  testonly = true
  manifest = "meta/target_1_no_colocate.cml"
  component_name = "target_1_no_colocate"
  deps = [ "//src/devices/tests/v2/reload-driver/target-1:driver" ]
  info = "info.json"
}

group("tests") {
  testonly = true
  deps = [ ":replacedriver_test" ]
}

group("drivers") {
  testonly = true
  deps = [
    ":component",
    "target-1-replacement:component",
    "target-2-replacement:component",
    "//src/devices/tests/v2/reload-driver:drivers",
  ]
}

rustc_test("bin") {
  edition = "2021"
  testonly = true
  output_name = "replacedriver_bin"
  source_root = "test.rs"
  sources = [ "test.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.driver.development:fuchsia.driver.development_rust",
    "//sdk/fidl/fuchsia.driver.registrar:fuchsia.driver.registrar_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg_rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/devices/tests/v2/reload-driver:reloadtest_tools",
    "//src/devices/tests/v2/reload-driver/fidl:fuchsia.reloaddriver.test_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]
}

fuchsia_unittest_package("replacedriver_test") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  deps = [
    ":bin",
    ":component",  # target 1 with colocate = false
    "target-1-replacement:component",
    "target-2-replacement:component",
    "//sdk/lib/driver_test_realm:driver_test_realm_component",
    "//src/devices/bin/driver_host2:component",
    "//src/devices/tests/v2/reload-driver/composite:component",
    "//src/devices/tests/v2/reload-driver/leaf:component",
    "//src/devices/tests/v2/reload-driver/left-parent:component",
    "//src/devices/tests/v2/reload-driver/right-parent:component",
    "//src/devices/tests/v2/reload-driver/root:component",
    "//src/devices/tests/v2/reload-driver/target-2:component",
  ]
}
