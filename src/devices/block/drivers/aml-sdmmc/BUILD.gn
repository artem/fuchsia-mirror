# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bazel/drivers/bazel_driver_package.gni")
import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

bazel_driver_package("aml-sdmmc-no-banjo-bazel-package") {
  bazel_target = "//src/devices/block/drivers/aml-sdmmc:aml-sdmmc-no-banjo"
  package_name = "aml-sdmmc"
}

driver_bind_rules("aml-sdmmc-bind") {
  rules = "aml-sdmmc.bind"
  bind_output = "aml-sdmmc.bindbc"
  tests = "aml-sdmmc-bind-test.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_bindlib",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_bindlib",
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.pwm",
  ]
}

_common_deps = [
  ":aml-sdmmc-bind",
  ":aml-sdmmc-config",
  "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp",
  "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
  "//sdk/fidl/fuchsia.hardware.power:fuchsia.hardware.power_cpp",
  "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_cpp",
  "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
  "//sdk/lib/driver/compat/cpp",
  "//sdk/lib/driver/component/cpp:cpp",
  "//sdk/lib/driver/power/cpp",
  "//sdk/lib/fit",
  "//sdk/lib/inspect/component/cpp",
  "//src/devices/block/lib/sdio",
  "//src/devices/block/lib/sdmmc",
  "//src/devices/lib/amlogic",
  "//src/devices/lib/dma-buffer",
  "//src/devices/lib/mmio",
  "//src/lib/ddk:ddk_metadata",
  "//src/lib/vmo_store",
  "//zircon/system/ulib/ddk-platform-defs",
  "//zircon/system/ulib/hwreg",
  "//zircon/system/ulib/inspect",
  "//zircon/system/ulib/sync",
  "//zircon/system/ulib/trace",
  "//zircon/system/ulib/zircon-internal",
  "//zircon/system/ulib/zx",
]

fuchsia_driver("aml-sdmmc-driver") {
  output_name = "aml-sdmmc"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",

    # TODO(https://fxbug.dev/42085293): delete the below and fix compiler warnings
    "//build/config:Wno-vla-cxx-extension",
  ]
  sources = [
    "aml-sdmmc-with-banjo-export.cc",
    "aml-sdmmc-with-banjo.cc",
    "aml-sdmmc.cc",
  ]
  deps = _common_deps + [
           "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
           "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_cpp",
           "//sdk/lib/driver/runtime:driver_runtime_cpp",
           "//src/devices/bin/driver_runtime",
         ]
}

fuchsia_component_manifest("aml-sdmmc-manifest") {
  component_name = "aml-sdmmc"
  manifest = "meta/aml-sdmmc.cml"
}

fuchsia_driver_component("aml-sdmmc") {
  cm_label = ":aml-sdmmc-manifest"
  deps = [ ":aml-sdmmc-driver" ]
  info = "aml-sdmmc-info.json"
}

fuchsia_structured_config_cpp_elf_lib("aml-sdmmc-config") {
  cm_label = ":aml-sdmmc-manifest"
}

fuchsia_structured_config_values("sc-values") {
  cm_label = ":aml-sdmmc-manifest"
  values = {
    enable_suspend = false
  }
}

fuchsia_driver_package("package") {
  package_name = "aml-sdmmc"
  driver_components = [ ":aml-sdmmc" ]
  deps = [ ":sc-values" ]
}

test("aml-sdmmc-test-bin") {
  output_name = "aml-sdmmc-test"
  sources = [
    "aml-sdmmc-with-banjo-test.cc",
    "aml-sdmmc-with-banjo.cc",
    "aml-sdmmc.cc",
  ]
  deps =
      _common_deps + [
        "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
        "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_cpp",
        "//sdk/lib/async_patterns/testing/cpp",
        "//sdk/lib/driver/testing/cpp:cpp",
        "//sdk/lib/inspect/testing/cpp:zxtest",
        "//src/devices/bus/testing/fake-pdev",
        "//src/devices/lib/mmio:test_helper",
        "//src/devices/testing/fake-bti",
        "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
        "//zircon/system/ulib/zxtest",
      ]
}

fuchsia_unittest_package("aml-sdmmc-test") {
  deps = [ ":aml-sdmmc-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":aml-sdmmc-bind_test",
    ":aml-sdmmc-test",
  ]
}
