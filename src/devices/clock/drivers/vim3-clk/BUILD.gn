# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

driver_bind_rules("bind") {
  rules = "vim3-clk.bind"
  bind_output = "vim3-clk.bindbc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.platform",
  ]
}

fuchsia_driver("vim3-clk-driver") {
  output_name = "vim3-clk"
  sources = [
    "aml-fclk-rates.cc",
    "meson_gate.cc",
    "meson_pll.cc",
    "vim3_clk.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.driver.compat:fuchsia.driver.compat_cpp",
    "//sdk/fidl/fuchsia.hardware.clockimpl:fuchsia.hardware.clockimpl_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp",
    "//src/devices/bind/fuchsia.test:fuchsia.test_cpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/mmio",
    "//zircon/system/ulib/hwreg",
  ]
}

fuchsia_driver_component("vim3-clk") {
  component_name = "vim3-clk"
  manifest = "meta/vim3-clk.cml"
  deps = [
    ":bind",
    ":vim3-clk-driver",
  ]
  info = "component-info.json"
}

fuchsia_driver_package("package") {
  package_name = "vim3_clk_driver"
  driver_components = [ ":vim3-clk" ]
}

group("tests") {
  testonly = true

  deps = [ "tests:vim3-clk-test-pkg" ]
}
