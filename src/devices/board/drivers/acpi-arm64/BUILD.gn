# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")
import("//src/devices/bus/drivers/pci/pci.gni")

driver_bind_rules("acpi-arm64-bind") {
  rules = "acpi-arm64.bind"
  header_output = "acpi-arm64-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.arm.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

fuchsia_driver("acpi-arm64-driver") {
  output_name = "acpi-arm64"
  sources = [
    "acpi-arm64.cc",
    "sysmem.cc",
  ]
  deps = [
    ":acpi-arm64-bind",
    "//sdk/banjo/fuchsia.hardware.pciroot:fuchsia.hardware.pciroot_banjo_cpp",
    "//sdk/banjo/fuchsia.sysmem:fuchsia.sysmem_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/devices/board/lib/acpi",
    "//src/devices/board/lib/smbios",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/iommu:iommu-arm",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//third_party/acpica",
    "//zircon/system/ulib/zx",
  ]

  if (platform_enable_user_pci) {
    defines = [ "ENABLE_USER_PCI" ]
  }
}

fuchsia_driver_component("acpi-arm64") {
  component_name = "acpi-arm64"
  deps = [ ":acpi-arm64-driver" ]
  info = "acpi-arm64-info.json"
  manifest = "meta/acpi-arm64.cml"
}

fuchsia_driver_package("package") {
  package_name = "acpi-arm64"
  driver_components = [ ":acpi-arm64" ]
}
