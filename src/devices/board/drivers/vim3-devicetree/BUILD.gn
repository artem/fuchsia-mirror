# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/bazel/bazel_inputs.gni")
import("//build/bazel/drivers/bazel_driver_package.gni")
import("//build/bazel/wrappers.gni")
import("//build/bind/bind.gni")
import("//build/devicetree/devicetree.gni")
import("//build/zbi/zbi_input.gni")

group("tests") {
  testonly = true
  deps = [
    ":vim3-devicetree-bind_test",
    ":vim3-devicetree-test-package",
  ]
}

devicetree_fragment("amlogic-a311d") {
  sources = [ "dts/amlogic-a311d.dtsi" ]
}

devicetree_fragment("meson-g12-common") {
  sources = [ "dts/meson-g12-common.dtsi.S" ]
}

devicetree("vim3") {
  sources = [ "dts/khadas-vim3.dts.S" ]
  deps = [
    ":amlogic-a311d",
    ":meson-g12-common",
    "//zircon/system/ulib/ddk-platform-defs",
  ]
  visitors = [
    "//sdk/lib/driver/devicetree/visitors/drivers/sysmem:sysmem-visitor",
    "//sdk/lib/driver/devicetree/visitors/drivers/interrupt-controllers/arm-gicv2",
    "//sdk/lib/driver/devicetree/visitors/drivers/registers:registers-visitor",
    "//sdk/lib/driver/devicetree/visitors/drivers/clock-controllers/clockimpl-visitor",
    "//sdk/lib/driver/devicetree/visitors/drivers/mali-gpu:mali-gpu-visitor",
    "//sdk/lib/driver/devicetree/visitors/drivers/gpio-controllers/gpioimpl-visitor",
  ]
  golden = "dts/khadas-vim3.golden.dts"
}

zbi_input("vim3-dtb-item") {
  deps = [ ":vim3" ]
  type = "devicetree"

  dtb_file = get_target_outputs(":vim3.dtb")
  sources = [ dtb_file[0] ]
}

driver_bind_rules("vim3-devicetree-bind") {
  rules = "vim3-devicetree.bind"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.khadas.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

bazel_driver_package("bazel_package") {
  bazel_target = "//src/devices/board/drivers/vim3-devicetree:vim3-devicetree"
  package_name = "vim3-devicetree"
}

bazel_fuchsia_test_package("vim3-devicetree-test-package") {
  test_package_name = "vim3-devicetree-test"
  test_component_names = [ "vim3-devicetree-test" ]

  # Integration test for the board driver requires these prebuilt artifacts:
  #   - devicetree blob (.dtb)
  #   - Platform bus driver (.far)
  #   - driver-test-realm package (.far)
  bazel_inputs = [
    ":vim3",
    "//sdk/lib/driver/devicetree/testing:devicetree-test-bazel-inputs",
  ]
}

bazel_input_resource("vim3-dtb") {
  sources = [ "$target_out_dir/khadas-vim3.dtb" ]
  outputs = [ "{{source_file_part}}" ]
  deps = [ ":vim3" ]
}
