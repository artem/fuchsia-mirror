# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

driver_bind_rules("sherlock-post-init-bind") {
  rules = "post-init.bind"
  tests = "post-init-bind-test.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_bindlib",
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.google.platform",
    "//src/devices/bind/fuchsia.gpio",
  ]
}

fuchsia_driver("post-init-driver") {
  output_name = "sherlock-post-init"
  sources = [
    "display.cc",
    "post-init.cc",
    "touch.cc",
  ]
  deps = [
    ":sherlock-post-init-bind",
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_cpp",
    "//sdk/fidl/fuchsia.hardware.amlogiccanvas:fuchsia.hardware.amlogiccanvas_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/inspect/component/cpp",
    "//sdk/lib/stdcompat",
    "//src/devices/bin/driver_runtime",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.amlogic.platform:fuchsia.amlogic.platform_cpp",
    "//src/devices/bind/fuchsia.amlogic.platform.t931:fuchsia.amlogic.platform.t931_cpp",
    "//src/devices/bind/fuchsia.display.dsi:fuchsia.display.dsi_cpp",
    "//src/devices/bind/fuchsia.focaltech.platform:fuchsia.focaltech.platform_cpp",
    "//src/devices/bind/fuchsia.gpio:fuchsia.gpio_cpp",
    "//src/devices/bind/fuchsia.i2c:fuchsia.i2c_cpp",
    "//src/devices/bind/fuchsia.sysmem:fuchsia.sysmem_cpp",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/focaltech",
    "//src/graphics/display/lib/device-protocol-display",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//zircon/system/ulib/ddk-platform-defs",
  ]
}

fuchsia_driver_component("post-init") {
  component_name = "sherlock-post-init"
  deps = [ ":post-init-driver" ]
  info = "post-init-info.json"
  manifest = "meta/post-init.cml"
}

group("tests") {
  testonly = true
  deps = [ ":sherlock-post-init-bind_test" ]
}
