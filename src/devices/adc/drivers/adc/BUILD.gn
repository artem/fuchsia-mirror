# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("adc_bind") {
  rules = "adc.bind"
  bind_output = "adc.bindbc"
  tests = "test_bind.json"
  deps =
      [ "//sdk/fidl/fuchsia.hardware.adcimpl:fuchsia.hardware.adcimpl_bindlib" ]
}

source_set("common") {
  sources = [ "adc.cc" ]
  public_deps = [
    ":adc_bind",
    "//sdk/fidl/fuchsia.driver.compat:fuchsia.driver.compat_cpp",
    "//sdk/fidl/fuchsia.hardware.adc:fuchsia.hardware.adc_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.adc:fuchsia.hardware.adc_cpp",
    "//sdk/fidl/fuchsia.hardware.adcimpl:fuchsia.hardware.adcimpl_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp",
    "//src/devices/bind/fuchsia.adc:fuchsia.adc_cpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/fidl-metadata",
    "//src/devices/lib/mmio",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver("adc-driver") {
  output_name = "adc"
  deps = [
    ":common",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_driver_component("adc") {
  info = "adc-info.json"
  component_name = "adc"
  deps = [ ":adc-driver" ]
  manifest = "meta/adc.cml"
}

fuchsia_driver_package("package") {
  package_name = "adc"
  driver_components = [ ":adc" ]
}

test("adc-test-bin") {
  output_name = "adc-test"
  sources = [ "adc-test.cc" ]
  deps = [
    ":common",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/driver/testing/cpp:cpp",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("adc-test") {
  deps = [ ":adc-test-bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":adc-test",
    ":adc_bind_test",
  ]
}
