# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("aml_spi_bind") {
  rules = "aml_spi.bind"
  bind_output = "aml-spi.bindbc"
  tests = "bind_tests.json"

  deps = [
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_bindlib",
    "//sdk/fidl/fuchsia.hardware.registers:fuchsia.hardware.registers_bindlib",
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

group("common") {
  public_deps = [
    ":aml_spi_bind",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_cpp",
    "//sdk/fidl/fuchsia.hardware.registers:fuchsia.hardware.registers_cpp",
    "//sdk/fidl/fuchsia.hardware.spiimpl:fuchsia.hardware.spiimpl_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.spiimpl:fuchsia.hardware.spiimpl_cpp",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_cpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/fit-promise",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/mmio",
    "//src/lib/ddk:ddk_metadata",
    "//src/lib/vmo_store",
    "//zircon/system/ulib/async",
    "//zircon/system/ulib/ddk-platform-defs",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
  ]
}

test("aml-spi-test-bin") {
  output_name = "aml-spi-test"
  sources = [
    "aml-spi.cc",
    "tests/aml-spi-bti-test.cc",
    "tests/aml-spi-clock-mode-test.cc",
    "tests/aml-spi-shutdown-test.cc",
    "tests/aml-spi-test.cc",
  ]
  deps = [
    ":common",
    "//sdk/lib/driver/testing/cpp:gtest_fixture",
    "//src/devices/gpio/testing/fake-gpio",
    "//src/devices/registers/testing/mock-registers",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake-mmio-reg",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("aml-spi-test") {
  deps = [ ":aml-spi-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":aml-spi-test",
    ":aml_spi_bind_test",
  ]
}

fuchsia_driver("aml-spi-driver") {
  output_name = "aml-spi"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "aml-spi.cc" ]
  deps = [ ":common" ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("aml-spi") {
  info = "aml-spi-info.json"
  component_name = "aml-spi"
  deps = [ ":aml-spi-driver" ]
  manifest = "meta/aml-spi.cml"
}

fuchsia_driver_package("package") {
  package_name = "aml-spi"
  driver_components = [ ":aml-spi" ]
}
