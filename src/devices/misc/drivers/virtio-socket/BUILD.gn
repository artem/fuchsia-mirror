# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("virtio_socket_bind") {
  rules = "virtio_socket.bind"
  tests = "bind_tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.pci:fuchsia.hardware.pci_bindlib",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_bindlib",
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.pci",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":test_package",
    ":virtio_socket_bind_test",
  ]
}

source_set("common") {
  sources = [ "socket.cc" ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.vsock:fuchsia.hardware.vsock_cpp",
    "//src/devices/bin/driver_runtime",
    "//src/devices/bus/lib/virtio",
    "//src/devices/lib/driver",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_driver("virtio_socket-driver") {
  output_name = "virtio_socket"
  sources = [ "socket_driver.cc" ]
  deps = [
    ":common",
    ":virtio_socket_bind",
  ]
}

fuchsia_driver_component("virtio_socket") {
  info = "virtio_socket-info.json"
  component_name = "virtio_socket"
  manifest = "meta/virtio_socket.cml"
  deps = [ ":virtio_socket-driver" ]
}

fuchsia_driver_package("package") {
  package_name = "virtio_socket"
  driver_components = [ ":virtio_socket" ]
}

test("test") {
  output_name = "virtio-socket-test"
  sources = [ "test.cc" ]
  deps = [
    ":common",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("test_package") {
  package_name = "virtio-socket-tests"
  deps = [ ":test" ]
}
