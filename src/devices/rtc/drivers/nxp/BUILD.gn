# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")
import("//build/packages/prebuilt_package.gni")

driver_bind_rules("pcf8563_rtc_bind") {
  rules = "pcf8563_rtc.bind"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.nxp.platform" ]
}

fuchsia_driver("nxp-driver") {
  output_name = "nxp"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "pcf8563-rtc.cc" ]
  deps = [
    ":pcf8563_rtc_bind",
    "//sdk/fidl/fuchsia.hardware.rtc:fuchsia.hardware.rtc_cpp",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/rtc/lib/rtc",
    "//src/devices/rtc/lib/rtc:rtc_llcpp",
    "//src/lib/ddktl",
  ]
}

fuchsia_driver_component("nxp") {
  info = "nxp-info.json"
  component_name = "nxp"
  deps = [ ":nxp-driver" ]
  manifest = "meta/nxp.cml"
}

fuchsia_driver_package("nxp-pkg") {
  driver_components = [ ":nxp" ]
  package_name = "nxp"
}

group("tests") {
  testonly = true
  deps = [ ":pcf8563_rtc_bind_test" ]
}

# This is the OOT built RTC driver
prebuilt_package("pcf8563") {
  package_name = "pcf8563"
  archive = "//prebuilt/drivers/rtc/pcf8563/pcf8563"
}
