# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("registers_bind") {
  rules = "registers.bind"
  bind_output = "registers.bindbc"
  tests = "tests.json"
  deps = [
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.platform",
  ]
}

fuchsia_driver("registers-driver") {
  output_name = "registers"
  sources = [ "registers.cc" ]
  deps = [
    ":common",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_driver_component("registers") {
  info = "registers-info.json"
  component_name = "registers"
  deps = [ ":registers-driver" ]
  manifest = "meta/registers.cml"
}

fuchsia_driver_package("package") {
  package_name = "registers"
  driver_components = [ ":registers" ]
}

test("registers-test") {
  output_name = "registers-test"
  sources = [
    "registers-test.cc",
    "registers.cc",
  ]
  deps = [
    ":common",
    "//sdk/lib/driver/testing/cpp:gtest_fixture",
    "//src/devices/lib/fidl-metadata",
    "//src/devices/testing/mock-mmio-reg",
    "//src/lib/fxl/test:gtest_main",
  ]
}

group("common") {
  public_deps = [
    ":registers_bind",
    "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_cpp",
    "//sdk/fidl/fuchsia.hardware.registers:fuchsia.hardware.registers_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//src/devices/bin/driver_runtime",
    "//src/devices/bind/fuchsia.register:fuchsia.register_cpp",
    "//src/devices/lib/mmio",
    "//src/lib/ddk:ddk_metadata",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_unittest_package("registers-test-package") {
  package_name = "registers-test"
  deps = [ ":registers-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":registers-test-package",
    ":registers_bind_test",
  ]
}
