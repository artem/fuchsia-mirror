# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dist/component_manifest_resources.gni")
import("//build/toolchain/zircon/user_basic_redirect.gni")

group("driver_host2") {
  deps = [ ":component" ]
}

static_library("entry_point") {
  public = [ "entry_point.h" ]
  sources = [ "entry_point.cc" ]
  public_deps = [
    "//src/zircon/lib/zircon",
    "//zircon/system/public",
  ]
}

executable("bin") {
  output_name = "driver_host2"
  sources = [ "driver_host.cc" ]
  deps = [ ":entry_point" ]
}

user_basic_redirect("modules.basic") {
  visibility = [ ":*" ]
  public_deps = [ ":bin" ]
}

fuchsia_component("component") {
  component_name = "driver_host2"
  manifest = "meta/driver_host2.cml"
  deps = [ ":modules.basic" ]
}

fuchsia_package("package") {
  package_name = "driver_host2"
  deps = [ ":component" ]
}
