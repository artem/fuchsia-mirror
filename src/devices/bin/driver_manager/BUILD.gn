# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = []

  # TODO(https://fxbug.dev/42167791): Failed test disabled on coverage.
  # Please fix the failure and remove the condition below.
  if (!is_coverage) {
    deps += [
      ":driver-manager-tests",
      ":driver-runner-tests",
    ]
  }
}

fuchsia_component_manifest("manifest") {
  component_name = "driver_manager"
  manifest = "meta/driver_manager.cml"
  restricted_features = [ "allow_long_names" ]
}

fuchsia_structured_config_cpp_elf_lib("driver_manager_config") {
  cm_label = ":manifest"
}

fuchsia_structured_config_values("driver_manager_config_default") {
  cm_label = ":manifest"
  values = {
    set_root_driver_host_critical = true
    delay_fallback_until_base_drivers_indexed = true
    suspend_timeout_fallback = true
    verbose = false
    use_driver_framework_v2 = true
    driver_host_crash_policy = "restart-driver-host"
    root_driver = "fuchsia-boot:///platform-bus#meta/platform-bus.cm"
    enable_test_shutdown_delays = false
  }
}

# TODO(https://fxbug.dev/42113849): Migrate to the new bind rules and delete the below
source_set("binding") {
  sources = [
    "binding.cc",
    "binding.h",
  ]
  public_deps = [
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//zircon/system/ulib/fbl",
  ]
  deps = [
    "//src/devices/lib/bind:bind-lib",
    "//src/devices/lib/log",
  ]
}

source_set("inspect") {
  visibility = [ "*" ]
  sources = [
    "inspect.cc",
    "inspect.h",
  ]
  public_deps = [
    "//sdk/lib/inspect/component/cpp",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//src/storage/lib/vfs/cpp",
    "//zircon/system/ulib/fbl",
  ]
  deps = [ "//sdk/lib/inspect/component/cpp" ]
}

source_set("driver_host_loader_service") {
  visibility = [ ":*" ]
  sources = [
    "driver_host_loader_service.cc",
    "driver_host_loader_service.h",
  ]
  public_deps = [ "//src/lib/loader_service" ]
  deps = [
    "//src/devices/lib/log",
    "//src/lib/files",
  ]
}

source_set("driver_runner") {
  sources = [
    "bind_manager.cc",
    "bind_manager.h",
    "bind_result_tracker.cc",
    "bind_result_tracker.h",
    "composite_assembler.cc",
    "composite_assembler.h",
    "composite_node_spec_v2.cc",
    "composite_node_spec_v2.h",
    "driver_host.cc",
    "driver_host.h",
    "driver_runner.cc",
    "driver_runner.h",
    "node.cc",
    "node.h",
    "node_removal_tracker.cc",
    "node_removal_tracker.h",
    "parent_set_collector.cc",
    "parent_set_collector.h",
    "runner.cc",
    "runner.h",
    "shutdown_helper.cc",
    "shutdown_helper.h",
    "shutdown_manager.cc",
    "shutdown_manager.h",
  ]

  deps = [
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/zbi-format",
    "//src/bringup/lib/mexec",
    "//src/devices/bin/driver_manager:binding",
    "//src/devices/bin/driver_manager/composite_node_spec:lib",
    "//src/devices/bind/fuchsia.platform:fuchsia.platform_cpp",
    "//src/devices/lib/log",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/zbitl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_cpp",
    "//sdk/fidl/fuchsia.component:fuchsia.component_cpp",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_cpp",
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_cpp",
    "//sdk/fidl/fuchsia.device.composite:fuchsia.device.composite_cpp",
    "//sdk/fidl/fuchsia.device.manager:fuchsia.device.manager_cpp",
    "//sdk/fidl/fuchsia.driver.development:fuchsia.driver.development_cpp",
    "//sdk/fidl/fuchsia.driver.host:fuchsia.driver.host_cpp",
    "//sdk/fidl/fuchsia.driver.index:fuchsia.driver.index_cpp",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp",
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_cpp",
    "//sdk/lib/async_patterns/cpp",
    "//sdk/lib/inspect/component/cpp",
    "//sdk/lib/vfs/cpp",
    "//src/devices/bin/driver_manager:inspect",
    "//src/devices/bin/driver_manager/devfs",
    "//src/lib/ddk",
    "//src/storage/lib/vfs/cpp",
    "//zircon/system/ulib/zircon-internal",
  ]
}

source_set("driver_development_service") {
  sources = [
    "driver_development_service.cc",
    "driver_development_service.h",
  ]
  public_deps = [
    ":driver_runner",
    "//sdk/fidl/fuchsia.driver.development:fuchsia.driver.development_cpp",
    "//sdk/lib/component/outgoing/cpp",
  ]
  deps = [
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//src/devices/bin/driver_manager/driver_development:lib",
    "//src/devices/lib/log",
  ]
}

source_set("driver_manager_test_base") {
  testonly = true
  sources = [
    "tests/driver_manager_test_base.cc",
    "tests/driver_manager_test_base.h",
  ]
  public_deps = [
    ":driver_runner",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/system/ulib/zircon-internal",
  ]
}

executable("driver_runner_test") {
  testonly = true
  sources = [ "tests/driver_runner_test.cc" ]

  deps = [
    ":driver_runner",
    "//sdk/fidl/fuchsia.component:fuchsia.component_cpp_testing",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_cpp_testing",
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_cpp_testing",
    "//sdk/fidl/fuchsia.driver.host:fuchsia.driver.host_cpp_testing",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp_testing",
    "//sdk/lib/inspect/testing/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/devices/bind/fuchsia.platform:fuchsia.platform_cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_unittest_component("driver-runner-test") {
  deps = [ ":driver_runner_test" ]
}

executable("bind_manager_test") {
  testonly = true
  sources = [
    "tests/bind_manager_test.cc",
    "tests/bind_manager_test_base.cc",
    "tests/bind_manager_test_base.h",
    "tests/multibind_test.cc",
  ]

  deps = [
    ":driver_manager_test_base",
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_cpp",
    "//sdk/fidl/fuchsia.driver.index:fuchsia.driver.index_cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/lib/testing/loop_fixture",
  ]
}

fuchsia_unittest_component("bind-manager-test") {
  deps = [ ":bind_manager_test" ]
}

executable("composite_assembler_test") {
  testonly = true
  sources = [ "tests/composite_assembler_test.cc" ]

  deps = [
    ":driver_manager_test_base",
    "//sdk/lib/inspect/testing/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/ddk",
  ]
}

fuchsia_unittest_component("composite-assembler-test") {
  deps = [ ":composite_assembler_test" ]
}

executable("composite_node_spec_v2_test") {
  testonly = true
  sources = [ "tests/composite_node_spec_v2_test.cc" ]

  deps = [ ":driver_manager_test_base" ]
}

fuchsia_unittest_component("node-test") {
  deps = [ ":node_test" ]
}

executable("node_test") {
  testonly = true
  sources = [ "tests/node_test.cc" ]

  deps = [
    ":driver_manager_test_base",
    "//sdk/fidl/fuchsia.component:fuchsia.component_hlcpp",
  ]
}

fuchsia_unittest_component("composite-node-spec-v2-test") {
  deps = [ ":composite_node_spec_v2_test" ]
}

executable("node_removal_tracker_test") {
  testonly = true
  sources = [ "tests/node_removal_tracker_test.cc" ]

  deps = [
    ":driver_runner",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/system/ulib/zircon-internal",
  ]
}

fuchsia_unittest_component("node-removal-tracker-test") {
  deps = [ ":node_removal_tracker_test" ]
}

fuchsia_unittest_component("node-shutdown-test") {
  deps = [ ":node_shutdown_test" ]
}

executable("node_shutdown_test") {
  testonly = true
  sources = [ "tests/node_shutdown_test.cc" ]

  deps = [
    ":driver_manager_test_base",
    "//sdk/fidl/fuchsia.component:fuchsia.component_hlcpp",
  ]
}

fuchsia_test_package("driver-runner-tests") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }

  test_components = [
    ":bind-manager-test",
    ":composite-assembler-test",
    ":composite-node-spec-v2-test",
    ":driver-runner-test",
    ":node-removal-tracker-test",
    ":node-shutdown-test",
    ":node-test",
  ]
}
executable("driver_manager") {
  sources = [ "main.cc" ]

  deps = [
    ":driver_development_service",
    ":driver_host_loader_service",
    ":driver_manager_config",
    ":driver_runner",
    "//src/devices/lib/log",
    "//src/sys/lib/stdout-to-debuglog/cpp",
  ]

  configs += [ "//build/config:all_source" ]

  if (toolchain_variant.libprefix != "") {
    # The libprefix always ends with a / but that's not part of the
    # "config" string in the loader-service protocol.
    ldsvc_config =
        get_path_info("${toolchain_variant.libprefix}libfoo.so", "dir")
    assert(ldsvc_config != "" && ldsvc_config != ".")
    defines = [ "DRIVERHOST_LDSVC_CONFIG=\"$ldsvc_config\"" ]
  }
}

test("driver_manager_devfs_test") {
  sources = [ "tests/devfs_test.cc" ]

  deps = [
    "devfs:devfs",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fdio",
    "//src/storage/lib/vfs/cpp:test_support",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync:sync-cpp",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_component("driver-manager-devfs-test") {
  deps = [ ":driver_manager_devfs_test" ]
}

# This is a separate test binary because this test fixture uses gtest, not zxtest
test("driver_host_loader_service_test") {
  sources = [ "tests/driver_host_loader_service_test.cc" ]
  deps = [
    ":driver_host_loader_service",
    "//src/lib/loader_service:loader_service_test_fixture",
  ]
}

fuchsia_unittest_component("driver-host-loader-service-test") {
  manifest = "meta/driver-host-loader-service-test.cml"
  deps = [ ":driver_host_loader_service_test" ]
  test_type = "system"
}

test("composite_node_spec_manager_test") {
  sources = [ "tests/composite_node_spec_manager_test.cc" ]

  deps = [
    ":driver_runner",
    "composite_node_spec:lib",
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_component("composite-node-spec-manager-test") {
  deps = [ ":composite_node_spec_manager_test" ]
}

fuchsia_test_package("driver-manager-tests") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }

  test_components = [
    ":composite-node-spec-manager-test",
    ":driver-host-loader-service-test",
    ":driver-manager-devfs-test",
  ]
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":driver_manager" ]
}

# List of shared libraries used by drivers.
group("driver_shared_libraries") {
  deps = [
    "//sdk/lib/fdio",
    "//sdk/lib/svc",
    "//sdk/lib/syslog/cpp:backend_fuchsia_globals",
    "//src/devices/bin/driver_runtime",
    "//src/devices/lib/driver",
    "//src/security/lib/tee/tee-client-api",
    "//third_party/boringssl:crypto-shared",
    "//third_party/boringssl:ssl-shared",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/syslog",
    "//zircon/system/ulib/trace-engine",
  ]
}

fuchsia_package("dfv2_package") {
  package_name = "driver_manager"
  deps = [
    ":component",
    ":driver_manager_config_default",
    ":driver_shared_libraries",
  ]
}
