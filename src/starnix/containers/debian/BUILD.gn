# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/storage/ext4/ext4_to_pkg.gni")

group("debian") {
  deps = [ ":debian_package" ]
}

group("container_resources") {
  deps = [
    ":system_image",
    "//src/starnix/containers:data_tmp_target",
    "//src/starnix/containers:default_init",
  ]
}

ext4_to_pkg("system_image") {
  if (target_cpu == "x64") {
    input = "//prebuilt/starnix/linux-amd64/amd64_system.img"
  } else if (target_cpu == "arm64") {
    input = "//prebuilt/starnix/linux-arm64/arm64_system.img"
  } else if (target_cpu == "riscv64") {
    input = "//prebuilt/starnix/linux-riscv64/riscv64_system.img"
  } else {
    assert(false, "Unsupported CPU architecture.")
  }
  prefix = "data/system"
}

fuchsia_component("debian_container") {
  manifest = "meta/debian_container.cml"
}

fuchsia_component("debian_container_profiled") {
  manifest = "meta/debian_container_profiled.cml"
}

fuchsia_package("debian_package") {
  package_name = "debian"
  deps = [
    ":container_resources",
    ":debian_container",
    ":debian_container_profiled",
  ]
}
