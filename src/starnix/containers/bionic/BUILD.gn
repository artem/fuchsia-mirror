# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/storage/ext4/ext4_to_pkg.gni")

# The system image for the container.
ext4_to_pkg("system_image") {
  input = "//prebuilt/starnix/android-image-amd64/system.img"
  prefix = "data/system"
}

resource("minimal_init_rc") {
  sources = [ "init.rc" ]
  outputs = [ "data/init.rc" ]
}

group("container_resources") {
  deps = [ "//src/starnix/containers:default_init" ]

  if (target_cpu == "x64") {
    deps += [
      ":minimal_init_rc",
      ":system_image",
      "//src/starnix/containers:data_tmp_target",
    ]
  }
}

fuchsia_component("bionic_container") {
  manifest = "meta/bionic_container.cml"
  component_name = "bionic_container"
}

fuchsia_package("bionic_package") {
  package_name = "bionic"
  deps = [
    ":bionic_container",
    ":container_resources",
  ]
}
