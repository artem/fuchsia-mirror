# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/rust/rustc_binary.gni")
import("//build/testing/host_test_data.gni")

group("gen_android_starnix_container") {
  deps = [
    ":assembly_inputs($host_toolchain)",
    ":host",
  ]
}

install_host_tools("host") {
  deps = [ ":gen_android_starnix_container_bin" ]
  outputs = [ "gen-android-starnix-container" ]
}

if (is_host) {
  # Generates an assembly_inputs.json for gen-android-starnix-container and
  # makes it available to //:cipd_assembly_artifacts in GN. This will add this
  # binary to the CIPD package created by infra.
  generated_file("assembly_inputs") {
    deps = [ ":gen_android_starnix_container_bin" ]
    _bin_src = rebase_path("${root_out_dir}/gen-android-starnix-container",
                           root_build_dir)

    contents = [
      {
        source = _bin_src
        destination = "gen-android-starnix-container"
      },
    ]
    output_conversion = "json"

    outputs = [
      "${target_out_dir}/gen_android_starnix_container_assembly_inputs.json",
    ]
    metadata = {
      assembly_inputs = [
        {
          path = rebase_path(outputs[0], root_build_dir)
        },
      ]
      assembly_inputs_barrier = []
    }
  }

  host_test_data("test_ext4_image") {
    sources = [ "//src/storage/ext4/tests/test.img" ]
    outputs =
        [ "$root_out_dir/test_data/gen-android-starnix-container/test.img" ]
    visibility = [ ":*" ]
  }

  rustc_binary("gen_android_starnix_container_bin") {
    name = "gen-android-starnix-container"
    edition = "2021"
    with_unit_tests = true
    deps = [
      "//src/lib/fuchsia-url",
      "//src/starnix/lib/linux_uapi",
      "//src/storage/ext4/lib/ext4_extract",
      "//src/storage/ext4/lib/metadata",
      "//src/sys/pkg/lib/far/rust:fuchsia-archive",
      "//src/sys/pkg/lib/fuchsia-pkg",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:argh",
      "//third_party/rust_crates:camino",
      "//third_party/rust_crates:serde",
      "//third_party/rust_crates:serde_json",
      "//third_party/rust_crates:static_assertions",
    ]

    test_deps = [
      ":test_ext4_image",
      "//third_party/rust_crates:assert_matches",
      "//third_party/rust_crates:tempfile",
    ]

    sources = [
      "src/depfile.rs",
      "src/hal_manifest.rs",
      "src/main.rs",
      "src/remote_bundle.rs",
    ]
  }

  group("tests") {
    testonly = true
    deps = [ ":gen_android_starnix_container_bin_test" ]
  }
}
