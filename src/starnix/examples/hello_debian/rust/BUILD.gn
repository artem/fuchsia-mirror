# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/starnix/tests/build/starnix_linux_test_rustc_binary.gni")

starnix_linux_test_rustc_binary("bin") {
  edition = "2021"
  output_name = "hello_debian_rust"
  source_root = "hello_debian.rs"
  sources = [ source_root ]
}

fuchsia_component("hello_debian_rust") {
  manifest = "meta/hello_debian_rust.cml"
  deps = [ ":bin" ]
}

fuchsia_package("hello_debian_rust_package") {
  package_name = "hello_debian_rust"
  deps = [ ":hello_debian_rust" ]
}

fuchsia_test_component("hello_debian_rust_integration") {
  manifest = "meta/integration_test.cml"
  deps = [ "//src/starnix/examples/hello_debian:hello_debian_verifier" ]

  test_type = "starnix"

  # TODO(https://fxbug.dev/296309679) remove once ScopedInstance can be used separately
  check_includes = false
}

fuchsia_test_package("hello_debian_rust_integration_test") {
  test_components = [ ":hello_debian_rust_integration" ]
  subpackages = [
    ":hello_debian_rust_package",
    "//src/starnix/kernel:starnix_kernel_package",
    "//src/starnix/containers/debian:debian_package",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":hello_debian_rust_integration_test" ]
}

group("rust") {
  deps = [ ":hello_debian_rust_package" ]
}
