# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/environments.gni")
import("//src/starnix/tests/starnix_test_subpackages.gni")

group("tests") {
  testonly = true
  if (target_cpu == "x64" && !is_asan) {
    deps = [
      ":alpine_tests",
      "//src/starnix/containers/alpine:alpine_package",
    ]
  }
}

resource("alpine_ssh_test_resources") {
  sources = [ "alpine_ssh_test.sh" ]
  outputs = [ "data/tests/{{source_file_part}}" ]
}

fuchsia_test_component("alpine_ssh_test") {
  check_references = false
  manifest = "meta/alpine_ssh_test.cml"
  deps = [
    ":alpine_ssh_test_resources",
    "//src/starnix/tests:starnix_test",
  ]
  test_type = "starnix"
}

fuchsia_test_package("alpine_tests") {
  test_components = [ ":alpine_ssh_test" ]

  if (target_cpu == "x64") {
    subpackages = starnix_test_subpackages
    subpackages += [
      "//src/starnix/containers/alpine:alpine_package",
      "//src/ui/testing/test_ui_stack:test-ui-stack",
    ]
  }

  test_specs = {
    environments = with_display_envs
  }
}
