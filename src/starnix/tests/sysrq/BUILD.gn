# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true

  deps = [ ":starnix_sysrq_integration_test" ]
}

rustc_test("sysrq_integration_test_bin") {
  edition = "2021"
  sources = [ "src/lib.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/fuchsia-fs",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:futures",
  ]
}

fuchsia_test_component("sysrq_test") {
  manifest = "meta/integration_test.cml"
  deps = [ ":sysrq_integration_test_bin" ]
  test_type = "starnix"
}

fuchsia_component("container") {
  testonly = true
  manifest = "//src/starnix/containers/debian/meta/debian_container.cml"
}

fuchsia_component("kernel_with_container") {
  testonly = true
  manifest = "meta/kernel_with_container.cml"
}

fuchsia_test_package("starnix_sysrq_integration_test") {
  test_components = [ ":sysrq_test" ]
  deps = [ ":kernel_with_container" ]
  subpackages = [
    "//src/starnix/kernel:starnix_kernel_package",
    "//src/starnix/containers/debian:debian_package",
  ]

  # These tests cause the starnix kernel to panic and crash on purpose, observing error logs.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
