# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//build/testing/host_test.gni")
import("//build/testing/host_test_data.gni")
import("//src/lib/testing/expectation/generated_expectations_file.gni")
import("//src/starnix/kernel/starnix.gni")
import("//src/starnix/tests/build/starnix_linux_test_component.gni")
import("//src/starnix/tests/starnix_test_subpackages.gni")

syscall_tests = [
  "abort_test",
  "ashmem_test",
  "bpf_test",
  "custom_artifact_test",
  "epoll_test",
  "extended_pstate_test",
  "fcntl_test",
  "fs_test",
  "fsverity_test",
  "fuse_test",
  "futex_test",
  "input_test",
  "ioctl_test",
  "loop_test",
  "memfd_test",
  "mmap_test",
  "mount_test",
  "overlayfs_test",
  "pidfd_test",
  "pipe_test",
  "poll_test",
  "prctl_test",
  "proc_test",
  "procfs_mem_test",
  "ptrace_test",
  "read_write_test",
  "restart_read_test",
  "rlimit_test",
  "seccomp_test",
  "sendfile_test",
  "signal_handling_test",
  "socket_test",
  "sw_breakpoint_test",
  "sysfs_kernel_test",
  "sysfs_power_test",
  "syslog_test",
  "task_test",
  "terminal_test",
  "test_helper_test",
  "time_test",
  "timerfd_test",
  "tmpfs_test",
  "trivial_test",
  "tun_test",
  "udev_test",
  "uio_test",
  "vdso_test",
  "vmsplice_test",
  "waitpid_exit_signal_test",
  "waitpid_flags_test",
]

if (target_cpu == "x64") {
  # TODO(https://fxbug.dev/42059962): arm64 emulator does not have Vulkan support yet.
  # This test requires test-ui-stack.
  syscall_tests += [ "uinput_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":starnix_syscalls_cpp_tests" ]
  if (host_os == "linux" && host_cpu == "x64" && has_board) {
    foreach(test_name, syscall_tests) {
      deps += [
        # These are Linux tests, so we can run them as host tests on our host Linux bot.
        ":${test_name}_host($host_toolchain)",
      ]
    }
  }
}

if (is_host && has_board) {
  foreach(test_name, syscall_tests) {
    host_test("${test_name}_host") {
      binary_path = get_label_info(":${test_name}_bin($target_linux_toolchain)",
                                   "root_out_dir") + "/${test_name}_bin"
      deps = [
        ":${test_name}_bin($target_linux_toolchain)",
        ":host_runtime_deps",
      ]
    }
  }

  host_test_data("host_runtime_deps") {
    sources = [
      get_label_info(
              ":extended_pstate_initial_state_child($target_linux_toolchain)",
              "root_out_dir") + "/extended_pstate_initial_state_child",
      get_label_info(":syscall_test_exec_child($target_linux_toolchain)",
                     "root_out_dir") + "/syscall_test_exec_child",
      get_label_info(":ptrace_test_exec_child($target_linux_toolchain)",
                     "root_out_dir") + "/ptrace_test_exec_child",
    ]
    deps = [
      ":extended_pstate_initial_state_child($target_linux_toolchain)",
      ":ptrace_test_exec_child($target_linux_toolchain)",
      ":syscall_test_exec_child($target_linux_toolchain)",
    ]
  }
}

source_set("test_utils") {
  testonly = true

  sources = [
    "fault_test.h",
    "fault_test_suite.h",
    "proc_test_base.h",
    "syscall_matchers.h",
    "task_test.h",
    "test_helper.cc",
    "test_helper.h",
  ]
  public_deps = [
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
  deps = [ "//src/lib/fxl" ]
  configs += [ "//build/config:rtti" ]
}

if (target_cpu == "x64" || target_cpu == "arm64") {
  resource("fuse_overlayfs") {
    sources = []
    if (target_cpu == "x64") {
      sources += [ "//prebuilt/starnix/fuse-overlayfs-amd64/fuse-overlayfs" ]
    }
    if (target_cpu == "arm64") {
      sources += [ "//prebuilt/starnix/fuse-overlayfs-arm64/fuse-overlayfs" ]
    }
    outputs = [ "data/fuse-overlayfs" ]
  }
} else {
  group("fuse_overlayfs") {
  }
}

resource("simple_ext4_image") {
  sources = [
    "data/hello_world.txt",
    "data/simple_ext4.img",
  ]
  outputs = [ "data/{{source_file_part}}" ]
}

config("march") {
  if (current_cpu == "riscv64") {
    # Keep `march` consistent with the value used by Android, see
    # https://cs.android.com/android/platform/superproject/+/master:build/soong/cc/config/riscv64_device.go;drc=726b001ae1a3388f65b571350bef4d373a3768c8;l=36
    cflags = [ "-march=rv64gcv_zba_zbb_zbs" ]
  }
}

template("syscall_test_component") {
  test_name = invoker.test_name

  # Generate the manifest
  _manifest_label = "${test_name}.cml"
  _manifest_file = "$target_gen_dir/${_manifest_label}"
  generated_file(_manifest_label) {
    outputs = [ _manifest_file ]
    output_conversion = "json"
    contents = {
      include =
          [ "//src/starnix/tests/syscalls/cpp/meta/syscalls_cpp_test.cml" ]
      program = {
        binary = "data/tests/${test_name}_bin"
      }
    }
  }

  _test_label = "${test_name}_bin"
  test(_test_label) {
    # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
    exclude_toolchain_tags = [ "asan" ]
    sources = [ "${test_name}.cc" ]
    deps = [
      ":test_utils",
      "//sdk/fidl/fuchsia.fs:fuchsia.fs_cpp",
      "//sdk/lib/stdcompat",
      "//src/lib/files",
      "//src/lib/fxl/test:gtest_main",
      "//third_party/googletest:gmock",
      "//third_party/googletest:gtest",
      "//zircon/system/public",
      "//zircon/system/ulib/fbl",
    ]
    include_dirs = [
      "//third_party/android/platform/bionic/libc/kernel/uapi",
      "//third_party/android/platform/bionic/libc/kernel/android/uapi",
    ]
    if (target_cpu == "x64") {
      include_dirs +=
          [ "//third_party/android/platform/bionic/libc/kernel/uapi/asm-x86" ]
    } else if (target_cpu == "arm64") {
      include_dirs +=
          [ "//third_party/android/platform/bionic/libc/kernel/uapi/asm-arm64" ]
    }

    configs += [ "//build/config:rtti" ]

    if (!is_fuchsia) {
      configs += [ "//build/config:thread_safety_annotations" ]
    }

    # TODO(https://fxbug.dev/42085293): delete the below and fix compiler warnings
    configs += [ "//build/config:Wno-vla-cxx-extension" ]

    # TODO(b/307959737): Compile syscall test with the bionic toolchain and then move this
    # to the bionic toolchain.
    configs += [ ":march" ]

    # TODO(b/331282813): 'ENODATA' is deprecated in ISO C++, disable the warning for now.
    configs += [ "//build/config:Wno-deprecated-pragma" ]
  }

  starnix_linux_test_component(target_name) {
    test_label = ":${_test_label}"
    test_binary = _test_label
    manifest = _manifest_file
    deps = [
      ":${_manifest_label}",
      ":fuse_overlayfs",
      ":ptrace_test_exec_child_resource($target_linux_toolchain)",
      ":simple_ext4_image",
      ":syscall_test_exec_child_resource($target_linux_toolchain)",
    ]
    uses_expectations = true
    generated_expectations = ":syscalls_cpp_test_expectations"
    test_type = "starnix"
  }
}

foreach(test_name, syscall_tests) {
  syscall_test_component(test_name) {
    test_name = test_name
  }
}

test("crash") {
  # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
  exclude_toolchain_tags = [ "asan" ]
  sources = [ "crash.cc" ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

test("syslog_non_root") {
  # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
  exclude_toolchain_tags = [ "asan" ]
  sources = [ "syslog_non_root_test.cc" ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

if (is_linux) {
  executable("extended_pstate_initial_state_child") {
    # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
    exclude_toolchain_tags = [ "instrumented" ]
    sources = [ "extended_pstate_initial_state_child.cc" ]

    # This binary does not use libc or a dynamic linker since it is testing the state
    # presented to _start.
    disable_syslog_backend = true
    cflags = [ "-fno-builtin" ]
    ldflags = [
      "-nostdlib",
      "-nostartfiles",
      "-static",
    ]

    # On RISC-V the linker uses base address that's below the base allowed by
    # Zircon (0x200000). See b/311221240. Specify the base address to
    # workaround this issue.
    if (current_cpu == "riscv64") {
      ldflags += [ "-Wl,--image-base=0x200000" ]
    }

    configs -= [
      "//build/config/linux:default-pie",
      "//build/config/linux:implicit-host-libs",
    ]
  }

  executable("ptrace_test_exec_child") {
    testonly = true
    sources = [ "ptrace_test_exec_child.cc" ]

    # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
    exclude_toolchain_tags = [
      "asan",
      "coverage",
    ]
    deps = [ ":test_utils" ]
  }

  resource("ptrace_test_exec_child_resource") {
    testonly = true
    sources = [ "$root_out_dir/ptrace_test_exec_child" ]
    outputs = [ "data/tests/ptrace_test_exec_child" ]
    deps = [ ":ptrace_test_exec_child" ]
  }

  executable("syscall_test_exec_child") {
    testonly = true
    sources = [ "syscall_test_exec_child.cc" ]

    # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
    exclude_toolchain_tags = [
      "asan",
      "coverage",
    ]
    deps = [ ":test_utils" ]
  }

  resource("syscall_test_exec_child_resource") {
    testonly = true
    sources = [ "$root_out_dir/syscall_test_exec_child" ]
    outputs = [ "data/tests/syscall_test_exec_child" ]
    deps = [ ":syscall_test_exec_child" ]
  }
}

resource("extended_pstate_initial_state_child_resource") {
  binary_name = "extended_pstate_initial_state_child"
  exe_label = ":${binary_name}($target_linux_toolchain)"
  out_dir = get_label_info(exe_label, "root_out_dir")
  sources = [ "${out_dir}/${binary_name}" ]
  outputs = [ "data/tests/${binary_name}" ]
  deps = [ exe_label ]
}

generated_expectations_file("syscalls_cpp_test_expectations") {
  includes = [
    "expectations/syscalls_cpp_test.json5",
    "expectations/syscalls_cpp_test_${current_cpu}.json5",
  ]
}

starnix_linux_test_component("crash_test") {
  test_label = ":crash"
  test_binary = "crash"
  test_type = "starnix"
}

starnix_linux_test_component("syslog_non_root_test") {
  test_label = ":syslog_non_root"
  test_binary = "syslog_non_root"
  test_type = "starnix"
}

fuchsia_test_package("starnix_syscalls_cpp_tests") {
  test_components = [
    ":syslog_non_root_test",
    ":crash_test",
  ]
  foreach(test_name, syscall_tests) {
    test_components += [ ":${test_name}" ]
  }

  subpackages = starnix_test_subpackages
  subpackages += [ "//src/starnix/containers/debian:debian_package" ]

  if (target_cpu == "x64") {
    # TODO(https://fxbug.dev/42059962): arm64 emulator does not have Vulkan support yet.
    # Without Vulkan, test-ui-stack will stuck on Scenic startup.
    subpackages += [ "//src/ui/testing/test_ui_stack:test-ui-stack" ]
  }

  deps = [
    ":extended_pstate_initial_state_child_resource",
    "//src/lib/testing/expectation:expectation_comparer",
  ]

  test_specs = {
    # TODO(https://fxbug.dev/319150842): GCE doesn't support Vulkan, so, for now, just disable all
    # of these tests on GCE.
    _env = basic_envs
    _env += [ gce_env ]
    _env -= [ gce_env ]
    environments = _env

    log_settings = {
      # Tests exercise failure cases, so ERROR logs are expected.
      max_severity = "ERROR"
    }
  }
}
