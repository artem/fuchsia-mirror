# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//build/testing/host_test.gni")
import("//src/starnix/kernel/starnix.gni")
import("//src/starnix/tests/build/starnix_linux_test_component.gni")
import("//src/starnix/tests/starnix_test_subpackages.gni")

group("tests") {
  testonly = true
  deps = [ ":starnix_syscalls_rust_tests" ]
  if (host_os == "linux" && host_cpu == "x64" && has_board) {
    deps += [
      # These are Linux tests, so we can run them as host tests on our host Linux bot.
      ":syscalls_rust_host_tests($host_toolchain)",
    ]
  }
}

if (is_host && has_board) {
  host_test("syscalls_rust_host_tests") {
    binary_path =
        get_label_info(":syscalls_rust_test_bin($target_linux_toolchain)",
                       "root_out_dir") + "/syscalls_rust_test_bin"
    deps = [ ":syscalls_rust_test_bin($target_linux_toolchain)" ]
  }
}

rustc_test("syscalls_rust_test_bin") {
  edition = "2021"
  source_root = "src/lib.rs"
  sources = [
    "src/device_mapper_test.rs",
    source_root,
  ]

  deps = [
    "//src/starnix/lib/linux_uapi",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:serial_test",
    "//third_party/rust_crates:test-case",
    "//third_party/rust_crates:zerocopy",
  ]

  # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
  exclude_toolchain_tags = [ "asan" ]
}

resource("simple_ext4_image") {
  sources = [
    "data/corrupted_hashtree.txt",
    "data/corrupted_image_with_hashtree.txt",
    "data/hashtree_truncated.txt",
    "data/root_hash.txt",
    "data/simple_ext4.img",
    "data/valid_image_with_hashtree.txt",
  ]
  outputs = [ "data/{{source_file_part}}" ]
}

starnix_linux_test_component("syscalls_rust_test") {
  test_label = ":syscalls_rust_test_bin"
  test_binary = "syscalls_rust_test_bin"
  deps = [ ":simple_ext4_image" ]
  test_type = "starnix"
}

fuchsia_test_package("starnix_syscalls_rust_tests") {
  test_components = [ ":syscalls_rust_test" ]

  subpackages = starnix_test_subpackages
  subpackages += [ "//src/starnix/containers/debian:debian_package" ]

  deps = [ "//src/lib/testing/expectation:expectation_comparer" ]
}
