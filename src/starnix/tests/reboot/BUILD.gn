# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//src/starnix/tests/starnix_test_subpackages.gni")

group("tests") {
  testonly = true
  if (target_cpu == "x64") {
    deps = [ ":starnix_reboot_integration_tests" ]
  }
}

rustc_test("integration_test") {
  testonly = true
  name = "test_runner"
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_rust",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/sys/pkg/testing/mock-reboot",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:parking_lot",
  ]

  source_root = "src/test_runner.rs"
  sources = [ source_root ]
}

fuchsia_test_component("reboot_no_args") {
  manifest = "meta/reboot_no_args.cml"
}
fuchsia_test_component("reboot_ota_update") {
  manifest = "meta/reboot_ota_update.cml"
}

fuchsia_test_component("realm") {
  manifest = "meta/realm.cml"
}

fuchsia_test_component("reboot_integration_test") {
  manifest = "meta/integration_test.cml"
  deps = [ ":integration_test" ]
  test_type = "starnix"
}

fuchsia_test_package("starnix_reboot_integration_tests") {
  test_components = [ ":reboot_integration_test" ]
  deps = [
    ":realm",
    ":reboot_no_args",
    ":reboot_ota_update",
    "//src/starnix/tests/android:container_resources",
  ]

  subpackages = starnix_test_subpackages +
                [ "//src/starnix/containers/bionic:bionic_package" ]

  # TODO(b/295073633): remove when starnix can gracefully shutdown
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
