# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//src/starnix/kernel/starnix.gni")

group("correct_pid") {
  testonly = true

  # TODO(b/312589311) enable on riscv
  if (target_cpu != "riscv64") {
    deps = [ ":starnix_pid_diagnostics_test" ]
  }
}

rustc_binary("starnix_pid_test_bin") {
  testonly = true
  edition = "2021"
  sources = [ "src/main.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.tracing.controller:fuchsia.tracing.controller_rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/performance/lib/fxt/rust",
    "//src/starnix/tests/diagnostics/lib/parse_starnix_inspect",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

_bin_name = "linux_kmsg_hello_world"
if (is_linux) {
  executable(_bin_name) {
    testonly = true
    sources = [ "src/kmsg_hello_world.cc" ]

    # TODO(https://fxbug.dev/42067974) enable on asan builders
    exclude_toolchain_tags = [ "instrumented" ]
  }
}

_hello_world_bin_label = ":${_bin_name}($target_linux_toolchain)"
_hello_world_out_dir = get_label_info(_hello_world_bin_label, "root_out_dir")
resource("linux_kmsg_hello_world_resource") {
  testonly = true
  sources = [ "${_hello_world_out_dir}/${_bin_name}" ]
  outputs = [ "data/tests/${_bin_name}" ]
  deps = [ _hello_world_bin_label ]
}

fuchsia_component("kmsg_hello_world") {
  testonly = true
  manifest = "meta/kmsg_hello_world.cml"
  deps = [ ":linux_kmsg_hello_world_resource" ]
}

fuchsia_test_component("integration_test") {
  manifest = "meta/integration_test.cml"
  deps = [ ":starnix_pid_test_bin" ]
  test_type = "starnix"
}

fuchsia_test_package("starnix_pid_diagnostics_test") {
  test_components = [ ":integration_test" ]
  deps = [ ":kmsg_hello_world" ]
  subpackages = [
    "//src/starnix/kernel:starnix_kernel_package",
    "//src/starnix/containers/debian:debian_package",
    "//src/performance/trace_manager:package_hermetic",
  ]
}
