# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")
import("//build/testing/host_test_data.gni")

policy = {
  name = "selinux_policy"
  edition = "2021"
  deps = [
    "//src/starnix/lib/selinux/common",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:bstr",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:zerocopy",
  ]
  sources = [
    "src/arrays.rs",
    "src/error.rs",
    "src/extensible_bitmap.rs",
    "src/index.rs",
    "src/lib.rs",
    "src/metadata.rs",
    "src/parsed_policy.rs",
    "src/parser.rs",
    "src/security_context.rs",
    "src/symbols.rs",
  ]
  inputs = [ "//src/starnix/lib/selinux/testdata/policies/emulator" ]
}

additional_policy_configs = [ "//src/starnix/config:starnix_clippy_lints" ]

rustc_library("policy") {
  with_unit_tests = true
  configs += additional_policy_configs
  forward_variables_from(policy, "*")
}

fuchsia_unittest_package("selinux_policy-tests") {
  deps = [ ":policy_test" ]
}

if (is_host) {
  rustc_library("policy_for_host_tests") {
    testonly = true
    with_unit_tests = false
    output_dir = "$target_out_dir/host_test_lib"
    features = [ "selinux_policy_test_api" ]
    configs += additional_policy_configs
    forward_variables_from(policy, "*")
  }

  testdata_policies = [
    "emulator",
    "selinux_testsuite",
  ]

  policy_files = []
  expectation_files = []
  foreach(file, testdata_policies) {
    policy_files += [ "../testdata/policies/$file" ]
    expectation_files += [ "../testdata/expectations/$file" ]
  }

  host_test_data("testdata_policies") {
    sources = policy_files
    outputs = [ "$target_out_dir/testdata/policies/{{source_file_part}}" ]
  }

  host_test_data("testdata_expectations") {
    sources = expectation_files
    outputs = [ "$target_out_dir/testdata/expectations/{{source_file_part}}" ]
  }

  testdata_micro_policies = [
    "allow_a_attr_b_attr_class0_perm0",
    "allow_a_t_a1_attr_class0_perm0_a2_attr_class0_perm1",
    "allow_a_t_b_attr_class0_perm0",
    "allow_a_t_b_t_class0_perm0",
    "minimal",
    "multiple_levels_and_categories",
    "no_allow_a_attr_b_attr_class0_perm0",
    "no_allow_a_t_b_attr_class0_perm0",
    "no_allow_a_t_b_t_class0_perm0",
  ]

  micro_policy_files = []
  foreach(file, testdata_micro_policies) {
    micro_policy_files += [ "../testdata/micro_policies/${file}_policy.pp" ]
  }

  host_test_data("testdata_micro_policies") {
    sources = micro_policy_files
    outputs = [ "$target_out_dir/testdata/micro_policies/{{source_file_part}}" ]
  }

  testdata_composite_policies = [
    "class_defaults",
    "minimal",
    "range_transition",
    "role_transition_not_allowed",
    "role_transition",
    "type_transition",
  ]

  composite_policy_files = []
  foreach(file, testdata_composite_policies) {
    composite_policy_files +=
        [ "../testdata/composite_policies/compiled/${file}_policy.pp" ]
  }

  host_test_data("testdata_composite_policies") {
    sources = composite_policy_files
    outputs = [ "$target_out_dir/testdata/composite_policies/compiled/{{source_file_part}}" ]
  }

  config("host_test_config") {
    testdata_dir = "$target_out_dir/testdata"
    rustenv = [ "TESTDATA_DIR=" + rebase_path(testdata_dir, root_build_dir) ]
  }

  rustc_test("selinux_policy_host_tests") {
    edition = "2021"
    source_root = "host_tests/lib.rs"
    sources = [ "host_tests/lib.rs" ]
    deps = [
      ":policy_for_host_tests",
      ":testdata_composite_policies",
      ":testdata_expectations",
      ":testdata_micro_policies",
      ":testdata_policies",
      "//src/starnix/lib/selinux/common",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:serde",
      "//third_party/rust_crates:serde_json5",
    ]
    configs += [ ":host_test_config" ]
    features = [ "selinux_policy_test_api" ]
  }
}

group("host_tests") {
  testonly = true
  deps = [ ":selinux_policy_host_tests($host_toolchain)" ]
}

group("tests") {
  testonly = true
  deps = [
    ":host_tests",
    ":selinux_policy-tests",
  ]
}
