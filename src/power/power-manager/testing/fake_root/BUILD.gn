# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/drivers.gni")
import("//build/rust/rustc_test.gni")

fidl("fuchsia.powermanager.root.test") {
  sources = [ "test.fidl" ]
}

driver_bind_rules("root_bind") {
  disable_autobind = true
  bind_output = "root.bindbc"
}

fuchsia_driver("root_driver") {
  output_name = "power_manager_driver_root"
  sources = [ "root_driver.cc" ]
  deps = [
    ":fuchsia.powermanager.root.test_cpp_wire",
    ":root_bind",
    "../fuchsia.powermanager.driver.bind:powermanager_bindlib_cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_driver_component("root") {
  info = "root-info.json"
  manifest = "meta/root.cml"
  deps = [ ":root_driver" ]
}

group("fake_root") {
  deps = [ ":root" ]
}

rustc_test("bin") {
  edition = "2021"
  testonly = true
  output_name = "power_manager_driver_root_bin"
  source_root = "test.rs"
  sources = [ "test.rs" ]
  deps = [
    ":fuchsia.powermanager.root.test_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/lib/device-watcher/rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_unittest_package("fake_root_test") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  deps = [
    ":bin",
    ":root",
    "//src/devices/bin/driver_host2:component",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":fake_root_test" ]
}
