# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/rust/rustc_test.gni")

rustc_library("lib") {
  testonly = true
  name = "power_manager_integration_test_lib"
  edition = "2021"
  with_unit_tests = true

  deps = [
    ":integration-component",
    "../../testing/fuchsia.powermanager.driver.fidl:fuchsia.powermanager.driver.temperaturecontrol_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_rust",
    "//sdk/fidl/fuchsia.power.clientlevel:fuchsia.power.clientlevel_rust",
    "//sdk/fidl/fuchsia.power.profile:fuchsia.power.profile_rust",
    "//sdk/fidl/fuchsia.power.systemmode:fuchsia.power.systemmode_rust",
    "//sdk/fidl/fuchsia.settings:fuchsia.settings_rust",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2_rust",
    "//sdk/fidl/fuchsia.thermal:fuchsia.thermal_rust",
    "//sdk/fidl/fuchsia.ui.activity:fuchsia.ui.activity_rust",
    "//sdk/lib/device-watcher/rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/cobalt/bin/testing/mock_cobalt:component_v2",
    "//src/lib/fake-clock/fidl:fidl_rust",
    "//src/lib/fake-clock/svc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//src/lib/fuchsia",
    "//third_party/rust_crates:assert_matches",
  ]

  sources = [
    "src/client_connectors/mod.rs",
    "src/client_connectors/power_profile_client.rs",
    "src/client_connectors/reboot_watcher_client.rs",
    "src/client_connectors/system_power_mode_client.rs",
    "src/client_connectors/thermal_client.rs",
    "src/lib.rs",
    "src/mocks/activity_service.rs",
    "src/mocks/input_settings_service.rs",
    "src/mocks/kernel_service.rs",
    "src/mocks/mod.rs",
    "src/mocks/system_controller.rs",
  ]
}

fuchsia_component_manifest("integration-power-manager-manifest") {
  testonly = true
  manifest = "meta/power-manager-for-integration.cml"
  component_name = "power-manager"
}

fuchsia_component_manifest("integration-cpu-manager-manifest") {
  testonly = true
  manifest = "meta/cpu-manager-for-integration.cml"
  component_name = "cpu-manager"
}

fuchsia_structured_config_values(
    "integration-power-manager-structured-config") {
  testonly = true
  cm_label = ":integration-power-manager-manifest"
  values = {
    enable_debug_service = false
    node_config_path = ""
    disable_temperature_filter = true
  }
}

fuchsia_structured_config_values("integration-cpu-manager-structured-config") {
  testonly = true
  cm_label = ":integration-cpu-manager-manifest"
  values = {
    node_config_path = ""
  }
}

fuchsia_component("integration-component") {
  cm_label = ":integration-power-manager-manifest"
  testonly = true
  deps = [
    ":integration-cpu-manager-manifest",
    ":integration-cpu-manager-structured-config",
    ":integration-power-manager-manifest",
    ":integration-power-manager-structured-config",
    "//src/power/cpu-manager:bin_with_fake_time",
    "//src/power/power-manager:bin_with_fake_time",
  ]
}

fuchsia_unittest_package("power-manager-integration-lib-test") {
  deps = [ ":lib_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":power-manager-integration-lib-test" ]
}
