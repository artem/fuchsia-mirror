# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

template("paver_template") {
  not_needed(invoker, [ "*" ])
  _target_name = target_name

  executable("paver-${target_name}") {
    defines = [ "${_target_name}" ]
    sources = [ "paver.cc" ]
    deps = [
      "//sdk/fidl/fuchsia.paver:fuchsia.paver_cpp",
      "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_cpp",
      "//sdk/lib/component/outgoing/cpp",
      "//sdk/lib/fdio",
      "//src/storage/lib/paver:${_target_name}",
      "//src/sys/lib/stdout-to-debuglog/cpp",
      "//zircon/system/ulib/async-loop:async-loop-cpp",
      "//zircon/system/ulib/async-loop:async-loop-default",
    ]
  }

  fuchsia_component("component-${target_name}") {
    component_name = "paver"
    manifest = "meta/paver-${_target_name}.cml"
    deps = [ ":paver-${_target_name}" ]
  }

  fuchsia_package("package-${target_name}") {
    package_name = "paver"
    deps = [ ":component-${_target_name}" ]
  }
}

paver_template("astro") {
}
paver_template("luis") {
}
paver_template("nelson") {
}
paver_template("sherlock") {
}
paver_template("vim3") {
}
paver_template("violet") {
}
paver_template("x64") {
}

# Legacy targets to be deleted.
executable("paver") {
  defines = [ "LEGACY_PAVER" ]
  sources = [ "paver.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fdio",
    "//src/storage/lib/paver",
    "//src/sys/lib/stdout-to-debuglog/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("component") {
  component_name = "paver"
  manifest = "meta/paver-legacy.cml"
  deps = [ ":paver" ]
}

fuchsia_package("package") {
  package_name = "paver"
  deps = [ ":component" ]
}
