# Copyright 2024 The Fuchsia Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_test.gni")
import("//build/components/fuchsia_test_component.gni")
import("//build/components/fuchsia_test_package.gni")
import("//build/rust/rustc_test.gni")
import("//src/tee/manager/ta_manager_config.gni")

# This sets up a hermetic test package containing a test TA manager along with a test configuration
# and subpackages containing TAs or other test components.
#
# Parameters:
#
#    config_sources
#    - Required: list of paths to config files
#
#    test_sources
#    - Required: list of source files for the test target
#
#    subpackages
#    - Optional: list of subpackages to include in the test package

template("ta_manager_config_test") {
  rustc_test_target = "${target_name}-rustc-test"
  test_component_target = "${target_name}-test"
  test_component_name = target_name
  test_component_manifest = "meta/${target_name}.cml"
  test_config_target = "${target_name}-config"
  test_root_target = "${target_name}-root"
  test_root_manifest = "meta/${target_name}-root.cml"
  test_package_target = "${target_name}-package"

  rustc_test(rustc_test_target) {
    edition = "2021"

    source_root = invoker.test_source_root
    sources = [ source_root ]
    if (defined(invoker.test_sources)) {
      sources += invoker.test_sources
    }
    configs -= [ "//build/config/rust/lints:allow_unused_results" ]
    deps = [
      "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
      "//src/lib/fuchsia",
      "//src/lib/fuchsia-component",
      "//src/lib/fuchsia-fs",
      "//third_party/rust_crates:anyhow",
    ]
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
  }

  fuchsia_test_component(test_component_target) {
    component_name = test_component_name
    manifest = test_component_manifest
    deps = [ ":${rustc_test_target}" ]
  }

  ta_manager_config(test_config_target) {
    base_name = "config"
    config_sources = invoker.config_sources
  }

  fuchsia_test_component(test_root_target) {
    manifest = test_root_manifest
  }

  fuchsia_package(test_package_target) {
    testonly = true
    deps = [
      ":${test_component_target}",
      ":${test_root_target}",
    ]
    subpackages = [
      "//src/tee/manager:ta-manager-realm-test",
      ":${test_config_target}",
    ]
    if (defined(invoker.subpackages)) {
      subpackages += invoker.subpackages
    }
  }

  fuchsia_test(target_name) {
    package = ":${test_package_target}"
    component = ":${test_root_target}"
  }
}
