# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

# Defines storage conformance test components and package for a given filesystem harness binary.
#
# Parameters
#
#   harness_manifest (required)
#     The component manifest of the filesystem's harness
#     Type: path
#
#   harness_binary (required)
#     The filesystem's hanress binary.
#     Type: target
template("storage_conformance_test") {
  test_name = "${target_name}_storage_conformance_test"

  harness_component_target_name =
      "${target_name}_storage_conformance_hanress_component"
  fuchsia_component(harness_component_target_name) {
    component_name = "harness"
    manifest = invoker.harness_manifest
    deps = [ invoker.harness_binary ]
    testonly = true
  }

  test_component_target_name = "${test_name}_component"
  fuchsia_component(test_component_target_name) {
    component_name = "${test_name}"
    manifest = "//src/storage/conformance/meta/conformance_test.cml"
    deps = [ "//src/storage/conformance:conformance_test" ]
    testonly = true
  }

  fuchsia_test_package(target_name) {
    package_name = "${test_name}"
    test_components = [ ":${test_component_target_name}" ]
    deps = [ ":${harness_component_target_name}" ]
  }
}
