# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//src/developer/ffx/build/ffx_plugin.gni")

rustc_binary("blackout-fxfs-tree-bin") {
  output_name = "blackout_fxfs_tree_bin"
  with_unit_tests = true
  testonly = true
  edition = "2021"
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_rust",
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/storage/blackout/target/lib:blackout-target",
    "//src/storage/fxfs/fidl/fuchsia.fxfs:fuchsia.fxfs_rust",
    "//src/storage/lib/fs_management/rust:fs_management",
    "//src/storage/testing/rust:storage-isolated-driver-manager",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:tracing",
  ]
  data_deps = [
    "//src/storage/fxfs",
    "//src/storage/fxfs-crypt",
  ]
  sources = [ "src/main.rs" ]
  rustenv = [ "DATA_FILESYSTEM_FORMAT=fxfs" ]
}

fuchsia_component("blackout-fxfs-tree-component") {
  testonly = true
  deps = [
    ":blackout-fxfs-tree-bin",
    "//src/storage/fs_test:test_crypt_service",
    "//src/storage/fxfs:fxfs_component",
    "//src/storage/fxfs:test_fxfs_component_manifest_shard",
  ]
  manifest = "meta/blackout_fxfs_tree.cml"
}

fuchsia_package("blackout-fxfs-tree") {
  testonly = true
  deps = [ ":blackout-fxfs-tree-component" ]
}
