# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  testonly = true
  name = "pkgdir"
  visibility = [ ":*" ]
  edition = "2021"
  sources = [ "src/main.rs" ]
  deps = [
    ":config_lib",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/storage/benchmarks/fuchsia/pkgdir-component/fidl/test.pkgdir:test.pkgdir_rust",
    "//src/sys/pkg/lib/blobfs",
    "//src/sys/pkg/lib/fuchsia-hash",
    "//src/sys/pkg/lib/package-directory",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_component_manifest("manifest") {
  component_name = "pkgdir-component"
  manifest = "meta/pkgdir_component.cml"
}

fuchsia_structured_config_rust_lib("config_lib") {
  cm_label = ":manifest"
  name = "pkgdir_config"
  visibility = [ ":*" ]
}

fuchsia_component("component") {
  testonly = true
  visibility = [ ":*" ]
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_structured_config_values("config_values_from_gn") {
  cm_label = ":manifest"
  values = {
    use_fxblob = false
  }
  visibility = [ ":*" ]
}

group("production-component-and-config") {
  testonly = true
  public_deps = [
    ":component",
    ":config_values_from_gn",
  ]
}
