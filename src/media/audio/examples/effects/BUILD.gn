# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("effects") {
  testonly = true
  deps = [ ":audio_effects_example" ]
}

group("tests") {
  testonly = true
  deps = [ ":audio_effects_example_tests" ]
}

source_set("headers") {
  testonly = true
  sources = [
    "delay_effect.h",
    "effect_base.h",
    "rechannel_effect.h",
    "swap_effect.h",
  ]
}

loadable_module("audio_effects_example") {
  testonly = true

  sources = [
    "delay_effect.cc",
    "effect_base.cc",
    "lib_audio_effects.cc",
  ]

  deps = [
    ":headers",
    "//sdk/lib/media/audio/effects",
    "//third_party/rapidjson",
    "//zircon/system/ulib/fbl",
  ]
}

executable("test_bin") {
  testonly = true
  output_name = "audio_effects_example_tests"

  sources = [ "test/audio_effects_example_tests.cc" ]

  deps = [
    ":headers",
    "//sdk/lib/media/audio/effects",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//src/media/audio/lib/effects_loader",
    "//src/zircon/lib/zircon",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fbl",
  ]

  # TODO(https://fxbug.dev/135694): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-vla-cxx-extension" ]
}

fuchsia_unittest_package("audio_effects_example_tests") {
  deps = [
    ":audio_effects_example",
    ":test_bin",
  ]
}
