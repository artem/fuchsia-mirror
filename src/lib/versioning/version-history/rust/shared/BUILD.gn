# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_embed_files.gni")
import("//build/rust/rustc_library.gni")

rustc_library("version-history-shared") {
  edition = "2021"
  with_unit_tests = true

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]

  deps = [
    ":version-history-data",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:itertools",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
  ]

  sources = [ "src/lib.rs" ]
  visibility = [ "//src/lib/versioning/version-history/rust/*" ]
}

group("tests") {
  testonly = true
  deps = [ ":version-history-shared_test($host_toolchain)" ]
}

rustc_embed_files("version-history-data") {
  edition = "2021"

  _version_history_label = "//sdk:version_history($default_toolchain)"
  _version_history_dir =
      get_label_info(_version_history_label, "target_out_dir")

  files = [
    {
      constant_name = "VERSION_HISTORY"
      source = "${_version_history_dir}/version_history.json"
      deps = [ _version_history_label ]
    },
  ]
}
