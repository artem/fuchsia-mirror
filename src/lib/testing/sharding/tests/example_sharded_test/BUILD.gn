# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_test.gni")
import("//src/lib/testing/sharding/fuchsia_sharded_test_package.gni")

group("tests") {
  testonly = true
  deps = [ ":example-sharded-test" ]
}

rustc_test("underlying_test_to_be_sharded") {
  edition = "2021"
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
  test_deps = [ "//third_party/rust_crates:paste" ]
  sources = [ "src/lib.rs" ]
}

rustc_test("underlying_test_to_be_sharded_with_failures") {
  edition = "2021"
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
  test_deps = [ "//third_party/rust_crates:paste" ]
  sources = [ "src/lib.rs" ]

  rustflags = [ "--cfg=make_case_f_fail" ]
}

fuchsia_sharded_test_package("example-sharded-test") {
  test_components = [
    {
      name = "example-test"
      manifest = "meta/example-test.cml"
      deps = [ ":underlying_test_to_be_sharded" ]
      num_shards = 3
    },
    {
      name = "example-test-with-expectations"
      manifest = "meta/example-test-with-expectations.cml"
      deps = [ ":underlying_test_to_be_sharded_with_failures" ]
      num_shards = 3
      expectations = "example_expectations.json5"
    },
    {
      name = "example-test-sharded-by-section"
      manifest = "meta/example-test.cml"
      deps = [ ":underlying_test_to_be_sharded" ]
      num_shards = 3
      shard_part_regex = "section_(\d+)::case_.+"
    },
  ]
}
