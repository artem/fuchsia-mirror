# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true
  deps = [ ":sharding-integration-tests" ]
}

rustc_test("sharding_integration_test") {
  edition = "2021"
  testonly = true
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
  sources = [ "src/lib.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager_rust",
    "//src/lib/fuchsia-async",
    "//src/sys/test_manager:test_lib",
    "//src/sys/test_runners:test_lib",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:pretty_assertions",
    "//third_party/rust_crates:regex",
  ]
}

fuchsia_test_component("sharding-integration-test") {
  manifest = "meta/sharding-integration-test.cml"
  deps = [ ":sharding_integration_test" ]

  # Unfortunately, integration tests that make use of test_manager cannot be
  # hermetic, as test_manager's use of fuchsia.component.resolution.Resolver
  # precludes loading tests from subpackages rather than from absolute URLs.
  # TODO(http://b/301507292): Stop requiring use of the system resolver in
  # test manager tests.
  test_type = "test_arch"
}

fuchsia_test_package("sharding-integration-tests") {
  test_components = [ ":sharding-integration-test" ]
  subpackages = [ "//src/sys/test_manager:pkg" ]

  # Note that this test non-hermetically loads the example-sharded-test package
  # from universe via the subpackaged test manager.
  # TODO(http://b/301507292): Stop requiring use of the system resolver in
  # test manager tests.
  deps = [ "//src/lib/testing/sharding/tests/example_sharded_test:example-sharded-test" ]
}
