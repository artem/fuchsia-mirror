# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

driver_deps = [
  "//sdk/fidl/fuchsia.boot:fuchsia.boot_rust",
  "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
  "//sdk/fidl/fuchsia.component.resolution:fuchsia.component.resolution_rust",
  "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
  "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
  "//sdk/fidl/fuchsia.pkg:fuchsia.pkg_rust",
  "//src/lib/fidl/rust/fidl",
  "//src/lib/fuchsia",
  "//src/lib/fuchsia-component-test",
  "//src/lib/fuchsia-fs",
  "//src/lib/storage/vfs/rust:vfs",
  "//src/lib/versioning/version-history/rust:version-history",
  "//src/storage/fxfs/fidl/fuchsia.fxfs:fuchsia.fxfs_rust",
  "//src/sys/lib/mem_util",
  "//src/sys/pkg/lib/fuchsia-hash",
  "//src/sys/pkg/lib/fuchsia-pkg",
  "//src/sys/pkg/lib/fuchsia-pkg-testing",
  "//src/sys/pkg/lib/system-image",
  "//src/sys/pkg/testing/blobfs-ramdisk",
  "//third_party/rust_crates:assert_matches",
  "//third_party/rust_crates:futures",
]

rustc_test("driver") {
  name = "base_resolver_integration_test"
  edition = "2021"
  source_root = "src/lib.rs"
  sources = [ "src/lib.rs" ]
  deps = driver_deps
}

rustc_test("driver_fxblob") {
  name = "base_resolver_integration_fxblob_test"
  edition = "2021"
  source_root = "src/lib.rs"
  sources = [ "src/lib.rs" ]
  deps = driver_deps
  deps += [ "//src/storage/fxfs:fxfs_component" ]

  features = [ "use_fxblob" ]
}

fuchsia_component("component") {
  testonly = true
  manifest = "meta/base-resolver-integration-test.cml"
  deps = [
    ":driver",
    "//src/storage/testing:storage_driver_test_realm",
    "//src/sys/base-resolver:production-component-and-config",
    "//src/sys/pkg/bin/pm:pm_bin",
  ]
}

fuchsia_component("component_fxblob") {
  testonly = true
  manifest = "meta/base-resolver-integration-fxblob-test.cml"
  deps = [
    ":driver_fxblob",
    "//src/storage/testing:storage_driver_test_realm",
    "//src/sys/base-resolver:production-component-and-config",
    "//src/sys/pkg/bin/pm:pm_bin",
  ]
}

fuchsia_test_package("base-resolver-integration-tests") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  test_components = [ ":component" ]
}

fuchsia_test_package("base-resolver-integration-fxblob-tests") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  test_components = [ ":component_fxblob" ]
}

group("tests") {
  testonly = true
  public_deps = [
    ":base-resolver-integration-fxblob-tests",
    ":base-resolver-integration-tests",
  ]
}
