# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/bootfs_files_for_assembly.gni")
import("//build/components.gni")
import("//build/dist/component_manifest_resources.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")
import("//src/storage/fshost/generated_fshost_config.gni")

group("base-resolver") {
  testonly = true
  deps = [ ":tests" ]
}

group("tests") {
  testonly = true
  deps = [
    ":base-resolver-tests",
    "tests",
  ]
}

rustc_binary("bin") {
  edition = "2021"
  output_name = "base_resolver"
  with_unit_tests = true

  configs += [ "//build/config/rust:bootfs" ]

  deps = [
    ":config_lib",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_rust",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//sdk/fidl/fuchsia.component.resolution:fuchsia.component.resolution_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg_rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-url",
    "//src/lib/versioning/version-history/rust:version-history",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/fidl-fuchsia-component-abi-ext",
    "//src/sys/lib/mem_util",
    "//src/sys/pkg/lib/blobfs",
    "//src/sys/pkg/lib/fuchsia-hash",
    "//src/sys/pkg/lib/package-directory",
    "//src/sys/pkg/lib/system-image",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:hmac",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:sha2",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//src/sys/pkg/lib/fuchsia-pkg",
    "//src/sys/pkg/lib/fuchsia-pkg-testing",
    "//src/sys/pkg/testing/blobfs-ramdisk",
    "//third_party/rust_crates:assert_matches",
  ]

  sources = [
    "src/component.rs",
    "src/context_authenticator.rs",
    "src/main.rs",
    "src/package.rs",
  ]
}

component_manifest_resources("root-manifest") {
  sources = [ "meta/base-resolver.cml" ]
}

fuchsia_component_manifest("manifest") {
  component_name = "base-resolver"
  manifest = "meta/base-resolver.cml"
}

fuchsia_structured_config_values("config_values_from_gn") {
  cm_label = ":manifest"
  values = {
    use_fxblob = fxfs_blob
  }
  visibility = [ ":*" ]
}

fuchsia_structured_config_values("config_values_from_gn-fxblob") {
  cm_label = ":manifest"
  values = {
    use_fxblob = true
  }
  visibility = [ ":*" ]
}

group("component-and-config-fxblob") {
  public_deps = [
    ":base-resolver-component",
    ":config_values_from_gn-fxblob",
  ]
}

fuchsia_structured_config_values("config_values_from_gn-cpp-blobfs") {
  cm_label = ":manifest"
  values = {
    use_fxblob = false
  }
  visibility = [ ":*" ]
}

group("component-and-config-cpp-blobfs") {
  public_deps = [
    ":base-resolver-component",
    ":config_values_from_gn-cpp-blobfs",
  ]
}

fuchsia_structured_config_rust_lib("config_lib") {
  cm_label = ":manifest"
  name = "base_resolver_config"
  visibility = [ ":*" ]
}

fuchsia_component("base-resolver-component") {
  component_name = "base-resolver"
  manifest = "meta/base-resolver.cml"
  deps = [ ":bin" ]
}

group("production-component-and-config") {
  public_deps = [
    ":base-resolver-component",
    ":config_values_from_gn",
  ]
}

fuchsia_unittest_package("base-resolver-tests") {
  manifest = "meta/base-resolver-unittests.cml"
  deps = [
    ":bin_test",
    "//src/storage/testing:storage_driver_test_realm",
    "//src/sys/pkg/bin/pm:pm_bin",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

bootfs_files_for_assembly("bootfs") {
  deps = [
    ":bin",
    ":config_values_from_gn",
    ":root-manifest",
  ]
}
