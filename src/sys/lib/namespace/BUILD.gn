# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

group("tests") {
  testonly = true
  deps = [ ":namespace-unittests" ]
}

rustc_library("namespace") {
  edition = "2021"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/flyweights",
    "//third_party/rust_crates:memchr",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:thiserror",
  ]

  test_deps = [ "//third_party/rust_crates:assert_matches" ]

  if (is_fuchsia) {
    deps += [
      "//src/lib/process_builder",
      "//src/lib/zircon/rust:fuchsia-zircon",
    ]
    test_deps += [
      "//src/lib/fuchsia-async",
      "//src/lib/fuchsia-fs",
      "//src/storage/lib/vfs/rust:vfs",
    ]
  }

  sources = [
    "src/lib.rs",
    "src/path.rs",
    "src/tree.rs",
  ]
}

fuchsia_unittest_package("namespace-unittests") {
  deps = [ ":namespace_test" ]
}
