# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

executable("bin") {
  testonly = true
  output_name = "structured_config_cpp_elf"
  sources = [ "test.cc" ]
  deps = [
    ":config_lib",
    "//sdk/lib/inspect/component/cpp",
    "//src/lib/fxl/test:gtest_main",
  ]
}

fuchsia_component_manifest("manifest") {
  testonly = true
  component_name = "cpp_elf_receiver"
  manifest = "meta/test.cml"
}

fuchsia_structured_config_cpp_elf_lib("config_lib") {
  testonly = true
  namespace = "receiver_config"
  cm_label = ":manifest"
}

fuchsia_structured_config_values("values") {
  testonly = true
  cm_label = ":manifest"
  values_source = "../config/values.json5"
}

fuchsia_component("test_component") {
  testonly = true
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_test_package("cpp_elf") {
  package_name = "sc_cpp_elf_client_integration_test"
  test_components = [ ":test_component" ]
  deps = [
    ":values",

    # https://fxbug.dev/42052321: Dep included for affected tests presubmit
    "//src/sys/component_manager:component-manager-realm-builder-cmp",
  ]
}
