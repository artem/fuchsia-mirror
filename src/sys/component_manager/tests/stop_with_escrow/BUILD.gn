# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true

  deps = [ ":stop_with_escrow_integration_test" ]
}

rustc_test("stop_with_escrow_integration_test_bin") {
  name = "stop_with_escrow_integration_test"
  edition = "2021"
  source_root = "stop_with_escrow_integration_test.rs"
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.process:fuchsia.process_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component-test",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/component_manager/tests/fidl:components_rust",
  ]

  sources = [ "stop_with_escrow_integration_test.rs" ]
}

rustc_binary("stop_with_pending_request_bin") {
  name = "stop_with_pending_request"
  edition = "2021"
  source_root = "stop_with_pending_request.rs"
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/component_manager/tests/fidl:components_rust",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "stop_with_pending_request.rs" ]
}

fuchsia_component("stop_with_pending_request") {
  testonly = true
  manifest = "meta/stop_with_pending_request.cml"
  deps = [ ":stop_with_pending_request_bin" ]
}

rustc_binary("stop_with_delivery_on_readable_request_bin") {
  name = "stop_with_delivery_on_readable_request"
  edition = "2021"
  source_root = "stop_with_delivery_on_readable_request.rs"
  deps = [
    "//src/lib/detect-stall",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/sys/component_manager/tests/fidl:components_rust",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "stop_with_delivery_on_readable_request.rs" ]
}

fuchsia_component("stop_with_delivery_on_readable_request") {
  testonly = true
  manifest = "meta/stop_with_delivery_on_readable_request.cml"
  deps = [ ":stop_with_delivery_on_readable_request_bin" ]
  restricted_features = [ "delivery_type" ]
}

fuchsia_component("test_root") {
  testonly = true
  manifest = "meta/test_root.cml"
}

fuchsia_unittest_package("stop_with_escrow_integration_test") {
  manifest = "meta/stop_with_escrow_integration_test.cml"
  deps = [
    ":stop_with_delivery_on_readable_request",
    ":stop_with_escrow_integration_test_bin",
    ":stop_with_pending_request",
    ":test_root",
    "//src/sys/component_manager:component-manager-realm-builder-cmp",
  ]
}
