# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_fuchsia, "These can only be defined in the default toolchain")

# WARNING: Known to infrastructure
group("minimal") {
  testonly = true

  # The buildbot groups are not allowed to add platform features, only tests
  # and tools.
  assert_no_deps = [ "//bundles/platform/*" ]

  deps = [
    ":additional_build_targets",

    # TODO(fxbug.dev/131001): Define the set of tests that should run on minimal.
    "//examples:tests",
    "//sdk:tests",
    "//src/connectivity/network:tests",

    # TODO(fxbug.dev/132473): Uncomment once all tests pass on minimal.x64.
    # "//src/developer:tests",
    "//src/fonts:tests",
    "//src/hwinfo:tests",
    "//src/intl:tests",
    "//src/lib:tests",
    "//src/power:tests",
  ]

  if (current_cpu == "riscv64") {
    deps += [
      # Run a subset of the tests until fxbug.dev/122609 is addressed and all
      # network tests can be built and run. See below.
      "//src/connectivity/network/tests:tests",
    ]
    deps -= [
      # TODO(fxbug.dev/122609): Remove after removing Go dependencies on RISC-V.
      "//src/connectivity/network:tests",

      # TODO(fxbug.dev/130998): Enable once all dependencies succeed on RISC-V.
      # TODO(fxbug.dev/132473): Uncomment when adding to the default targets.
      # "//src/developer:tests",

      # TODO(fxbug.dev/132426): Remove once the tests in the bug pass on RISC-V.
      "//src/lib:tests",
    ]
  }
}

# Contains additional targets to be built that are not needed for tests.
# This allows bots to ensure buildable targets do not regress even if they are
# not being run by another bot (i.e., during bringup of a new CPU architecture).
group("additional_build_targets") {
  testonly = true
  deps = [ "//examples" ]
}
