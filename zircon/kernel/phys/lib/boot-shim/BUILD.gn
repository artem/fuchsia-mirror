# Copyright 2021 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/zircon/migrated_targets.gni")

zx_library("boot-shim") {
  sdk = "source"
  sdk_headers = [
    "lib/boot-shim/devicetree.h",
    "lib/boot-shim/acpi.h",
    "lib/boot-shim/boot-shim.h",
    "lib/boot-shim/devicetree-boot-shim.h",
    "lib/boot-shim/efi.h",
    "lib/boot-shim/debugdata.h",
    "lib/boot-shim/efi-boot-shim.h",
    "lib/boot-shim/item-base.h",
    "lib/boot-shim/pool-mem-config.h",
    "lib/boot-shim/test-serial-number.h",
    "lib/boot-shim/uart.h",
  ]

  public_deps = [
    # <lib/boot-shim/item-base.h> has #include <lib/stdcompat/span.h>.
    "//sdk/lib/stdcompat",

    # <lib/boot-shim/acpi.h> has #include <lib/zbi-format/driver-config.h>.
    # <lib/boot-shim/item-base.h> has #include <lib/zbi-format/zbi.h>.
    "//sdk/lib/zbi-format",

    # <lib/boot-shim/uart.h> has #include <lib/uart/all.h>.
    "//zircon/system/ulib/uart",

    # <lib/boot-shim/item-base.h> has #include <lib/zbitl/image.h>.
    "//src/lib/zbitl",

    # <lib/boot-shim/boot-shim.h> has #include <lib/elfldltl/note.h>.
    "//src/lib/elfldltl",

    # <lib/boot-shim/item-base.h> has #include <lib/fit/result.h>
    "//zircon/system/ulib/zx",

    # <lib/boot-shim/efi.h> #include <efi/system-table.h>
    "//zircon/kernel/lib/efi",

    # <lib/boot-shim/devicetree-boot-shim> has include <lib/devictree/matcher.h>
    # <lib/devicetree/devicetree.h>
    "//zircon/kernel/lib/devicetree",

    # <lib/boot-shim/devicetree-boot-shim> has include <fbl/macros.h>
    "//zircon/system/ulib/fbl",

    # <lib/boot-shim/devicetree.h> exposes memalloc::Range.
    "//zircon/kernel/phys/lib/memalloc",
  ]

  sources = [
    "acpi.cc",
    "boot-shim.cc",
    "cmdline.cc",
    "debugdata.cc",
    "devicetree-arm-gic-item.cc",
    "devicetree-arm-psci-item.cc",
    "devicetree-arm-timer-item.cc",
    "devicetree-chosen-matcher.cc",
    "devicetree-cpu-topology-item.cc",
    "devicetree-irq-resolver.cc",
    "devicetree-memory-matcher.cc",
    "devicetree-riscv-plic-item.cc",
    "devicetree-riscv-timer-item.cc",
    "efi-boot-shim.cc",
    "efi.cc",
    "pool-mem-config.cc",
    "test-serial-number.cc",
  ]

  deps = [
    "//zircon/kernel/lib/acpi_lite",
    "//zircon/kernel/lib/boot-options",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/uart",
  ]
}

if (!is_kernel) {
  import("//build/components.gni")
  import("//build/test.gni")

  group("tests") {
    testonly = true
    deps = [
      ":boot-shim-devicetree-tests",
      ":boot-shim-devicetree-unittests($host_toolchain)",
      ":boot-shim-tests",
      ":boot-shim-unittests($host_toolchain)",
    ]
  }

  zx_library("boot-shim-test") {
    testonly = true

    sdk = "source"
    sdk_headers = [
      "lib/boot-shim/testing/test-helper.h",
      "lib/boot-shim/testing/devicetree-test-fixture.h",
    ]

    sources = [
      "devicetree-test-fixture.cc",
      "test-helper.cc",
    ]
    public_deps = [
      ":boot-shim",
      "//sdk/lib/stdcompat",
      "//sdk/lib/zbi-format",
      "//zircon/kernel/lib/devicetree/testing",
    ]
    deps = [
      "//src/lib/zbitl",
      "//zircon/system/ulib/zxtest",
    ]
  }

  test("boot-shim-devicetree-unittests") {
    defines = [ "UART_ALL_DRIVERS=1" ]
    sources = [
      "devicetree-arm-cpu-topology-item-test.cc",
      "devicetree-arm-gic-item-test.cc",
      "devicetree-arm-psci-item-test.cc",
      "devicetree-arm-timer-item-test.cc",
      "devicetree-boot-shim-tests.cc",
      "devicetree-chosen-matcher-test.cc",
      "devicetree-memory-matcher-test.cc",
      "devicetree-riscv-cpu-topology-item-test.cc",
      "devicetree-riscv-plic-item-test.cc",
      "devicetree-riscv-timer-item-test.cc",
    ]
    deps = [
      ":boot-shim",
      ":boot-shim-test",
      "//sdk/lib/fit",
      "//src/lib/zbitl",
      "//zircon/system/ulib/zxtest",
    ]
  }

  test("boot-shim-unittests") {
    defines = [ "UART_ALL_DRIVERS=1" ]
    sources = [
      "acpi-tests.cc",
      "boot-shim-tests.cc",
      "debugdata-tests.cc",
      "efi-boot-shim-tests.cc",
      "efi-tests.cc",
      "pool-mem-config-tests.cc",
      "test-serial-number-tests.cc",
      "uart-tests.cc",
    ]
    deps = [
      ":boot-shim",
      ":boot-shim-test",
      "//sdk/lib/zbi-format",
      "//zircon/kernel/lib/acpi_lite",
      "//zircon/kernel/lib/devicetree/testing",
      "//zircon/kernel/phys/lib/memalloc",
      "//zircon/system/ulib/zxtest",
    ]
  }

  fuchsia_unittest_package("boot-shim-tests") {
    deps = [ ":boot-shim-unittests" ]
  }

  fuchsia_unittest_package("boot-shim-devicetree-tests") {
    deps = [ ":boot-shim-devicetree-unittests" ]
  }
}
