# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//build/testing/bootfs_test.gni")

# To run this test, either
#   * add debugger-test-pkg to your universe and run `fx test` (preferred).
#   * add debugger-bootfs-test to dev_bootfs_labels and run `fx shell /boot/test/debugger-test`.
test("debugger-test") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "crash-and-recover.cc",
    "crash-and-recover.h",
    "debugger.cc",
    "debugger.h",
    "dyn_break_on_load.cc",
    "hw-breakpoint-test.cc",
    "inferior-control.cc",
    "inferior-control.h",
    "inferior.cc",
    "inferior.h",
    "main.cc",
    "reg-get-set-test.cc",
    "start-thread-reg-access.cc",
    "suspend-on-start.cc",
    "suspended-reg-access.cc",
    "utils.cc",
    "utils.h",
    "watchpoint-test.cc",
  ]

  deps = [
    "//sdk/lib/tbi",
    "//src/lib/debug:backtrace-request",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/runtime",
    "//zircon/system/ulib/test-utils",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]

  deps += [ "//zircon/system/utest/dlopen-indirect-deps:dlopen-indirect-deps-test-module" ]
}

bootfs_test("debugger-bootfs-test") {
  name = "debugger-test"
  deps = [ ":debugger-test" ]
}

fuchsia_unittest_package("debugger-test-pkg") {
  package_name = "debugger-test"
  manifest = "meta/debugger-test.cml"
  deps = [ ":debugger-test" ]
}

group("tests") {
  testonly = true
  deps = [ ":debugger-test-pkg" ]
}
