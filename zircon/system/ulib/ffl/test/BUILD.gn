# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":ffl($host_toolchain)",
    ":ffl-test-pkg",
  ]
}

test("ffl") {
  output_name = "ffl-test"
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "exponential_average_tests.cc",
    "ffl_tests.cc",
  ]
  deps = [
    "//zircon/system/ulib/ffl",
    "//zircon/system/ulib/zxtest",
  ]

  if (is_fuchsia) {
    deps += [ "//sdk/lib/fdio" ]
  }
}

fuchsia_unittest_package("ffl-test-pkg") {
  package_name = "ffl-test"
  deps = [ ":ffl" ]
}
